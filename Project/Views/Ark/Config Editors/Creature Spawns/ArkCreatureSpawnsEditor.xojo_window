#tag DesktopWindow
Begin ArkConfigEditor ArkCreatureSpawnsEditor
   AllowAutoDeactivate=   True
   AllowFocus      =   False
   AllowFocusRing  =   False
   AllowTabs       =   True
   Backdrop        =   0
   BackgroundColor =   &cFFFFFF00
   Composited      =   False
   DoubleBuffer    =   "False"
   Enabled         =   True
   EraseBackground =   "True"
   HasBackgroundColor=   False
   Height          =   548
   Index           =   -2147483648
   InitialParent   =   ""
   Left            =   0
   LockBottom      =   True
   LockLeft        =   True
   LockRight       =   True
   LockTop         =   True
   TabIndex        =   0
   TabPanelIndex   =   0
   TabStop         =   True
   Tooltip         =   ""
   Top             =   0
   Transparent     =   True
   Visible         =   True
   Width           =   980
   Begin BeaconListbox List
      AllowAutoDeactivate=   True
      AllowAutoHideScrollbars=   True
      AllowExpandableRows=   False
      AllowFocusRing  =   False
      AllowInfiniteScroll=   False
      AllowResizableColumns=   False
      AllowRowDragging=   False
      AllowRowReordering=   False
      Bold            =   False
      ColumnCount     =   1
      ColumnWidths    =   ""
      DefaultRowHeight=   26
      DefaultSortColumn=   0
      DefaultSortDirection=   0
      DropIndicatorVisible=   False
      EditCaption     =   "Edit"
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      GridLineStyle   =   0
      HasBorder       =   False
      HasHeader       =   False
      HasHorizontalScrollbar=   False
      HasVerticalScrollbar=   True
      HeadingIndex    =   -1
      Height          =   445
      Index           =   -2147483648
      InitialParent   =   ""
      InitialValue    =   ""
      Italic          =   False
      Left            =   0
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      PreferencesKey  =   ""
      RequiresSelection=   False
      RowSelectionType=   1
      Scope           =   2
      TabIndex        =   3
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   82
      Transparent     =   False
      TypeaheadColumn =   0
      Underline       =   False
      Visible         =   True
      VisibleRowCount =   0
      Width           =   250
      _ScrollOffset   =   0
      _ScrollWidth    =   -1
   End
   Begin StatusBar ListStatus
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      Borders         =   1
      Caption         =   ""
      ContentHeight   =   0
      Enabled         =   True
      Height          =   21
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   False
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   4
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   527
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
   Begin FadedSeparator MainSeparator
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   548
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   250
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   1
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   True
      Visible         =   True
      Width           =   1
   End
   Begin DesktopPagePanel Pages
      AllowAutoDeactivate=   True
      Enabled         =   True
      Height          =   548
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   251
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   True
      LockTop         =   True
      PanelCount      =   2
      Panels          =   ""
      Scope           =   2
      SelectedPanelIndex=   0
      TabIndex        =   2
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   False
      Value           =   0
      Visible         =   True
      Width           =   729
      Begin ArkSpawnPointEditor Editor
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   False
         AllowTabs       =   True
         Backdrop        =   0
         BackgroundColor =   &cFFFFFF00
         Composited      =   False
         Enabled         =   True
         HasBackgroundColor=   False
         Height          =   548
         Index           =   -2147483648
         InitialParent   =   "Pages"
         Left            =   251
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Modified        =   False
         Scope           =   2
         TabIndex        =   0
         TabPanelIndex   =   2
         TabStop         =   True
         Tooltip         =   ""
         Top             =   0
         Transparent     =   True
         Visible         =   True
         Width           =   729
      End
      Begin LogoFillCanvas NoSelectionFillCanvas
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   True
         AllowTabs       =   False
         Backdrop        =   0
         Caption         =   "No Selection"
         ContentHeight   =   0
         Enabled         =   True
         Height          =   527
         Index           =   -2147483648
         InitialParent   =   "Pages"
         Left            =   251
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Scope           =   2
         ScrollActive    =   False
         ScrollingEnabled=   False
         ScrollSpeed     =   20
         TabIndex        =   0
         TabPanelIndex   =   1
         TabStop         =   True
         Tooltip         =   ""
         Top             =   0
         Transparent     =   True
         Visible         =   True
         Width           =   729
      End
      Begin StatusBar NoSelectionStatusBar
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   True
         AllowTabs       =   False
         Backdrop        =   0
         Borders         =   1
         Caption         =   ""
         ContentHeight   =   0
         Enabled         =   True
         Height          =   21
         Index           =   -2147483648
         InitialParent   =   "Pages"
         Left            =   251
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   False
         Scope           =   2
         ScrollActive    =   False
         ScrollingEnabled=   False
         ScrollSpeed     =   20
         TabIndex        =   1
         TabPanelIndex   =   1
         TabStop         =   True
         Tooltip         =   ""
         Top             =   527
         Transparent     =   True
         Visible         =   True
         Width           =   729
      End
   End
   Begin OmniBar ConfigToolbar
      Alignment       =   0
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      BackgroundColor =   ""
      ContentHeight   =   0
      Enabled         =   True
      Height          =   41
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LeftPadding     =   -1
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      RightPadding    =   -1
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   5
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
   Begin DelayedSearchField FilterField
      Active          =   False
      AllowAutoDeactivate=   True
      AllowFocusRing  =   True
      AllowRecentItems=   False
      ClearMenuItemValue=   "Clear"
      DelayPeriod     =   250
      Enabled         =   True
      Height          =   22
      Hint            =   "Filter Spawns"
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   9
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      MaximumRecentItems=   -1
      PanelIndex      =   0
      RecentItemsValue=   "Recent Searches"
      Scope           =   2
      TabIndex        =   6
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   ""
      Tooltip         =   ""
      Top             =   50
      Transparent     =   False
      Visible         =   True
      Width           =   232
      _mIndex         =   0
      _mInitialParent =   ""
      _mName          =   ""
      _mPanelIndex    =   0
   End
   Begin OmniBarSeparator FilterSeparator
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   1
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   7
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   81
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
End
#tag EndDesktopWindow

#tag WindowCode
	#tag Event
		Sub Closing()
		  Var Simulator As ArkSpawnSimulatorWindow = Self.SimulatorWindow(False)
		  If (Simulator Is Nil) = False Then
		    Simulator.Close
		    Self.mSimulatorWindow = Nil
		  End If
		End Sub
	#tag EndEvent

	#tag Event
		Sub Opening()
		  Self.MinimumWidth = Self.ListMinWidth + Self.MainSeparator.Width + ArkSpawnPointEditor.MinEditorWidth
		  Self.MinimumHeight = 350
		End Sub
	#tag EndEvent

	#tag Event
		Function ParsingFinished(Project As Ark.Project) As Boolean
		  If Project Is Nil Or Project.HasConfigGroup(Ark.Configs.NameSpawnPoints) = False Then
		    Return True
		  End If
		  
		  Var ParsedConfig As Ark.Configs.SpawnPoints = Ark.Configs.SpawnPoints(Project.ConfigGroup(Ark.Configs.NameSpawnPoints))
		  If ParsedConfig = Nil Or ParsedConfig.Count = 0 Then
		    Self.ShowAlert("No spawn points to import", "The parsed ini content did not contain any spawn point data.")
		    Return True
		  End If
		  
		  Self.HandlePastedSpawnPoints(ParsedConfig.Points)
		  Return True
		End Function
	#tag EndEvent

	#tag Event
		Sub Resize(Initial As Boolean)
		  #Pragma Unused Initial
		  
		  Self.SetListWidth(Preferences.SpawnPointsSplitterPosition)
		  
		  Var Resizer As OmniBarItem = Self.ConfigToolbar.Item("Resizer")
		  If (Resizer Is Nil) = False Then
		    Resizer.Enabled = Self.Width > Self.MinEditorWidth
		  End If
		End Sub
	#tag EndEvent

	#tag Event
		Sub RunTool(Tool As Ark.ProjectTool)
		  Select Case Tool.UUID
		  Case "614cfc80-b7aa-437d-b17e-01534f2ab778"
		    Var Changes As Integer = Self.Project.ConvertDinoReplacementsToSpawnOverrides()
		    Self.SetupUI
		    If Changes = 0 Then
		      Self.ShowAlert("No changes made", "Beacon was unable to find any replaced creatures that it could convert into spawn point additions.")
		    ElseIf Changes = 1 Then
		      Self.ShowAlert("Converted 1 creature replacement", "Beacon found 1 creature that it was able to convert into spawn point additions. The replaced creature has been disabled.")
		    Else
		      Self.ShowAlert("Converted " + Changes.ToString + " creature replacements", "Beacon found " + Changes.ToString + " creatures that it was able to convert into spawn point additions. The replaced creatures have been disabled.")
		    End If
		  Case "8913bca3-fbae-43bd-a94b-7c3ac06b6ca1"
		    Var SourceConfig As Ark.Configs.SpawnPoints = Self.Config(False)
		    If ArkBulkSpawnEditWindow.Present(Self, SourceConfig, Self.Project.ContentPacks, Self.Project.MapMask, Self.Project.Difficulty.DifficultyValue) Then
		      Call Self.Config(True) // To retain the config
		      Self.Modified = SourceConfig.Modified
		      Self.SetupUI
		    End If
		  Case Self.ToolSpawnSimulator
		    Var Win As ArkSpawnSimulatorWindow = Self.SimulatorWindow(True)
		    Win.RunSimulator()
		    Win.Show
		  End Select
		End Sub
	#tag EndEvent

	#tag Event
		Sub SetupUI()
		  Self.UpdateList()
		End Sub
	#tag EndEvent


	#tag MenuHandler
		Function ConvertCreatureReplacementsToSpawnPointAdditions() As Boolean Handles ConvertCreatureReplacementsToSpawnPointAdditions.Action
		  If Self.IsFrontmost = False Then
		    Return False
		  End If
		  
		  Var Changes As Integer = Self.Project.ConvertDinoReplacementsToSpawnOverrides()
		  Self.SetupUI()
		  If Changes = 0 Then
		    Self.ShowAlert("No changes made", "Beacon was unable to find any replaced creatures in Creature Adjustments that it could convert into spawn point additions.")
		  ElseIf Changes = 1 Then
		    Self.ShowAlert("Converted 1 creature replacement", "Beacon found 1 creature in Creature Adjustments that it was able to convert into spawn point additions. The replaced creature has been disabled in Creature Adjustments.")
		  Else
		    Self.ShowAlert("Converted " + Changes.ToString + " creature replacements", "Beacon found " + Changes.ToString + " creatures in Creature Adjustments that it was able to convert into spawn point additions. The replaced creatures have been disabled in Creature Adjustments.")
		  End If
		  Return True
		End Function
	#tag EndMenuHandler


	#tag Method, Flags = &h1
		Protected Function Config(ForWriting As Boolean) As Ark.Configs.SpawnPoints
		  Return Ark.Configs.SpawnPoints(Super.Config(ForWriting))
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub HandlePastedSpawnPoints(SpawnPoints() As Ark.SpawnPoint)
		  If SpawnPoints.LastIndex = -1 Then
		    Return
		  End If
		  
		  Var Config As Ark.Configs.SpawnPoints = Self.Config(True)
		  For Each SpawnPoint As Ark.SpawnPoint In SpawnPoints
		    Config.Add(SpawnPoint)
		  Next
		  
		  Self.Modified = Config.Modified
		  Self.UpdateList(SpawnPoints)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Function InternalName() As String
		  Return Ark.Configs.NameSpawnPoints
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Shared Function MinEditorWidth() As Integer
		  Return ListMinWidth + ArkSpawnPointEditor.MinEditorWidth
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub SetListWidth(NewSize As Integer)
		  Var ListWidth, EditorWidth As Integer
		  If Self.Width <= Self.MinEditorWidth Then
		    ListWidth = Self.ListMinWidth
		    EditorWidth = ArkSpawnPointEditor.MinEditorWidth
		  Else
		    Var AvailableSpace As Integer = Self.Width - Self.MainSeparator.Width
		    ListWidth = Min(Max(NewSize, Self.ListMinWidth), AvailableSpace - ArkSpawnPointEditor.MinEditorWidth)
		    EditorWidth = AvailableSpace - ListWidth
		  End If
		  
		  Self.ConfigToolbar.Width = ListWidth
		  Self.MainSeparator.Left = ListWidth
		  Self.List.Width = ListWidth
		  Self.ListStatus.Width = ListWidth
		  Self.FilterSeparator.Width = ListWidth
		  Self.FilterField.Width = ListWidth - (Self.FilterField.Left * 2)
		  Self.Pages.Left = Self.MainSeparator.Left + Self.MainSeparator.Width
		  Self.Pages.Width = EditorWidth
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function SimulatorWindow(Create As Boolean) As ArkSpawnSimulatorWindow
		  Var Win As ArkSpawnSimulatorWindow
		  If Self.mSimulatorWindow Is Nil Or Self.mSimulatorWindow.Value Is Nil Or (Self.mSimulatorWindow.Value IsA ArkSpawnSimulatorWindow) = False Then
		    If Create Then
		      Win = New ArkSpawnSimulatorWindow(Self.Project)
		      Self.mSimulatorWindow = New WeakRef(Win)
		    Else
		      Return Nil
		    End If
		  Else
		    Win = ArkSpawnSimulatorWindow(Self.mSimulatorWindow.Value)
		  End If
		  Return Win
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateList()
		  Var SpawnPoints() As Ark.SpawnPoint
		  Var Bound As Integer = Self.List.RowCount - 1
		  For I As Integer = 0 To Bound
		    If Self.List.RowSelectedAt(I) Then
		      SpawnPoints.Add(Self.List.RowTagAt(I))
		    End If
		  Next
		  Self.UpdateList(SpawnPoints)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateList(SelectedPoints() As Ark.SpawnPoint)
		  Var Config As Ark.Configs.SpawnPoints = Self.Config(False)
		  Var SpawnPoints() As Ark.SpawnPoint = Config.Points(Self.FilterField.Text)
		  Var Selected As New Dictionary
		  For Each SpawnPoint As Ark.SpawnPoint In SelectedPoints
		    Selected.Value(SpawnPoint.UniqueKey) = True
		  Next
		  
		  Var Labels As Dictionary = Ark.DataSource.Pool.Get(False).GetSpawnPointLabels(Self.Project.MapMask)
		  
		  Self.List.SelectionChangeBlocked = True
		  Self.List.RowCount = SpawnPoints.Count
		  For I As Integer = SpawnPoints.FirstIndex To SpawnPoints.LastIndex
		    Var Prefix As String
		    Select Case SpawnPoints(I).Mode
		    Case Ark.SpawnPoint.ModeOverride
		      Prefix = "Replace"
		    Case Ark.SpawnPoint.ModeAppend
		      Prefix = "Add to"
		    Case Ark.SpawnPoint.ModeRemove
		      Prefix = "Remove from"
		    Else
		      #if DebugBuild
		        System.DebugLog("Unknown mode for spawn point `" + SpawnPoints(I).Path + "`: " + CType(SpawnPoints(I).Mode, Integer).ToString)
		      #endif
		    End Select
		    
		    Var RowLabel As String = Prefix + " " + Labels.Lookup(SpawnPoints(I).ObjectID, SpawnPoints(I).Label).StringValue
		    Self.List.CellTextAt(I, 0) = RowLabel
		    Self.List.RowTagAt(I) = SpawnPoints(I)
		    Self.List.RowSelectedAt(I) = Selected.HasKey(SpawnPoints(I).UniqueKey)
		  Next I
		  Self.List.SortingColumn = 0
		  Self.List.Sort
		  Self.List.SelectionChangeBlocked = False
		  
		  Self.UpdateStatus()
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateStatus()
		  If Self.List.SelectedRowCount > 0 Then
		    Self.ListStatus.Caption = Self.List.SelectedRowCount.ToString + " of " + Language.NounWithQuantity(Self.List.RowCount, "Spawn Point", "Spawn Points") + " Selected"
		  Else
		    Self.ListStatus.Caption = Language.NounWithQuantity(Self.List.RowCount, "Spawn Point", "Spawn Points")
		  End If
		End Sub
	#tag EndMethod


	#tag Property, Flags = &h21
		Private mConfigRef As WeakRef
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mSimulatorWindow As WeakRef
	#tag EndProperty


	#tag Constant, Name = kClipboardType, Type = String, Dynamic = False, Default = \"com.thezaz.beacon.ark.spawn.point", Scope = Private
	#tag EndConstant

	#tag Constant, Name = ListDefaultWidth, Type = Double, Dynamic = False, Default = \"330", Scope = Public
	#tag EndConstant

	#tag Constant, Name = ListMinWidth, Type = Double, Dynamic = False, Default = \"225", Scope = Public
		#Tag Instance, Platform = Windows, Language = Default, Definition  = \"217"
	#tag EndConstant

	#tag Constant, Name = ToolSpawnSimulator, Type = String, Dynamic = False, Default = \"fb860a80-e301-43da-b283-cdfcd7369def", Scope = Public
	#tag EndConstant


#tag EndWindowCode

#tag Events List
	#tag Event
		Sub SelectionChanged()
		  Var SpawnPoints() As Ark.SpawnPoint
		  Var Bound As Integer = Me.RowCount - 1
		  For I As Integer = 0 To Bound
		    If Me.RowSelectedAt(I) Then
		      SpawnPoints.Add(Me.RowTagAt(I))
		    End If
		  Next
		  
		  Self.Editor.SpawnPoints = SpawnPoints
		  Var DuplicateButton As OmniBarItem = Self.ConfigToolbar.Item("DuplicateButton")
		  If (DuplicateButton Is Nil) = False Then
		    DuplicateButton.Enabled = Me.SelectedRowCount = 1
		  End If
		  Self.Pages.SelectedPanelIndex = If(SpawnPoints.LastIndex = -1, 0, 1)
		  Self.UpdateStatus()
		End Sub
	#tag EndEvent
	#tag Event
		Function CanDelete() As Boolean
		  Return Me.SelectedRowCount > 0
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformClear(Warn As Boolean)
		  Var Bound As Integer = Me.RowCount - 1
		  Var Config As Ark.Configs.SpawnPoints = Self.Config(True)
		  Var Points() As Ark.SpawnPoint
		  For I As Integer = 0 To Bound
		    If Me.RowSelectedAt(I) = False Then
		      Continue
		    End If
		    
		    Points.Add(Me.RowTagAt(I))
		  Next
		  
		  If Warn And Self.ShowDeleteConfirmation(Points, "spawn point", "spawn points") = False Then
		    Return
		  End If
		  
		  For Each Point As Ark.SpawnPoint In Points
		    Config.Remove(Point)
		  Next
		  
		  Self.UpdateList()
		  Self.Modified = Config.Modified
		End Sub
	#tag EndEvent
	#tag Event
		Function CanCopy() As Boolean
		  Return Me.SelectedRowCount > 0
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformCopy(Board As Clipboard)
		  Var SaveData() As Dictionary
		  Var Bound As Integer = Me.RowCount - 1
		  For I As Integer = 0 To Bound
		    If Not Me.RowSelectedAt(I) Then
		      Continue
		    End If
		    
		    Var SpawnPoint As Ark.SpawnPoint = Me.RowTagAt(I)
		    SaveData.Add(SpawnPoint.SaveData)
		  Next
		  
		  Board.RawData(Self.kClipboardType) = Beacon.GenerateJSON(SaveData, False)
		  
		  If Not Ark.Configs.ConfigUnlocked(Ark.Configs.NameSpawnPoints, App.IdentityManager.CurrentIdentity) Then
		    Return
		  End If
		  
		  Var Lines() As String
		  For I As Integer = 0 To Bound
		    If Not Me.RowSelectedAt(I) Then
		      Continue
		    End If
		    
		    Var SpawnPoint As Ark.SpawnPoint = Me.RowTagAt(I)
		    Var Value As Ark.ConfigValue = Ark.Configs.SpawnPoints.ConfigValueForSpawnPoint(SpawnPoint)
		    If Value <> Nil Then
		      Lines.Add(Value.Command)
		    End If
		  Next
		  
		  Board.Text = Lines.Join(EndOfLine)
		End Sub
	#tag EndEvent
	#tag Event
		Function CanPaste(Board As Clipboard) As Boolean
		  If Board.RawDataAvailable(Self.kClipboardType) Then
		    Return True
		  End If
		  
		  If Not Board.TextAvailable Then
		    Return False
		  End If
		  
		  Var CopiedText As String = Board.Text.Left(38)
		  Return CopiedText.IndexOf("ConfigOverrideNPCSpawnEntriesContainer") > -1 Or CopiedText.IndexOf("ConfigAddNPCSpawnEntriesContainer") > -1 Or CopiedText.IndexOf("ConfigSubtractNPCSpawnEntriesContainer") > -1
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformPaste(Board As Clipboard)
		  If Board.RawDataAvailable(Self.kClipboardType) Then
		    Try
		      Var Parsed() As Variant = Beacon.ParseJSON(Board.RawData(Self.kClipboardType))
		      Var SpawnPoints() As Ark.SpawnPoint
		      For Each Dict As Dictionary In Parsed
		        Var SpawnPoint As Ark.SpawnPoint = Ark.SpawnPoint.FromSaveData(Dict)
		        If SpawnPoint <> Nil Then
		          SpawnPoints.Add(SpawnPoint)
		        End If
		      Next
		      Self.HandlePastedSpawnPoints(SpawnPoints)
		    Catch Err As RuntimeException
		    End Try
		  ElseIf Board.TextAvailable Then
		    Self.Parse("", Board.Text, "Clipboard")
		  End If
		End Sub
	#tag EndEvent
	#tag Event
		Function RowComparison(row1 as Integer, row2 as Integer, column as Integer, ByRef result as Integer) As Boolean
		  If Column <> 0 Then
		    Return False
		  End If
		  
		  Var Point1 As Ark.SpawnPoint = Me.RowTagAt(Row1)
		  Var Point2 As Ark.SpawnPoint = Me.RowTagAt(Row2)
		  
		  If Point1 = Nil Or Point2 = Nil Then
		    Return False
		  End If
		  
		  Result = Point1.Label.Compare(Point2.Label)
		  If Result <> 0 Then
		    Return True
		  End If
		  
		  If Point1.Mode < Point2.Mode Then
		    Result = -1
		  ElseIf Point1.Mode > Point2.Mode Then
		    Result = 1
		  End If
		  
		  Return True
		End Function
	#tag EndEvent
	#tag Event
		Function Typeahead(Buffer As String) As Boolean
		  For Row As Integer = 0 To Me.LastRowIndex
		    Var Point As Ark.SpawnPoint = Me.RowTagAt(Row)
		    If (Point Is Nil) = False And Point.Label.BeginsWith(Buffer) Then
		      Me.SelectedRowIndex = Row
		      Me.EnsureSelectionIsVisible
		      Exit
		    End If
		  Next
		  
		  Return True
		End Function
	#tag EndEvent
#tag EndEvents
#tag Events Editor
	#tag Event
		Function GetProject() As Ark.Project
		  Return Self.Project
		End Function
	#tag EndEvent
	#tag Event
		Sub Changed()
		  Var Config As Ark.Configs.SpawnPoints = Self.Config(True)
		  Var Points() As Ark.SpawnPoint = Me.SpawnPoints
		  Var PathMap As New Dictionary
		  For Each Point As Ark.SpawnPoint In Points
		    Config.Add(Point)
		    PathMap.Value(Point.UniqueKey) = Point
		  Next
		  For I As Integer = 0 To Self.List.RowCount - 1
		    Var Point As Ark.SpawnPoint = Self.List.RowTagAt(I)
		    If Not PathMap.HasKey(Point.UniqueKey) Then
		      Continue
		    End If
		    
		    Var NewPoint As Ark.SpawnPoint = PathMap.Value(Point.UniqueKey)
		    Self.List.RowTagAt(I) = NewPoint
		  Next
		  
		  Self.Modified = Self.Config(False).Modified
		  
		  Var Simulator As ArkSpawnSimulatorWindow = Self.SimulatorWindow(False)
		  If (Simulator Is Nil) = False Then
		    Simulator.RunSimulator()
		  End If
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events ConfigToolbar
	#tag Event
		Sub ItemPressed(Item As OmniBarItem, ItemRect As Rect)
		  #Pragma Unused ItemRect
		  
		  Select Case Item.Name
		  Case "AddButton"
		    Var SpawnPoints() As Ark.SpawnPoint = ArkAddSpawnPointDialog.Present(Self, Self.Project)
		    If SpawnPoints.LastIndex = -1 Then
		      Return
		    End If
		    
		    Var Config As Ark.Configs.SpawnPoints = Self.Config(True)
		    For Each SpawnPoint As Ark.SpawnPoint In SpawnPoints
		      Config.Add(SpawnPoint)
		    Next
		    
		    Self.Modified = Config.Modified
		    Self.UpdateList(SpawnPoints)
		  Case "DuplicateButton"
		    Var TargetSpawnPoints() As Ark.SpawnPoint = ArkAddSpawnPointDialog.Present(Self, Self.Project, ArkAddSpawnPointDialog.UIModeDuplicate)
		    If TargetSpawnPoints.LastIndex = -1 Then
		      Return
		    End If
		    
		    Var SourceSpawnPoint As Ark.SpawnPoint = Self.List.RowTagAt(Self.List.SelectedRowIndex)
		    Var SourceLimits As String = SourceSpawnPoint.LimitsString
		    Var SourceSets As String = SourceSpawnPoint.SetsString
		    Var Config As Ark.Configs.SpawnPoints = Self.Config(True)
		    For Each Target As Ark.SpawnPoint In TargetSpawnPoints
		      Var SpawnPoint As New Ark.MutableSpawnPoint(Target)
		      SpawnPoint.Mode = SourceSpawnPoint.Mode
		      SpawnPoint.LimitsString = SourceLimits
		      SpawnPoint.SetsString = SourceSets
		      Config.Add(SpawnPoint)
		    Next
		    
		    Self.Modified = Config.Modified
		    Self.UpdateList(TargetSpawnPoints)
		  End Select
		End Sub
	#tag EndEvent
	#tag Event
		Sub Opening()
		  Me.Append(OmniBarItem.CreateTitle("EditorTitle", Self.ConfigLabel))
		  Me.Append(OmniBarItem.CreateSeparator("EditorTitleSeparator"))
		  Me.Append(OmniBarItem.CreateButton("AddButton", "New Point", IconToolbarAdd, "Override a spawn point."))
		  Me.Append(OmniBarItem.CreateButton("DuplicateButton", "Duplicate", IconToolbarClone, "Duplicate the selected spawn point.", False))
		  Me.Append(OmniBarItem.CreateFlexibleSpace)
		  Me.Append(OmniBarItem.CreateHorizontalResizer("Resizer"))
		  
		  Me.Item("EditorTitle").Priority = 5
		  Me.Item("EditorTitleSeparator").Priority = 5
		End Sub
	#tag EndEvent
	#tag Event
		Sub Resize(DraggedResizer As OmniBarItem, DeltaX As Integer, DeltaY As Integer)
		  #Pragma Unused DraggedResizer
		  #Pragma Unused DeltaY
		  
		  Self.SetListWidth(Me.Width + DeltaX)
		End Sub
	#tag EndEvent
	#tag Event
		Sub ResizeFinished(DraggedResizer As OmniBarItem)
		  #Pragma Unused DraggedResizer
		  
		  Preferences.SpawnPointsSplitterPosition = Self.List.Width
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events FilterField
	#tag Event
		Sub TextChanged()
		  If Self.SettingUp Then
		    Return
		  End If
		  
		  Self.UpdateList()
		End Sub
	#tag EndEvent
#tag EndEvents
#tag ViewBehavior
	#tag ViewProperty
		Name="Modified"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Composited"
		Visible=true
		Group="Window Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Index"
		Visible=true
		Group="ID"
		InitialValue="-2147483648"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="IsFrontmost"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="ViewTitle"
		Visible=true
		Group="Behavior"
		InitialValue="Untitled"
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="ViewIcon"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Progress"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Double"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimumWidth"
		Visible=true
		Group="Behavior"
		InitialValue="400"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimumHeight"
		Visible=true
		Group="Behavior"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Name"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Super"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Width"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Height"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="InitialParent"
		Visible=false
		Group="Position"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Left"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Top"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockLeft"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockTop"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockRight"
		Visible=true
		Group="Position"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockBottom"
		Visible=true
		Group="Position"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabIndex"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabPanelIndex"
		Visible=false
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabStop"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowAutoDeactivate"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Enabled"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Tooltip"
		Visible=true
		Group="Appearance"
		InitialValue=""
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocusRing"
		Visible=true
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Visible"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="BackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="&hFFFFFF"
		Type="ColorGroup"
		EditorType="ColorGroup"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Backdrop"
		Visible=true
		Group="Background"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasBackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocus"
		Visible=true
		Group="Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowTabs"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Transparent"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
#tag EndViewBehavior
