#tag DesktopWindow
Begin ArkConfigEditor ArkCreatureSpawnsEditor
   AllowAutoDeactivate=   True
   AllowFocus      =   False
   AllowFocusRing  =   False
   AllowTabs       =   True
   Backdrop        =   0
   BackgroundColor =   &cFFFFFF00
   Composited      =   False
   Enabled         =   True
   HasBackgroundColor=   False
   Height          =   548
   Index           =   -2147483648
   InitialParent   =   ""
   Left            =   0
   LockBottom      =   True
   LockLeft        =   True
   LockRight       =   True
   LockTop         =   True
   TabIndex        =   0
   TabPanelIndex   =   0
   TabStop         =   True
   Tooltip         =   ""
   Top             =   0
   Transparent     =   True
   Visible         =   True
   Width           =   980
   Begin BeaconListbox List
      AllowAutoDeactivate=   True
      AllowAutoHideScrollbars=   True
      AllowExpandableRows=   False
      AllowFocusRing  =   False
      AllowInfiniteScroll=   False
      AllowResizableColumns=   False
      AllowRowDragging=   False
      AllowRowReordering=   False
      Bold            =   False
      ColumnCount     =   1
      ColumnWidths    =   ""
      DefaultRowHeight=   26
      DefaultSortColumn=   0
      DefaultSortDirection=   0
      DropIndicatorVisible=   False
      EditCaption     =   "Edit"
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      GridLineStyle   =   0
      HasBorder       =   False
      HasHeader       =   False
      HasHorizontalScrollbar=   False
      HasVerticalScrollbar=   True
      HeadingIndex    =   -1
      Height          =   435
      Index           =   -2147483648
      InitialParent   =   ""
      InitialValue    =   ""
      Italic          =   False
      Left            =   0
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      PageSize        =   100
      PreferencesKey  =   ""
      RequiresSelection=   False
      RowSelectionType=   1
      Scope           =   2
      TabIndex        =   3
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   82
      TotalPages      =   -1
      Transparent     =   False
      TypeaheadColumn =   0
      Underline       =   False
      Visible         =   True
      VisibleRowCount =   0
      Width           =   250
      _ScrollOffset   =   0
      _ScrollWidth    =   -1
   End
   Begin FadedSeparator MainSeparator
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   548
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   250
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   1
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   True
      Visible         =   True
      Width           =   1
   End
   Begin DesktopPagePanel Pages
      AllowAutoDeactivate=   True
      Enabled         =   True
      Height          =   548
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   251
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   True
      LockTop         =   True
      PanelCount      =   2
      Panels          =   ""
      Scope           =   2
      SelectedPanelIndex=   0
      TabIndex        =   2
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   False
      Value           =   0
      Visible         =   True
      Width           =   729
      Begin ArkSpawnPointEditor Editor
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   False
         AllowTabs       =   True
         Backdrop        =   0
         BackgroundColor =   &cFFFFFF00
         Composited      =   False
         Enabled         =   True
         HasBackgroundColor=   False
         Height          =   548
         Index           =   -2147483648
         InitialParent   =   "Pages"
         Left            =   251
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Modified        =   False
         ReadOnly        =   False
         Scope           =   2
         TabIndex        =   0
         TabPanelIndex   =   2
         TabStop         =   True
         Tooltip         =   ""
         Top             =   0
         Transparent     =   True
         Visible         =   True
         Width           =   729
      End
      Begin LogoFillCanvas NoSelectionFillCanvas
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   True
         AllowTabs       =   False
         Backdrop        =   0
         Caption         =   "No Selection"
         ContentHeight   =   0
         Enabled         =   True
         Height          =   517
         Index           =   -2147483648
         InitialParent   =   "Pages"
         Left            =   251
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Scope           =   2
         ScrollActive    =   False
         ScrollingEnabled=   False
         ScrollSpeed     =   20
         TabIndex        =   0
         TabPanelIndex   =   1
         TabStop         =   True
         Tooltip         =   ""
         Top             =   0
         Transparent     =   True
         Visible         =   True
         Width           =   729
      End
      Begin StatusContainer NoSelectionStatus
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   False
         AllowTabs       =   True
         Backdrop        =   0
         BackgroundColor =   &cFFFFFF
         CenterCaption   =   ""
         Composited      =   False
         Enabled         =   True
         HasBackgroundColor=   False
         Height          =   31
         Index           =   -2147483648
         InitialParent   =   "Pages"
         Left            =   251
         LeftCaption     =   ""
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   False
         RightCaption    =   ""
         Scope           =   2
         TabIndex        =   1
         TabPanelIndex   =   1
         TabStop         =   True
         Tooltip         =   ""
         Top             =   517
         Transparent     =   True
         Visible         =   True
         Width           =   729
      End
   End
   Begin OmniBar ConfigToolbar
      Alignment       =   0
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      BackgroundColor =   ""
      ContentHeight   =   0
      Enabled         =   True
      Height          =   41
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LeftPadding     =   -1
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      RightPadding    =   -1
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   5
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
   Begin DelayedSearchField FilterField
      Active          =   False
      AllowAutoDeactivate=   True
      AllowFocusRing  =   True
      AllowRecentItems=   False
      AllowTabStop    =   True
      ClearMenuItemValue=   "Clear"
      DelayPeriod     =   250
      Enabled         =   True
      Height          =   22
      Hint            =   "Filter Spawns"
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   9
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      MaximumRecentItems=   -1
      PanelIndex      =   0
      RecentItemsValue=   "Recent Searches"
      Scope           =   2
      TabIndex        =   6
      TabPanelIndex   =   0
      Text            =   ""
      Tooltip         =   ""
      Top             =   50
      Transparent     =   False
      Visible         =   True
      Width           =   232
      _mIndex         =   0
      _mInitialParent =   ""
      _mName          =   ""
      _mPanelIndex    =   0
   End
   Begin OmniBarSeparator FilterSeparator
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   1
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   7
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   81
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
   Begin StatusContainer ListStatus
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   False
      AllowTabs       =   True
      Backdrop        =   0
      BackgroundColor =   &cFFFFFF
      CenterCaption   =   ""
      Composited      =   False
      Enabled         =   True
      HasBackgroundColor=   False
      Height          =   31
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LeftCaption     =   ""
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   False
      RightCaption    =   ""
      Scope           =   2
      TabIndex        =   8
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   517
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
End
#tag EndDesktopWindow

#tag WindowCode
	#tag Event
		Sub Closing()
		  Var Simulator As ArkSpawnSimulatorWindow = Self.SimulatorWindow(False)
		  If (Simulator Is Nil) = False Then
		    Simulator.Close
		    Self.mSimulatorWindow = Nil
		  End If
		End Sub
	#tag EndEvent

	#tag Event
		Sub Opening()
		  Self.MinimumWidth = Self.ListMinWidth + Self.MainSeparator.Width + ArkSpawnPointEditor.MinEditorWidth
		  Self.MinimumHeight = 350
		End Sub
	#tag EndEvent

	#tag Event
		Function ParsingFinished(Project As Ark.Project) As Boolean
		  If Project Is Nil Or Project.HasConfigGroup(Ark.Configs.NameCreatureSpawns) = False Then
		    Return True
		  End If
		  
		  Var ParsedConfig As Ark.Configs.SpawnPoints = Ark.Configs.SpawnPoints(Project.ConfigGroup(Ark.Configs.NameCreatureSpawns))
		  If ParsedConfig = Nil Or ParsedConfig.Count = 0 Then
		    Self.ShowAlert("No spawn points to import", "The parsed ini content did not contain any spawn point data.")
		    Return True
		  End If
		  
		  Self.HandlePastedSpawnPoints(ParsedConfig.Overrides)
		  Return True
		End Function
	#tag EndEvent

	#tag Event
		Sub Resize(Initial As Boolean)
		  #Pragma Unused Initial
		  
		  Self.SetListWidth(Preferences.SpawnPointsSplitterPosition)
		  
		  Var Resizer As OmniBarItem = Self.ConfigToolbar.Item("Resizer")
		  If (Resizer Is Nil) = False Then
		    Resizer.Enabled = Self.Width > Self.MinEditorWidth
		  End If
		End Sub
	#tag EndEvent

	#tag Event
		Sub RunTool(Tool As Ark.ProjectTool)
		  Select Case Tool.UUID
		  Case "614cfc80-b7aa-437d-b17e-01534f2ab778"
		    Var Changes As Integer = Self.Project.ConvertDinoReplacementsToSpawnOverrides()
		    Self.SetupUI
		    If Changes = 0 Then
		      Self.ShowAlert("No changes made", "Beacon was unable to find any replaced creatures that it could convert into spawn point additions.")
		    ElseIf Changes = 1 Then
		      Self.ShowAlert("Converted 1 creature replacement", "Beacon found 1 creature that it was able to convert into spawn point additions. The replaced creature has been disabled.")
		    Else
		      Self.ShowAlert("Converted " + Changes.ToString + " creature replacements", "Beacon found " + Changes.ToString + " creatures that it was able to convert into spawn point additions. The replaced creatures have been disabled.")
		    End If
		  Case "8913bca3-fbae-43bd-a94b-7c3ac06b6ca1"
		    Var SourceConfig As Ark.Configs.SpawnPoints = Self.Config(False)
		    If ArkBulkSpawnEditWindow.Present(Self, SourceConfig, Self.Project.ContentPacks, Self.Project.MapMask, Self.Project.Difficulty.DifficultyValue) Then
		      Call Self.Config(True) // To retain the config
		      Self.Modified = SourceConfig.Modified
		      Self.SetupUI
		    End If
		  Case Self.ToolSpawnSimulator
		    Var Win As ArkSpawnSimulatorWindow = Self.SimulatorWindow(True)
		    Win.RunSimulator()
		    Win.Show
		  End Select
		End Sub
	#tag EndEvent

	#tag Event
		Sub SetupUI()
		  Self.UpdateList()
		End Sub
	#tag EndEvent


	#tag MenuHandler
		Function ConvertCreatureReplacementsToSpawnPointAdditions() As Boolean Handles ConvertCreatureReplacementsToSpawnPointAdditions.Action
		  If Self.IsFrontmost = False Then
		    Return False
		  End If
		  
		  Var Changes As Integer = Self.Project.ConvertDinoReplacementsToSpawnOverrides()
		  Self.SetupUI()
		  If Changes = 0 Then
		    Self.ShowAlert("No changes made", "Beacon was unable to find any replaced creatures in Creature Adjustments that it could convert into spawn point additions.")
		  ElseIf Changes = 1 Then
		    Self.ShowAlert("Converted 1 creature replacement", "Beacon found 1 creature in Creature Adjustments that it was able to convert into spawn point additions. The replaced creature has been disabled in Creature Adjustments.")
		  Else
		    Self.ShowAlert("Converted " + Changes.ToString + " creature replacements", "Beacon found " + Changes.ToString + " creatures in Creature Adjustments that it was able to convert into spawn point additions. The replaced creatures have been disabled in Creature Adjustments.")
		  End If
		  Return True
		End Function
	#tag EndMenuHandler


	#tag Method, Flags = &h1
		Protected Function Config(ForWriting As Boolean) As Ark.Configs.SpawnPoints
		  Return Ark.Configs.SpawnPoints(Super.Config(ForWriting))
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub HandlePastedSpawnPoints(SpawnPoints() As Ark.SpawnPointOverride)
		  If SpawnPoints.Count = 0 Then
		    Return
		  End If
		  
		  Var Config As Ark.Configs.SpawnPoints = Self.Config(True)
		  For Each Override As Ark.SpawnPointOverride In SpawnPoints
		    Config.Add(Override)
		  Next
		  
		  Self.Modified = Config.Modified
		  Self.UpdateList(SpawnPoints)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Function InternalName() As String
		  Return Ark.Configs.NameCreatureSpawns
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Shared Function MinEditorWidth() As Integer
		  Return ListMinWidth + ArkSpawnPointEditor.MinEditorWidth
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub SetListWidth(NewSize As Integer)
		  Var ListWidth, EditorWidth As Integer
		  If Self.Width <= Self.MinEditorWidth Then
		    ListWidth = Self.ListMinWidth
		    EditorWidth = ArkSpawnPointEditor.MinEditorWidth
		  Else
		    Var AvailableSpace As Integer = Self.Width - Self.MainSeparator.Width
		    ListWidth = Min(Max(NewSize, Self.ListMinWidth), AvailableSpace - ArkSpawnPointEditor.MinEditorWidth)
		    EditorWidth = AvailableSpace - ListWidth
		  End If
		  
		  Self.ConfigToolbar.Width = ListWidth
		  Self.MainSeparator.Left = ListWidth
		  Self.List.Width = ListWidth
		  Self.ListStatus.Width = ListWidth
		  Self.FilterSeparator.Width = ListWidth
		  Self.FilterField.Width = ListWidth - (Self.FilterField.Left * 2)
		  Self.Pages.Left = Self.MainSeparator.Left + Self.MainSeparator.Width
		  Self.Pages.Width = EditorWidth
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function SimulatorWindow(Create As Boolean) As ArkSpawnSimulatorWindow
		  Var Win As ArkSpawnSimulatorWindow
		  If Self.mSimulatorWindow Is Nil Or Self.mSimulatorWindow.Value Is Nil Or (Self.mSimulatorWindow.Value IsA ArkSpawnSimulatorWindow) = False Then
		    If Create Then
		      Win = New ArkSpawnSimulatorWindow(Self.Project)
		      Self.mSimulatorWindow = New WeakRef(Win)
		    Else
		      Return Nil
		    End If
		  Else
		    Win = ArkSpawnSimulatorWindow(Self.mSimulatorWindow.Value)
		  End If
		  Return Win
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateList()
		  Var Overrides() As Ark.SpawnPointOverride
		  Var Bound As Integer = Self.List.RowCount - 1
		  For I As Integer = 0 To Bound
		    If Self.List.RowSelectedAt(I) Then
		      Overrides.Add(Self.List.RowTagAt(I))
		    End If
		  Next
		  Self.UpdateList(Overrides)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateList(SelectedPoints() As Ark.SpawnPointOverride)
		  Var Config As Ark.Configs.SpawnPoints = Self.Config(False)
		  Var Overrides() As Ark.SpawnPointOverride = Config.Overrides(Self.FilterField.Text)
		  Var Selected As New Dictionary
		  For Each Override As Ark.SpawnPointOverride In SelectedPoints
		    Selected.Value(Override.UniqueKey) = True
		  Next
		  
		  Var Labels As Dictionary = Ark.DataSource.Pool.Get(False).GetSpawnPointLabels(Self.Project.MapMask)
		  
		  Self.List.SelectionChangeBlocked = True
		  Self.List.RowCount = Overrides.Count
		  For I As Integer = Overrides.FirstIndex To Overrides.LastIndex
		    Var Prefix As String
		    Select Case Overrides(I).Mode
		    Case Ark.SpawnPointOverride.ModeOverride
		      Prefix = "Replace"
		    Case Ark.SpawnPointOverride.ModeAppend
		      Prefix = "Add to"
		    Case Ark.SpawnPointOverride.ModeRemove
		      Prefix = "Remove from"
		    Else
		      #if DebugBuild
		        System.DebugLog("Unknown mode for spawn point `" + Overrides(I).SpawnPointReference.Path + "`: " + CType(Overrides(I).Mode, Integer).ToString)
		      #endif
		    End Select
		    
		    Var RowLabel As String = Prefix + " " + Labels.Lookup(Overrides(I).SpawnPointId, Overrides(I).Label).StringValue
		    Self.List.CellTextAt(I, 0) = RowLabel
		    Self.List.RowTagAt(I) = Overrides(I)
		    Self.List.RowSelectedAt(I) = Selected.HasKey(Overrides(I).UniqueKey)
		  Next I
		  Self.List.SortingColumn = 0
		  Self.List.Sort
		  Self.List.SelectionChangeBlocked = False
		  
		  Self.UpdateStatus()
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateStatus()
		  Self.ListStatus.CenterCaption = Self.List.StatusMessage("Spawn Point", "Spawn Points")
		End Sub
	#tag EndMethod


	#tag Property, Flags = &h21
		Private mConfigRef As WeakRef
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mSimulatorWindow As WeakRef
	#tag EndProperty


	#tag Constant, Name = kClipboardType, Type = String, Dynamic = False, Default = \"com.thezaz.beacon.ark.spawn.point", Scope = Private
	#tag EndConstant

	#tag Constant, Name = ListDefaultWidth, Type = Double, Dynamic = False, Default = \"330", Scope = Public
	#tag EndConstant

	#tag Constant, Name = ListMinWidth, Type = Double, Dynamic = False, Default = \"225", Scope = Public
		#Tag Instance, Platform = Windows, Language = Default, Definition  = \"217"
	#tag EndConstant

	#tag Constant, Name = ToolSpawnSimulator, Type = String, Dynamic = False, Default = \"fb860a80-e301-43da-b283-cdfcd7369def", Scope = Public
	#tag EndConstant


#tag EndWindowCode

#tag Events List
	#tag Event
		Sub SelectionChanged()
		  Var Overrides() As Ark.SpawnPointOverride
		  Var Bound As Integer = Me.RowCount - 1
		  For I As Integer = 0 To Bound
		    If Me.RowSelectedAt(I) Then
		      Overrides.Add(Me.RowTagAt(I))
		    End If
		  Next
		  
		  Self.Editor.Overrides = Overrides
		  Var DuplicateButton As OmniBarItem = Self.ConfigToolbar.Item("DuplicateButton")
		  If (DuplicateButton Is Nil) = False Then
		    DuplicateButton.Enabled = Me.SelectedRowCount = 1
		  End If
		  Self.Pages.SelectedPanelIndex = If(Overrides.LastIndex = -1, 0, 1)
		  Self.UpdateStatus()
		End Sub
	#tag EndEvent
	#tag Event
		Function CanDelete() As Boolean
		  Return Me.SelectedRowCount > 0
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformClear(Warn As Boolean)
		  Var Bound As Integer = Me.RowCount - 1
		  Var Config As Ark.Configs.SpawnPoints = Self.Config(True)
		  Var Overrides() As Ark.SpawnPointOverride
		  For I As Integer = 0 To Bound
		    If Me.RowSelectedAt(I) = False Then
		      Continue
		    End If
		    
		    Overrides.Add(Me.RowTagAt(I))
		  Next
		  
		  If Warn And Self.ShowDeleteConfirmation(Overrides, "spawn point", "spawn points") = False Then
		    Return
		  End If
		  
		  For Each Override As Ark.SpawnPointOverride In Overrides
		    Config.Remove(Override)
		  Next
		  
		  Self.UpdateList()
		  Self.Modified = Config.Modified
		End Sub
	#tag EndEvent
	#tag Event
		Function CanCopy() As Boolean
		  Return Me.SelectedRowCount > 0 And Self.Project.ReadOnly = False
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformCopy(Board As Clipboard)
		  Var SaveData() As Dictionary
		  Var Bound As Integer = Me.RowCount - 1
		  For I As Integer = 0 To Bound
		    If Not Me.RowSelectedAt(I) Then
		      Continue
		    End If
		    
		    Var Override As Ark.SpawnPointOverride = Me.RowTagAt(I)
		    SaveData.Add(Override.SaveData)
		  Next
		  
		  If SaveData.Count = 0 Then
		    System.Beep
		    Return
		  End If
		  
		  Board.AddClipboardData(Self.kClipboardType, SaveData)
		End Sub
	#tag EndEvent
	#tag Event
		Function CanPaste(Board As Clipboard) As Boolean
		  Return Board.HasClipboardData(Self.kClipboardType)
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformPaste(Board As Clipboard)
		  Var Contents As Variant = Board.GetClipboardData(Self.kClipboardType)
		  If Contents.IsNull = False Then
		    Try
		      Var Dicts() As Variant = Contents
		      Var Overrides() As Ark.SpawnPointOverride
		      For Each Dict As Dictionary In Dicts
		        Var Override As Ark.SpawnPointOverride = Ark.SpawnPointOverride.FromSaveData(Dict)
		        If (Override Is Nil) = False Then
		          Overrides.Add(Override)
		        End If
		      Next
		      Self.HandlePastedSpawnPoints(Overrides)
		    Catch Err As RuntimeException
		      Self.ShowAlert("There was an error with the pasted content.", "The content is not formatted correctly.")
		    End Try
		    Return
		  End If
		End Sub
	#tag EndEvent
	#tag Event
		Function RowComparison(row1 as Integer, row2 as Integer, column as Integer, ByRef result as Integer) As Boolean
		  If Column <> 0 Then
		    Return False
		  End If
		  
		  Var Point1 As Ark.SpawnPointOverride = Me.RowTagAt(Row1)
		  Var Point2 As Ark.SpawnPointOverride = Me.RowTagAt(Row2)
		  
		  If Point1 Is Nil Or Point2 Is Nil Then
		    Return False
		  End If
		  
		  Result = Point1.Label.Compare(Point2.Label)
		  If Result <> 0 Then
		    Return True
		  End If
		  
		  If Point1.Mode < Point2.Mode Then
		    Result = -1
		  ElseIf Point1.Mode > Point2.Mode Then
		    Result = 1
		  End If
		  
		  Return True
		End Function
	#tag EndEvent
	#tag Event
		Function Typeahead(Buffer As String) As Boolean
		  For Row As Integer = 0 To Me.LastRowIndex
		    Var Override As Ark.SpawnPointOverride = Me.RowTagAt(Row)
		    If (Override Is Nil) = False And Override.Label.BeginsWith(Buffer) Then
		      Me.SelectedRowIndex = Row
		      Me.EnsureSelectionIsVisible
		      Exit
		    End If
		  Next
		  
		  Return True
		End Function
	#tag EndEvent
	#tag Event
		Function ContextualMenuItemSelected(HitItem As DesktopMenuItem) As Boolean
		  Select Case HitItem.Tag.Type
		  Case HitItem.Tag.TypeObject
		    Select Case HitItem.Tag.ObjectValue
		    Case IsA Dictionary
		      Var Tag As Dictionary = HitItem.Tag
		      If Tag.HasKey("Action") And Tag.Value("Action").Type = Variant.TypeString Then
		        Select Case Tag.Value("Action").StringValue
		        Case "Load Defaults"
		          Var Overrides() As Ark.SpawnPointOverride = Tag.Value("Overrides")
		          Var NumWithContent As Integer
		          For Each Override As Ark.SpawnPointOverride In Overrides
		            If Override.Count > 0 Then
		              NumWithContent = NumWithContent + 1
		            End If
		          Next
		          If NumWithContent > 0 And Self.ShowConfirm("Replace existing contents?", "Default spawn point contents will replace existing content. " + Language.NounWithQuantity(NumWithContent, "spawn point has", "spawn points have") + " content already.", "Load", Language.CommonCancel) = False Then
		            Return True
		          End If
		          
		          Var Config As Ark.Configs.SpawnPoints = Self.Config(True)
		          For Each Override As Ark.SpawnPointOverride In Overrides
		            Config.Add(New Ark.SpawnPointOverride(Override.SpawnPoint, Override.Mode, True))
		          Next
		          
		          Self.Modified = Config.Modified
		          Self.UpdateList(Overrides)
		        End Select
		      End If
		    End Select
		  End Select
		End Function
	#tag EndEvent
	#tag Event
		Function ConstructContextualMenu(Base As DesktopMenuItem, X As Integer, Y As Integer) As Boolean
		  Var RowIdx As Integer = Me.RowFromXY(X, Y)
		  If RowIdx < 0 Or RowIdx > Me.LastRowIndex Then
		    Return False
		  End If
		  
		  Var PressedOverride As Ark.SpawnPointOverride = Me.RowTagAt(RowIdx)
		  Var Overrides() As Ark.SpawnPointOverride
		  If Me.RowSelectedAt(RowIdx) = True Then
		    For Idx As Integer = 0 To Me.LastRowIndex
		      If Me.RowSelectedAt(Idx) = False Then
		        Continue
		      End If
		      
		      Var Override As Ark.SpawnPointOverride = Me.RowTagAt(Idx)
		      If Override.Mode <> Ark.SpawnPointOverride.ModeOverride Then
		        Continue
		      End If
		      Overrides.Add(Override)
		    Next
		  ElseIf PressedOverride.Mode = Ark.SpawnPointOverride.ModeOverride Then
		    Overrides.Add(PressedOverride)
		  End If
		  
		  Var Tag As New Dictionary
		  Tag.Value("Action") = "Load Defaults"
		  Tag.Value("Overrides") = Overrides
		  
		  Var LoadDefaultItem As New DesktopMenuItem("Load Default Contents", Tag)
		  LoadDefaultItem.Enabled = Overrides.Count > 0
		  Base.AddMenu(LoadDefaultItem)
		  
		  Return True
		End Function
	#tag EndEvent
#tag EndEvents
#tag Events Editor
	#tag Event
		Function GetProject() As Ark.Project
		  Return Self.Project
		End Function
	#tag EndEvent
	#tag Event
		Sub Changed()
		  Var Config As Ark.Configs.SpawnPoints = Self.Config(True)
		  Var Overrides() As Ark.SpawnPointOverride = Me.Overrides
		  Var PathMap As New Dictionary
		  For Each Override As Ark.SpawnPointOverride In Overrides
		    Config.Add(Override)
		    PathMap.Value(Override.UniqueKey) = Override
		  Next
		  For I As Integer = 0 To Self.List.RowCount - 1
		    Var Override As Ark.SpawnPointOverride = Self.List.RowTagAt(I)
		    If Not PathMap.HasKey(Override.UniqueKey) Then
		      Continue
		    End If
		    
		    Var NewPoint As Ark.SpawnPointOverride = PathMap.Value(Override.UniqueKey)
		    Self.List.RowTagAt(I) = NewPoint
		  Next
		  
		  Self.Modified = Self.Config(False).Modified
		  
		  #if false
		    Var Simulator As ArkSpawnSimulatorWindow = Self.SimulatorWindow(False)
		    If (Simulator Is Nil) = False Then
		      Simulator.RunSimulator()
		    End If
		  #endif
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events ConfigToolbar
	#tag Event
		Sub ItemPressed(Item As OmniBarItem, ItemRect As Rect)
		  #Pragma Unused ItemRect
		  
		  Select Case Item.Name
		  Case "AddButton"
		    Var Overrides() As Ark.SpawnPointOverride = ArkAddSpawnPointDialog.Present(Self, Self.Project)
		    If Overrides.Count = 0 Then
		      Return
		    End If
		    
		    Var Config As Ark.Configs.SpawnPoints = Self.Config(True)
		    For Each Override As Ark.SpawnPointOverride In Overrides
		      Config.Add(Override)
		    Next
		    
		    Self.Modified = Config.Modified
		    Self.UpdateList(Overrides)
		  Case "DuplicateButton"
		    Var TargetOverrides() As Ark.SpawnPointOverride = ArkAddSpawnPointDialog.Present(Self, Self.Project, ArkAddSpawnPointDialog.UIModeDuplicate)
		    If TargetOverrides.Count = 0 Then
		      Return
		    End If
		    
		    Var SourceOverride As Ark.SpawnPointOverride = Self.List.RowTagAt(Self.List.SelectedRowIndex)
		    Var Config As Ark.Configs.SpawnPoints = Self.Config(True)
		    Var NewOverrides() As Ark.SpawnPointOverride
		    For Each Target As Ark.SpawnPointOverride In TargetOverrides
		      Var Override As New Ark.MutableSpawnPointOverride(SourceOverride)
		      Override.SpawnPointReference = Target.SpawnPointReference
		      Config.Add(Override)
		      NewOverrides.Add(Override)
		    Next
		    
		    Self.Modified = Config.Modified
		    Self.UpdateList(NewOverrides)
		  End Select
		End Sub
	#tag EndEvent
	#tag Event
		Sub Opening()
		  Me.Append(OmniBarItem.CreateTitle("EditorTitle", Self.ConfigLabel))
		  Me.Append(OmniBarItem.CreateSeparator("EditorTitleSeparator"))
		  Me.Append(OmniBarItem.CreateButton("AddButton", "New Point", IconToolbarAdd, "Override a spawn point."))
		  Me.Append(OmniBarItem.CreateButton("DuplicateButton", "Duplicate", IconToolbarClone, "Duplicate the selected spawn point.", False))
		  Me.Append(OmniBarItem.CreateFlexibleSpace)
		  Me.Append(OmniBarItem.CreateHorizontalResizer("Resizer"))
		  
		  Me.Item("EditorTitle").Priority = 5
		  Me.Item("EditorTitleSeparator").Priority = 5
		End Sub
	#tag EndEvent
	#tag Event
		Sub Resize(DraggedResizer As OmniBarItem, DeltaX As Integer, DeltaY As Integer)
		  #Pragma Unused DraggedResizer
		  #Pragma Unused DeltaY
		  
		  Self.SetListWidth(Me.Width + DeltaX)
		End Sub
	#tag EndEvent
	#tag Event
		Sub ResizeFinished(DraggedResizer As OmniBarItem)
		  #Pragma Unused DraggedResizer
		  
		  Preferences.SpawnPointsSplitterPosition = Self.List.Width
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events FilterField
	#tag Event
		Sub TextChanged()
		  If Self.SettingUp Then
		    Return
		  End If
		  
		  Self.UpdateList()
		End Sub
	#tag EndEvent
#tag EndEvents
#tag ViewBehavior
	#tag ViewProperty
		Name="Modified"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Composited"
		Visible=true
		Group="Window Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Index"
		Visible=true
		Group="ID"
		InitialValue="-2147483648"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="IsFrontmost"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="ViewTitle"
		Visible=true
		Group="Behavior"
		InitialValue="Untitled"
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="ViewIcon"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Progress"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Double"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimumWidth"
		Visible=true
		Group="Behavior"
		InitialValue="400"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimumHeight"
		Visible=true
		Group="Behavior"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Name"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Super"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Width"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Height"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="InitialParent"
		Visible=false
		Group="Position"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Left"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Top"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockLeft"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockTop"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockRight"
		Visible=true
		Group="Position"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockBottom"
		Visible=true
		Group="Position"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabIndex"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabPanelIndex"
		Visible=false
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabStop"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowAutoDeactivate"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Enabled"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Tooltip"
		Visible=true
		Group="Appearance"
		InitialValue=""
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocusRing"
		Visible=true
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Visible"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="BackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="&hFFFFFF"
		Type="ColorGroup"
		EditorType="ColorGroup"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Backdrop"
		Visible=true
		Group="Background"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasBackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocus"
		Visible=true
		Group="Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowTabs"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Transparent"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
#tag EndViewBehavior
