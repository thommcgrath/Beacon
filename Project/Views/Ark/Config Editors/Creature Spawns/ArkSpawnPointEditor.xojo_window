#tag DesktopWindow
Begin BeaconContainer ArkSpawnPointEditor
   AllowAutoDeactivate=   True
   AllowFocus      =   False
   AllowFocusRing  =   False
   AllowTabs       =   True
   Backdrop        =   0
   BackgroundColor =   &cFFFFFF00
   Composited      =   False
   DoubleBuffer    =   "False"
   Enabled         =   True
   EraseBackground =   "True"
   HasBackgroundColor=   False
   Height          =   664
   Index           =   -2147483648
   InitialParent   =   ""
   Left            =   0
   LockBottom      =   True
   LockLeft        =   True
   LockRight       =   True
   LockTop         =   True
   TabIndex        =   0
   TabPanelIndex   =   0
   TabStop         =   True
   Tooltip         =   ""
   Top             =   0
   Transparent     =   False
   Visible         =   True
   Width           =   906
   Begin FadedSeparator ColumnSeparator
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   664
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   200
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   1
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   True
      Visible         =   True
      Width           =   1
   End
   Begin DesktopPagePanel Pages
      AllowAutoDeactivate=   True
      Enabled         =   True
      Height          =   664
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   201
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   True
      LockTop         =   True
      PanelCount      =   2
      Panels          =   ""
      Scope           =   2
      SelectedPanelIndex=   0
      TabIndex        =   2
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   False
      Value           =   0
      Visible         =   True
      Width           =   705
      Begin ArkSpawnPointSetEditor SetEditor
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   False
         AllowTabs       =   True
         Backdrop        =   0
         BackgroundColor =   &cFFFFFF00
         Composited      =   False
         Enabled         =   True
         HasBackgroundColor=   False
         Height          =   664
         Index           =   -2147483648
         InitialParent   =   "Pages"
         Left            =   201
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Modified        =   False
         Scope           =   2
         TabIndex        =   0
         TabPanelIndex   =   2
         TabStop         =   True
         Tooltip         =   ""
         Top             =   0
         Transparent     =   True
         Visible         =   True
         Width           =   705
      End
      Begin LogoFillCanvas NoSelectionFillCanvas
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   True
         AllowTabs       =   False
         Backdrop        =   0
         Caption         =   "No Selection"
         ContentHeight   =   0
         Enabled         =   True
         Height          =   643
         Index           =   -2147483648
         InitialParent   =   "Pages"
         Left            =   201
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Scope           =   2
         ScrollActive    =   False
         ScrollingEnabled=   False
         ScrollSpeed     =   20
         TabIndex        =   0
         TabPanelIndex   =   1
         TabStop         =   True
         Tooltip         =   ""
         Top             =   0
         Transparent     =   True
         Visible         =   True
         Width           =   705
      End
      Begin StatusBar NoSelectionStatusBar
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   True
         AllowTabs       =   False
         Backdrop        =   0
         Borders         =   1
         Caption         =   ""
         ContentHeight   =   0
         Enabled         =   True
         Height          =   21
         Index           =   -2147483648
         InitialParent   =   "Pages"
         Left            =   201
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   False
         Scope           =   2
         ScrollActive    =   False
         ScrollingEnabled=   False
         ScrollSpeed     =   20
         TabIndex        =   1
         TabPanelIndex   =   1
         TabStop         =   True
         Tooltip         =   ""
         Top             =   643
         Transparent     =   True
         Visible         =   True
         Width           =   705
      End
   End
   Begin StatusBar SetsStatusBar
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      Borders         =   3
      Caption         =   ""
      ContentHeight   =   0
      Enabled         =   True
      Height          =   22
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   False
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   4
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   349
      Transparent     =   True
      Visible         =   True
      Width           =   200
   End
   Begin StatusBar LimitsStatusBar
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      Borders         =   1
      Caption         =   ""
      ContentHeight   =   0
      Enabled         =   True
      Height          =   21
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   False
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   7
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   643
      Transparent     =   True
      Visible         =   True
      Width           =   200
   End
   Begin BeaconListbox SetsList
      AllowAutoDeactivate=   True
      AllowAutoHideScrollbars=   True
      AllowExpandableRows=   False
      AllowFocusRing  =   False
      AllowInfiniteScroll=   False
      AllowResizableColumns=   False
      AllowRowDragging=   False
      AllowRowReordering=   False
      Bold            =   False
      ColumnCount     =   1
      ColumnWidths    =   ""
      DefaultRowHeight=   26
      DefaultSortColumn=   0
      DefaultSortDirection=   0
      DropIndicatorVisible=   False
      EditCaption     =   "Edit"
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      GridLineStyle   =   0
      HasBorder       =   False
      HasHeader       =   False
      HasHorizontalScrollbar=   False
      HasVerticalScrollbar=   True
      HeadingIndex    =   -1
      Height          =   308
      Index           =   -2147483648
      InitialParent   =   ""
      InitialValue    =   ""
      Italic          =   False
      Left            =   0
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      PageSize        =   100
      PreferencesKey  =   ""
      RequiresSelection=   False
      RowSelectionType=   1
      Scope           =   2
      TabIndex        =   3
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   41
      Transparent     =   False
      TypeaheadColumn =   0
      Underline       =   False
      Visible         =   True
      VisibleRowCount =   0
      Width           =   200
      _ScrollOffset   =   0
      _ScrollWidth    =   -1
   End
   Begin BeaconListbox LimitsList
      AllowAutoDeactivate=   True
      AllowAutoHideScrollbars=   True
      AllowExpandableRows=   False
      AllowFocusRing  =   False
      AllowInfiniteScroll=   False
      AllowResizableColumns=   False
      AllowRowDragging=   False
      AllowRowReordering=   False
      Bold            =   False
      ColumnCount     =   2
      ColumnWidths    =   "*,75"
      DefaultRowHeight=   26
      DefaultSortColumn=   0
      DefaultSortDirection=   0
      DropIndicatorVisible=   False
      EditCaption     =   "Edit"
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      GridLineStyle   =   0
      HasBorder       =   False
      HasHeader       =   False
      HasHorizontalScrollbar=   False
      HasVerticalScrollbar=   True
      HeadingIndex    =   -1
      Height          =   231
      Index           =   -2147483648
      InitialParent   =   ""
      InitialValue    =   ""
      Italic          =   False
      Left            =   0
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   False
      PageSize        =   100
      PreferencesKey  =   ""
      RequiresSelection=   False
      RowSelectionType=   1
      Scope           =   2
      TabIndex        =   6
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   412
      Transparent     =   False
      TypeaheadColumn =   0
      Underline       =   False
      Visible         =   True
      VisibleRowCount =   0
      Width           =   200
      _ScrollOffset   =   0
      _ScrollWidth    =   -1
   End
   Begin OmniBar LimitsToolbar
      Alignment       =   0
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      BackgroundColor =   ""
      ContentHeight   =   0
      Enabled         =   True
      Height          =   41
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LeftPadding     =   -1
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      RightPadding    =   -1
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   8
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   371
      Transparent     =   True
      Visible         =   True
      Width           =   200
   End
   Begin OmniBar SetsToolbar
      Alignment       =   0
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      BackgroundColor =   ""
      ContentHeight   =   0
      Enabled         =   True
      Height          =   41
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LeftPadding     =   -1
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      RightPadding    =   -1
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   9
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   True
      Visible         =   True
      Width           =   200
   End
End
#tag EndDesktopWindow

#tag WindowCode
	#tag Event
		Sub ContentsChanged()
		  RaiseEvent Changed
		End Sub
	#tag EndEvent

	#tag Event
		Sub Resize(Initial As Boolean)
		  #Pragma Unused Initial
		  
		  Self.SetsListWidth = Preferences.ArkSpawnPointEditorSetsSplitterPosition
		  Self.LimitsListHeight = Preferences.ArkSpawnPointEditorLimitsSplitterPosition
		  
		  Var Resizer As OmniBarItem = Self.SetsToolbar.Item("Resizer")
		  If (Resizer Is Nil) = False Then
		    Resizer.Enabled = Self.Width > Self.MinEditorWidth
		  End If
		End Sub
	#tag EndEvent


	#tag Method, Flags = &h21
		Private Function LimitsListHeight() As Integer
		  Return Self.Height - Self.LimitsToolbar.Top
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub LimitsListHeight(Assigns Value As Integer)
		  // Remember, this works BACKWARDS so a value of 100 means 100 from the bottom of the window
		  
		  Const SetsListMinHeight = 200
		  
		  Var MinTop As Integer = SetsListMinHeight
		  Var MaxTop As Integer = Self.Height - LimitsListMinHeight
		  Var Top As Integer = Max(Min(Self.Height - Value, MaxTop), MinTop)
		  Value = Self.Height - Top
		  
		  Self.LimitsToolbar.Top = Top
		  Self.LimitsList.Top = Self.LimitsToolbar.Top + Self.LimitsToolbar.Height
		  Self.LimitsList.Height = Self.LimitsStatusBar.Top - Self.LimitsList.Top
		  
		  Self.SetsStatusBar.Top = Top - Self.SetsStatusBar.Height
		  Self.SetsList.Height = Self.SetsStatusBar.Top - Self.SetsList.Top
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Shared Function MinEditorWidth() As Integer
		  Return SetsListMinWidth + ArkSpawnPointSetEditor.MinEditorWidth
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub PresentLimitsDialog()
		  Var SelectedCreatures() As Ark.Creature
		  Self.PresentLimitsDialog(SelectedCreatures)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub PresentLimitsDialog(SelectedCreatures() As Ark.Creature)
		  Var DefinedCreatures() As Ark.Creature
		  Var CommonLimit As NullableDouble
		  Var CreatureFilter As New Dictionary
		  
		  For Each Point As Ark.SpawnPoint In Self.mSpawnPoints
		    Var Limits As Dictionary = Point.Limits
		    For Each Entry As DictionaryEntry In Limits
		      Var Creature As Ark.Creature = Entry.Key
		      If DefinedCreatures.IndexOf(Creature) = -1 Then
		        DefinedCreatures.Add(Creature)
		      End If
		      If SelectedCreatures.IndexOf(Creature) > -1 Then
		        Var CreatureLimit As Double = Entry.Value
		        If CommonLimit = Nil Then
		          CommonLimit = CreatureLimit
		        ElseIf CommonLimit <> -1.0 And CommonLimit <> CreatureLimit Then
		          CommonLimit = -1.0
		        End If
		      End If
		    Next
		    
		    Var Bound As Integer = Point.Count - 1
		    For I As Integer = 0 To Bound
		      Var Set As Ark.SpawnPointSet = Point.Set(I)
		      Var Entries() As Ark.SpawnPointSetEntry = Set.Entries
		      For Each Entry As Ark.SpawnPointSetEntry In Entries
		        Var Creature As Ark.Creature = Entry.Creature
		        If (Creature Is Nil) = False THen
		          CreatureFilter.Value(Creature.ObjectID) = Creature
		        End If
		      Next
		    Next
		  Next
		  
		  Var CreaturesInSpawnPoint() As Ark.Creature
		  For Each Entry As DictionaryEntry In CreatureFilter
		    CreaturesInSpawnPoint.Add(Entry.Value)
		  Next
		  
		  If CommonLimit <> Nil And CommonLimit = -1.0 Then
		    CommonLimit = Nil
		  End If
		  
		  Var Limit As NullableDouble = ArkSpawnPointLimitDialog.Present(Self, Self.Project.ContentPacks, CommonLimit, SelectedCreatures, DefinedCreatures, CreaturesInSpawnPoint)
		  If Limit <> Nil Then
		    For Each Point As Ark.MutableSpawnPoint In Self.mSpawnPoints
		      For Each Creature As Ark.Creature In SelectedCreatures
		        Point.Limit(Creature) = Limit
		      Next
		    Next
		    
		    Self.UpdateUI
		    RaiseEvent Changed
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function Project() As Ark.Project
		  Return RaiseEvent GetProject
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function SetsListWidth() As Integer
		  Return Self.SetsList.Width
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub SetsListWidth(Assigns Value As Integer)
		  Var ListWidth, EditorWidth As Integer
		  If Self.Width <= Self.MinEditorWidth Then
		    ListWidth = Self.SetsListMinWidth
		    EditorWidth = ArkSpawnPointSetEditor.MinEditorWidth
		  Else
		    Var AvailableSpace As Integer = Self.Width - Self.ColumnSeparator.Width
		    ListWidth = Min(Max(Value, Self.SetsListMinWidth), AvailableSpace - ArkSpawnPointSetEditor.MinEditorWidth)
		    EditorWidth = AvailableSpace - ListWidth
		  End If
		  
		  Self.SetsList.Width = ListWidth
		  Self.SetsToolbar.Width = ListWidth
		  Self.SetsStatusBar.Width = ListWidth
		  Self.ColumnSeparator.Left = ListWidth
		  Self.LimitsList.Width = ListWidth
		  Self.LimitsToolbar.Width = ListWidth
		  Self.LimitsStatusBar.Width = ListWidth
		  Self.Pages.Left = Self.ColumnSeparator.Left + Self.ColumnSeparator.Width
		  Self.Pages.Width = EditorWidth
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Function SpawnPoints() As Ark.SpawnPoint()
		  Var Points() As Ark.SpawnPoint
		  Points.ResizeTo(Self.mSpawnPoints.LastIndex)
		  For Idx As Integer = 0 To Self.mSpawnPoints.LastIndex
		    Points(Idx) = New Ark.SpawnPoint(Self.mSpawnPoints(Idx))
		  Next
		  Return Points
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub SpawnPoints(Assigns Points() As Ark.SpawnPoint)
		  If Points = Nil Then
		    Self.mSpawnPoints.ResizeTo(-1)
		  Else
		    Self.mSpawnPoints.ResizeTo(Points.LastIndex)
		    For I As Integer = 0 To Points.LastIndex
		      Self.mSpawnPoints(I) = New Ark.MutableSpawnPoint(Points(I))
		    Next
		  End If
		  
		  Self.UpdateUI
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateLimitsList(SelectCreatures() As Ark.Creature = Nil)
		  Self.LimitsList.SelectionChangeBlocked = True
		  
		  Const MixedLimitValue = -1.0
		  
		  Var CombinedLimits As Dictionary
		  
		  For Each Point As Ark.MutableSpawnPoint In Self.mSpawnPoints
		    Var PointLimits As Dictionary = Point.Limits
		    
		    If CombinedLimits = Nil Then
		      CombinedLimits = New Dictionary
		      For Each Entry As DictionaryEntry In PointLimits
		        Var Creature As Ark.Creature = Entry.Key
		        Var Limit As Double = Entry.Value
		        CombinedLimits.Value(Creature.ObjectID) = Limit
		      Next
		    Else
		      For Each Entry As DictionaryEntry In PointLimits
		        Var Creature As Ark.Creature = Entry.Key
		        Var Limit As Double = Entry.Value
		        
		        If CombinedLimits.HasKey(Creature.ObjectID) = False Then
		          CombinedLimits.Value(Creature.ObjectID) = Limit
		        ElseIf CombinedLimits.Value(Creature.ObjectID).DoubleValue <> Limit Then
		          CombinedLimits.Value(Creature.ObjectID) = MixedLimitValue
		        End If
		      Next
		    End If
		  Next
		  
		  If CombinedLimits <> Nil And CombinedLimits.KeyCount > 0 Then
		    Var SelectedCreatures() As String
		    If SelectCreatures <> Nil Then
		      For Each Creature As Ark.Creature In SelectCreatures
		        SelectedCreatures.Add(Creature.ObjectID)
		      Next
		    Else
		      For I As Integer = 0 To Self.LimitsList.RowCount - 1
		        If Self.LimitsList.RowSelectedAt(I) Then
		          SelectedCreatures.Add(Self.LimitsList.RowTagAt(I))
		        End If
		      Next
		    End If
		    
		    Self.LimitsList.RowCount = CombinedLimits.KeyCount
		    For RowIndex As Integer = 0 To CombinedLimits.KeyCount - 1
		      Var UUID As String = CombinedLimits.Key(RowIndex)
		      Var Limit As Double = CombinedLimits.Value(UUID)
		      Var Creature As Ark.Creature = Ark.DataSource.Pool.Get(False).GetCreatureByUUID(UUID)
		      If (Creature Is Nil) Then
		        Self.LimitsList.CellTextAt(RowIndex, 0) = "Unknown Creature"
		      Else
		        Self.LimitsList.CellTextAt(RowIndex, 0) = Creature.Label
		      End If
		      
		      Self.LimitsList.CellTextAt(RowIndex, 1) = If(Limit = MixedLimitValue, "Mixed", Beacon.PrettyText(Limit * 100, 2) + "%")
		      Self.LimitsList.RowTagAt(RowIndex) = UUID
		      Self.LimitsList.RowSelectedAt(RowIndex) = SelectedCreatures.IndexOf(UUID) > -1
		    Next
		  Else
		    Self.LimitsList.RowCount = 0
		  End If
		  
		  Self.UpdateLimitsStatus
		  
		  Self.LimitsList.SortingColumn = 0
		  Self.LimitsList.Sort
		  Self.LimitsList.SelectionChangeBlocked = False
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateLimitsStatus()
		  If Self.LimitsList.SelectedRowCount > 0 Then
		    Self.LimitsStatusBar.Caption = Self.LimitsList.SelectedRowCount.ToString + " of " + Language.NounWithQuantity(Self.LimitsList.RowCount, "Limit", "Limits") + " Selected"
		  Else
		    Self.LimitsStatusBar.Caption = Language.NounWithQuantity(Self.LimitsList.RowCount, "Limit", "Limits")
		  End If
		  
		  Var EditButton As OmniBarItem = Self.LimitsToolbar.Item("EditButton")
		  If (EditButton Is Nil) = False Then
		    EditButton.Enabled = Self.LimitsList.SelectedRowCount > 0
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateSetsList(SelectSets() As Ark.SpawnPointSet = Nil)
		  Self.SetsList.SelectionChangeBlocked = True
		  
		  Var CombinedSets As New Dictionary
		  For Each Point As Ark.MutableSpawnPoint In Self.mSpawnPoints
		    For Each Set As Ark.SpawnPointSet In Point
		      Var Hash As String = Set.Hash
		      Var Organizer As Ark.SpawnSetOrganizer
		      If CombinedSets.HasKey(Hash) Then
		        Organizer = CombinedSets.Value(Hash)
		      Else
		        Organizer = New Ark.SpawnSetOrganizer(Set)
		        CombinedSets.Value(Hash) = Organizer
		      End If
		      Organizer.Attach(Point, Set)
		    Next
		  Next
		  
		  If CombinedSets <> Nil And CombinedSets.KeyCount > 0 Then
		    Var SelectedSets() As String
		    If SelectSets = Nil Then
		      For I As Integer = 0 To Self.SetsList.RowCount - 1
		        If Self.SetsList.RowSelectedAt(I) Then
		          SelectedSets.Add(Ark.SpawnSetOrganizer(Self.SetsList.RowTagAt(I)).Template.SetId)
		        End If
		      Next
		    Else
		      For Each Set As Ark.SpawnPointSet In SelectSets
		        SelectedSets.Add(Set.Hash)
		      Next
		    End If
		    
		    Var ExtendedLabels As Boolean = Self.mSpawnPoints.LastIndex > 0
		    
		    Self.SetsList.RowCount = CombinedSets.KeyCount
		    Self.SetsList.DefaultRowHeight = If(ExtendedLabels, 34, 26)
		    For RowIndex As Integer = 0 To CombinedSets.KeyCount - 1
		      Var Hash As String = CombinedSets.Key(RowIndex)
		      Var Organizer As Ark.SpawnSetOrganizer = CombinedSets.Value(Hash)
		      
		      Self.SetsList.CellTextAt(RowIndex, 0) = Organizer.Label(ExtendedLabels)
		      Self.SetsList.RowTagAt(RowIndex) = Organizer
		      Self.SetsList.RowSelectedAt(RowIndex) = Organizer.Matches(SelectedSets)
		    Next
		  Else
		    Self.SetsList.RowCount = 0
		  End If
		  
		  Self.UpdateSetsStatus
		  
		  Self.SetsList.SortingColumn = 0
		  Self.SetsList.Sort
		  Self.SetsList.SelectionChangeBlocked = False
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateSetsStatus()
		  If Self.SetsList.SelectedRowCount > 0 Then
		    Self.SetsStatusBar.Caption = Self.SetsList.SelectedRowCount.ToString + " of " + Language.NounWithQuantity(Self.SetsList.RowCount, "Spawn Set", "Spawn Sets") + " Selected"
		  Else
		    Self.SetsStatusBar.Caption = Language.NounWithQuantity(Self.SetsList.RowCount, "Spawn Set", "Spawn Sets")
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateUI()
		  Self.UpdateSetsList()
		  Self.UpdateLimitsList()
		End Sub
	#tag EndMethod


	#tag Hook, Flags = &h0
		Event Changed()
	#tag EndHook

	#tag Hook, Flags = &h0
		Event GetProject() As Ark.Project
	#tag EndHook


	#tag Property, Flags = &h21
		Private mSpawnPoints() As Ark.MutableSpawnPoint
	#tag EndProperty


	#tag Constant, Name = kLimitsClipboardType, Type = String, Dynamic = False, Default = \"com.thezaz.beacon.ark.spawn.limit", Scope = Private
	#tag EndConstant

	#tag Constant, Name = kSetsClipboardType, Type = String, Dynamic = False, Default = \"com.thezaz.beacon.ark.spawn.set", Scope = Private
	#tag EndConstant

	#tag Constant, Name = LimitsListDefaultHeight, Type = Double, Dynamic = False, Default = \"192", Scope = Public
	#tag EndConstant

	#tag Constant, Name = LimitsListMinHeight, Type = Double, Dynamic = False, Default = \"150", Scope = Public
	#tag EndConstant

	#tag Constant, Name = PageNoSelection, Type = Double, Dynamic = False, Default = \"0", Scope = Private
	#tag EndConstant

	#tag Constant, Name = PageSetEditor, Type = Double, Dynamic = False, Default = \"1", Scope = Private
	#tag EndConstant

	#tag Constant, Name = SetsListDefaultWidth, Type = Double, Dynamic = False, Default = \"310", Scope = Public
	#tag EndConstant

	#tag Constant, Name = SetsListMinWidth, Type = Double, Dynamic = False, Default = \"225", Scope = Public
		#Tag Instance, Platform = Windows, Language = Default, Definition  = \"217"
	#tag EndConstant


#tag EndWindowCode

#tag Events SetEditor
	#tag Event
		Sub Changed()
		  If Self.SetsList.SelectedRowCount <> 1 Then
		    Break
		    Return
		  End If
		  
		  Var Organizer As Ark.SpawnSetOrganizer = Self.SetsList.RowTagAt(Self.SetsList.SelectedRowIndex)
		  Organizer.Replicate()
		  
		  Self.SetsList.CellTextAt(Self.SetsList.SelectedRowIndex, 0) = Organizer.Label(Self.mSpawnPoints.LastIndex > 0)
		  
		  RaiseEvent Changed
		End Sub
	#tag EndEvent
	#tag Event
		Function GetProject() As Ark.Project
		  Return Self.Project
		End Function
	#tag EndEvent
#tag EndEvents
#tag Events SetsList
	#tag Event
		Sub SelectionChanged()
		  Self.UpdateSetsStatus()
		  
		  If Me.SelectedRowCount = 1 Then
		    Self.Pages.SelectedPanelIndex = Self.PageSetEditor
		    Self.SetEditor.SpawnSet = Ark.SpawnSetOrganizer(Me.RowTagAt(Me.SelectedRowIndex)).Template
		    Return
		  End If
		  
		  Self.SetEditor.SpawnSet = Nil
		  Self.Pages.SelectedPanelIndex = Self.PageNoSelection
		  Self.NoSelectionFillCanvas.Caption = If(Me.SelectedRowCount = 0, "No Selection", "Multiple Selection")
		End Sub
	#tag EndEvent
	#tag Event
		Function CanDelete() As Boolean
		  Return Me.SelectedRowCount > 0
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformClear(Warn As Boolean)
		  Var Organizers() As Ark.SpawnSetOrganizer
		  Var Bound As Integer = Me.RowCount - 1
		  For I As Integer = 0 To Bound
		    If Me.RowSelectedAt(I) = False Then
		      Continue
		    End If
		    
		    Organizers.Add(Me.RowTagAt(I))
		  Next
		  
		  If Warn And Self.ShowDeleteConfirmation(Organizers, "spawn set", "spawn sets") = False Then
		    Return
		  End If
		  
		  For Each Organizer As Ark.SpawnSetOrganizer In Organizers
		    Var Points() As Ark.MutableSpawnPoint = Organizer.Points
		    For Each Point As Ark.MutableSpawnPoint In Points
		      Var Set As Ark.SpawnPointSet = Organizer.SetForPoint(Point)
		      Point.RemoveSet(Set)
		    Next
		  Next
		  
		  RaiseEvent Changed
		  Self.UpdateSetsList()
		End Sub
	#tag EndEvent
	#tag Event
		Function CanCopy() As Boolean
		  Return Me.SelectedRowCount > 0
		End Function
	#tag EndEvent
	#tag Event
		Function CanPaste(Board As Clipboard) As Boolean
		  Return Board.RawDataAvailable(Self.kSetsClipboardType) Or (Board.TextAvailable And Board.Text.IndexOf("""Type"": ""SpawnPointSet""") > -1)
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformCopy(Board As Clipboard)
		  Var Items() As Dictionary
		  For I As Integer = 0 To Me.RowCount - 1
		    If Not Me.RowSelectedAt(I) Then
		      Continue
		    End If
		    
		    Var Organizer As Ark.SpawnSetOrganizer = Me.RowTagAt(I)
		    Items.Add(Organizer.Template.SaveData)
		  Next
		  
		  Var JSON As String = Beacon.GenerateJSON(Items, True)
		  Board.Text = JSON
		  Board.RawData(Self.kSetsClipboardType) = JSON
		End Sub
	#tag EndEvent
	#tag Event
		Sub PerformPaste(Board As Clipboard)
		  If Not Me.CanPaste Then
		    Return
		  End If
		  
		  Var Items() As Variant
		  Try
		    If Board.RawDataAvailable(Self.kSetsClipboardType) Then
		      Items = Beacon.ParseJSON(Board.RawData(Self.kSetsClipboardType))
		    Else
		      Items = Beacon.ParseJSON(Board.Text)
		    End If
		  Catch Err As RuntimeException
		    Return
		  End Try
		  
		  Var SelectSets() As Ark.SpawnPointSet
		  For Each Item As Dictionary In Items
		    Var Set As Ark.SpawnPointSet = Ark.SpawnPointSet.FromSaveData(Item)
		    If Set = Nil Then
		      Continue
		    End If
		    
		    For Each Point As Ark.MutableSpawnPoint In Self.mSpawnPoints
		      Point.AddSet(Set)
		    Next
		    
		    SelectSets.Add(Set)
		  Next
		  
		  RaiseEvent Changed
		  Self.UpdateSetsList(SelectSets)
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events LimitsList
	#tag Event
		Sub Opening()
		  Me.ColumnAlignmentAt(1) = DesktopListbox.Alignments.Right
		End Sub
	#tag EndEvent
	#tag Event
		Sub SelectionChanged()
		  Self.UpdateLimitsStatus
		End Sub
	#tag EndEvent
	#tag Event
		Sub DoublePressed()
		  Var SelectedCreatures() As Ark.Creature
		  For I As Integer = 0 To Self.LimitsList.RowCount - 1
		    If Self.LimitsList.RowSelectedAt(I) Then
		      SelectedCreatures.Add(Ark.DataSource.Pool.Get(False).GetCreatureByUUID(Self.LimitsList.RowTagAt(I).StringValue))
		    End If
		  Next
		  Self.PresentLimitsDialog(SelectedCreatures)
		End Sub
	#tag EndEvent
	#tag Event
		Function CanDelete() As Boolean
		  Return Me.SelectedRowCount > 0
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformClear(Warn As Boolean)
		  Var Creatures() As Ark.Creature
		  For I As Integer = 0 To Me.RowCount - 1
		    If Me.RowSelectedAt(I) = False Then
		      Continue
		    End If
		    
		    Var Creature As Ark.Creature = Ark.DataSource.Pool.Get(False).GetCreatureByUUID(Me.RowTagAt(I).StringValue)
		    If Creature Is Nil Then
		      Continue
		    End If
		    
		    Creatures.Add(Creature)
		  Next
		  
		  If Warn And Self.ShowDeleteConfirmation(Creatures, "creature limit", "creature limits") = False Then
		    Return
		  End If
		  
		  For Each Creature As Ark.Creature In Creatures
		    For Each Point As Ark.MutableSpawnPoint In Self.mSpawnPoints
		      Point.Limit(Creature) = 1.0
		    Next
		  Next
		  
		  RaiseEvent Changed
		  Self.UpdateLimitsList()
		End Sub
	#tag EndEvent
	#tag Event
		Function CanCopy() As Boolean
		  Return Me.SelectedRowCount > 0
		End Function
	#tag EndEvent
	#tag Event
		Function CanPaste(Board As Clipboard) As Boolean
		  Return Board.RawDataAvailable(Self.kLimitsClipboardType) Or (Board.TextAvailable And Board.Text.IndexOf("""Type"": ""SpawnPointLimit""") > -1)
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformCopy(Board As Clipboard)
		  Var Limits As New Dictionary
		  For I As Integer = 0 To Me.RowCount - 1
		    If Me.RowSelectedAt(I) = False Then
		      Continue
		    End If
		    
		    Var UUID As String = Me.RowTagAt(I)
		    Var Creature As Ark.Creature = Ark.DataSource.Pool.Get(False).GetCreatureByUUID(UUID)
		    If Creature Is Nil Then
		      Continue
		    End If
		    
		    Var CommonLimit As NullableDouble
		    For Each Point As Ark.SpawnPoint In Self.mSpawnPoints
		      Var Limit As NullableDouble = Point.Limit(Creature)
		      If Limit = Nil Or Limit = 1.0 Then
		        Continue
		      End If
		      
		      If CommonLimit = Nil Then
		        CommonLimit = Limit
		      ElseIf CommonLimit <> Limit Then
		        Continue For I
		      End If
		    Next
		    
		    If CommonLimit <> Nil Then
		      Limits.Value(Creature.ObjectID) = CommonLimit.IntegerValue
		    End If
		  Next
		  
		  If Limits.KeyCount = 0 Then
		    System.Beep
		    Return
		  End If
		  
		  Limits.Value("Type") = "SpawnPointLimit"
		  
		  Var JSON As String = Beacon.GenerateJSON(Limits, True).Trim
		  Board.Text = JSON
		  Board.RawData(Self.kLimitsClipboardType) = JSON
		End Sub
	#tag EndEvent
	#tag Event
		Sub PerformPaste(Board As Clipboard)
		  Var Dict As Dictionary
		  Try
		    If Board.RawDataAvailable(Self.kLimitsClipboardType) Then
		      Dict = Beacon.ParseJSON(Board.RawData(Self.kLimitsClipboardType))
		    Else
		      Dict = Beacon.ParseJSON(Board.Text)
		    End If
		  Catch Err As RuntimeException
		    System.Beep
		    Return
		  End Try
		  
		  Var SelectCreatures() As Ark.Creature
		  For Each Entry As DictionaryEntry In Dict
		    If Entry.Key = "SpawnPointLimit" Then
		      Continue
		    End If
		    
		    Var UUID As String = Entry.Key
		    Var Creature As Ark.Creature
		    Try
		      Creature = Ark.DataSource.Pool.Get(False).GetCreatureByUUID(UUID)
		    Catch Err As UnsupportedFormatException
		      Var Creatures() As Ark.Creature = Ark.DataSource.Pool.Get(False).GetCreaturesByPath(UUID, Self.Project.ContentPacks)
		      If Creatures.Count > 0 Then
		        Creature = Creatures(0)
		      End If
		    End Try
		    If Creature Is Nil Then
		      Continue
		    End If
		    SelectCreatures.Add(Creature)
		    
		    Var Limit As Double = Entry.Value
		    For Each Point As Ark.MutableSpawnPoint In Self.mSpawnPoints
		      Point.Limit(Creature) = Limit
		    Next
		  Next
		  
		  RaiseEvent Changed
		  Self.UpdateLimitsList(SelectCreatures)
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events LimitsToolbar
	#tag Event
		Sub ItemPressed(Item As OmniBarItem, ItemRect As Rect)
		  #Pragma Unused ItemRect
		  
		  Select Case Item.Name
		  Case "AddButton"
		    Self.PresentLimitsDialog()
		  Case "EditButton"
		    Var SelectedCreatures() As Ark.Creature
		    For I As Integer = 0 To Self.LimitsList.RowCount - 1
		      If Self.LimitsList.RowSelectedAt(I) Then
		        SelectedCreatures.Add(Ark.DataSource.Pool.Get(False).GetCreatureByUUID(Self.LimitsList.RowTagAt(I).StringValue))
		      End If
		    Next
		    Self.PresentLimitsDialog(SelectedCreatures)
		  End Select
		End Sub
	#tag EndEvent
	#tag Event
		Sub Opening()
		  Me.Append(OmniBarItem.CreateTitle("LimitsTitle", "Limits"))
		  Me.Append(OmniBarItem.CreateSeparator("LimitsTitleSeparator"))
		  Me.Append(OmniBarItem.CreateButton("AddButton", "New Limit", IconToolbarAdd, "Define a new creature limit."))
		  Me.Append(OmniBarItem.CreateButton("EditButton", "Edit", IconToolbarEdit, "Edit the selected creature limit or limits.", False))
		  Me.Append(OmniBarItem.CreateFlexibleSpace)
		  Me.Append(OmniBarItem.CreateVerticalResizer("Resizer"))
		  
		  Me.Item("LimitsTitle").Priority = 5
		  Me.Item("LimitsTitleSeparator").Priority = 5
		End Sub
	#tag EndEvent
	#tag Event
		Sub Resize(DraggedResizer As OmniBarItem, DeltaX As Integer, DeltaY As Integer)
		  #Pragma Unused DraggedResizer
		  #Pragma Unused DeltaX
		  
		  Self.LimitsListHeight = Self.LimitsListHeight - DeltaY
		End Sub
	#tag EndEvent
	#tag Event
		Sub ResizeFinished(DraggedResizer As OmniBarItem)
		  #Pragma Unused DraggedResizer
		  
		  Preferences.ArkSpawnPointEditorLimitsSplitterPosition = Self.LimitsListHeight
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events SetsToolbar
	#tag Event
		Sub ItemPressed(Item As OmniBarItem, ItemRect As Rect)
		  #Pragma Unused ItemRect
		  
		  Select Case Item.Name
		  Case "AddButton"
		    Var Organizer As New Ark.SpawnSetOrganizer
		    For Each Point As Ark.MutableSpawnPoint In Self.mSpawnPoints
		      Organizer.Attach(Point)
		    Next
		    
		    Self.SetsList.AddRow(Organizer.Label(Self.mSpawnPoints.LastIndex > 0))
		    Self.SetsList.RowTagAt(Self.SetsList.LastAddedRowIndex) = Organizer
		    Self.SetsList.SelectedRowIndex = Self.SetsList.LastAddedRowIndex
		    Self.SetsList.Sort
		    Self.SetsList.EnsureSelectionIsVisible
		  Case "WizardButton"
		    Var Organizer As New Ark.SpawnSetOrganizer
		    For Each Point As Ark.MutableSpawnPoint In Self.mSpawnPoints
		      Organizer.Attach(Point)
		    Next
		    
		    If ArkSpawnSetWizard.Present(Self, Organizer, Self.Project.ContentPacks) = False Then
		      Return
		    End If
		    
		    Self.SetsList.AddRow(Organizer.Label(Self.mSpawnPoints.LastIndex > 0))
		    Self.SetsList.RowTagAt(Self.SetsList.LastRowIndex) = Organizer
		    Self.SetsList.SelectedRowIndex = Self.SetsList.LastRowIndex
		    Self.SetsList.Sort
		    Self.SetsList.EnsureSelectionIsVisible
		    
		    Self.UpdateLimitsList
		    
		    RaiseEvent Changed
		  End Select
		End Sub
	#tag EndEvent
	#tag Event
		Sub Opening()
		  Me.Append(OmniBarItem.CreateTitle("SetsTitle", "Spawn Sets"))
		  Me.Append(OmniBarItem.CreateSeparator("SetsTitleSeparator"))
		  Me.Append(OmniBarItem.CreateButton("AddButton", "New Set", IconToolbarAdd, "Create a new spawn set."))
		  Me.Append(OmniBarItem.CreateButton("WizardButton", "Auto Creature", IconToolbarWizard, "Quickly add a creature to this spawn point using a wizard."))
		  Me.Append(OmniBarItem.CreateFlexibleSpace)
		  Me.Append(OmniBarItem.CreateHorizontalResizer("Resizer"))
		  
		  Me.Item("SetsTitle").Priority = 5
		  Me.Item("SetsTitleSeparator").Priority = 5
		End Sub
	#tag EndEvent
	#tag Event
		Sub Resize(DraggedResizer As OmniBarItem, DeltaX As Integer, DeltaY As Integer)
		  #Pragma Unused DraggedResizer
		  #Pragma Unused DeltaY
		  
		  Self.SetsListWidth = Self.SetsListWidth + DeltaX
		End Sub
	#tag EndEvent
	#tag Event
		Sub ResizeFinished(DraggedResizer As OmniBarItem)
		  #Pragma Unused DraggedResizer
		  
		  Preferences.ArkSpawnPointEditorSetsSplitterPosition = Self.SetsListWidth
		End Sub
	#tag EndEvent
#tag EndEvents
#tag ViewBehavior
	#tag ViewProperty
		Name="Modified"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Composited"
		Visible=true
		Group="Window Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Index"
		Visible=true
		Group="ID"
		InitialValue="-2147483648"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Name"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Super"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Width"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Height"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="InitialParent"
		Visible=false
		Group="Position"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Left"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Top"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockLeft"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockTop"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockRight"
		Visible=true
		Group="Position"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockBottom"
		Visible=true
		Group="Position"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabIndex"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabPanelIndex"
		Visible=false
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabStop"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowAutoDeactivate"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Enabled"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Tooltip"
		Visible=true
		Group="Appearance"
		InitialValue=""
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocusRing"
		Visible=true
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Visible"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="BackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="&hFFFFFF"
		Type="ColorGroup"
		EditorType="ColorGroup"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Backdrop"
		Visible=true
		Group="Background"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasBackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocus"
		Visible=true
		Group="Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowTabs"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Transparent"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
#tag EndViewBehavior
