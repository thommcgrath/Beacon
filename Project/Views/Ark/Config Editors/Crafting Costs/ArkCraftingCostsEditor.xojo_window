#tag DesktopWindow
Begin ArkConfigEditor ArkCraftingCostsEditor
   AllowAutoDeactivate=   True
   AllowFocus      =   False
   AllowFocusRing  =   False
   AllowTabs       =   True
   Backdrop        =   0
   BackgroundColor =   &cFFFFFF00
   Composited      =   False
   DoubleBuffer    =   "False"
   Enabled         =   True
   EraseBackground =   "True"
   HasBackgroundColor=   False
   Height          =   396
   Index           =   -2147483648
   InitialParent   =   ""
   Left            =   0
   LockBottom      =   True
   LockLeft        =   True
   LockRight       =   True
   LockTop         =   True
   TabIndex        =   0
   TabPanelIndex   =   0
   TabStop         =   True
   Tooltip         =   ""
   Top             =   0
   Transparent     =   True
   Visible         =   True
   Width           =   650
   Begin StatusBar ListStatusBar
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      Borders         =   1
      Caption         =   ""
      ContentHeight   =   0
      Enabled         =   True
      Height          =   21
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   False
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   2
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   375
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
   Begin BeaconListbox List
      AllowAutoDeactivate=   True
      AllowAutoHideScrollbars=   True
      AllowExpandableRows=   False
      AllowFocusRing  =   True
      AllowInfiniteScroll=   False
      AllowResizableColumns=   False
      AllowRowDragging=   False
      AllowRowReordering=   False
      Bold            =   False
      ColumnCount     =   1
      ColumnWidths    =   ""
      DefaultRowHeight=   26
      DefaultSortColumn=   0
      DefaultSortDirection=   0
      DropIndicatorVisible=   False
      EditCaption     =   "Edit"
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      GridLineStyle   =   0
      HasBorder       =   False
      HasHeader       =   False
      HasHorizontalScrollbar=   False
      HasVerticalScrollbar=   True
      HeadingIndex    =   0
      Height          =   293
      Index           =   -2147483648
      InitialParent   =   ""
      InitialValue    =   ""
      Italic          =   False
      Left            =   0
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      PreferencesKey  =   ""
      RequiresSelection=   False
      RowSelectionType=   0
      Scope           =   2
      TabIndex        =   3
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   82
      Transparent     =   True
      TypeaheadColumn =   0
      Underline       =   False
      Visible         =   True
      VisibleRowCount =   0
      Width           =   250
      _ScrollOffset   =   0
      _ScrollWidth    =   -1
   End
   Begin FadedSeparator ListSeparator
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   396
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   250
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   4
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   True
      Visible         =   True
      Width           =   1
   End
   Begin DesktopPagePanel Panel
      AllowAutoDeactivate=   True
      Enabled         =   True
      Height          =   396
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   251
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   True
      LockTop         =   True
      PanelCount      =   3
      Panels          =   ""
      Scope           =   2
      SelectedPanelIndex=   0
      TabIndex        =   5
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   False
      Value           =   1
      Visible         =   True
      Width           =   399
      Begin ArkCraftingCostEditor Editor
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   False
         AllowTabs       =   True
         Backdrop        =   0
         BackgroundColor =   &cFFFFFF
         Composited      =   False
         Enabled         =   True
         HasBackgroundColor=   False
         Height          =   396
         Index           =   -2147483648
         InitialParent   =   "Panel"
         Left            =   251
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Scope           =   2
         TabIndex        =   0
         TabPanelIndex   =   2
         TabStop         =   True
         Tooltip         =   ""
         Top             =   0
         Transparent     =   True
         Visible         =   True
         Width           =   399
      End
      Begin LogoFillCanvas FillCanvas
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   True
         AllowTabs       =   False
         Backdrop        =   0
         Caption         =   "No Selection"
         ContentHeight   =   0
         Enabled         =   True
         Height          =   396
         Index           =   -2147483648
         InitialParent   =   "Panel"
         Left            =   251
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Scope           =   2
         ScrollActive    =   False
         ScrollingEnabled=   False
         ScrollSpeed     =   20
         TabIndex        =   0
         TabPanelIndex   =   1
         TabStop         =   True
         Tooltip         =   ""
         Top             =   0
         Transparent     =   True
         Visible         =   True
         Width           =   399
      End
      Begin LogoFillCanvas LogoFillCanvas1
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   True
         AllowTabs       =   False
         Backdrop        =   0
         Caption         =   "Multiple Selection"
         ContentHeight   =   0
         Enabled         =   True
         Height          =   396
         Index           =   -2147483648
         InitialParent   =   "Panel"
         Left            =   251
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Scope           =   2
         ScrollActive    =   False
         ScrollingEnabled=   False
         ScrollSpeed     =   20
         TabIndex        =   0
         TabPanelIndex   =   3
         TabStop         =   True
         Tooltip         =   ""
         Top             =   0
         Transparent     =   True
         Visible         =   True
         Width           =   399
      End
   End
   Begin Thread FibercraftBuilderThread
      DebugIdentifier =   ""
      Index           =   -2147483648
      LockedInPosition=   False
      Priority        =   5
      Scope           =   2
      StackSize       =   0
      TabPanelIndex   =   0
      ThreadID        =   0
      ThreadState     =   0
   End
   Begin Thread AdjusterThread
      DebugIdentifier =   ""
      Index           =   -2147483648
      LockedInPosition=   False
      Priority        =   5
      Scope           =   2
      StackSize       =   0
      TabPanelIndex   =   0
      ThreadID        =   0
      ThreadState     =   0
   End
   Begin OmniBar ConfigToolbar
      Alignment       =   0
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      BackgroundColor =   ""
      ContentHeight   =   0
      Enabled         =   True
      Height          =   41
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LeftPadding     =   -1
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      RightPadding    =   10
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   6
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
   Begin OmniBarSeparator FilterSeparator
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   1
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   8
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   81
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
   Begin DelayedSearchField FilterField
      Active          =   False
      AllowAutoDeactivate=   True
      AllowFocusRing  =   True
      AllowRecentItems=   False
      ClearMenuItemValue=   "Clear"
      DelayPeriod     =   250
      Enabled         =   True
      Height          =   22
      Hint            =   "Filter Recipes"
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   9
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      MaximumRecentItems=   -1
      PanelIndex      =   0
      RecentItemsValue=   "Recent Searches"
      Scope           =   2
      TabIndex        =   9
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   ""
      Tooltip         =   ""
      Top             =   50
      Transparent     =   False
      Visible         =   True
      Width           =   232
      _mIndex         =   0
      _mInitialParent =   ""
      _mName          =   ""
      _mPanelIndex    =   0
   End
End
#tag EndDesktopWindow

#tag WindowCode
	#tag Event
		Sub Opening()
		  Self.SetListWidth(Self.ListMinWidth)
		  Self.MinimumWidth = Self.ListMinWidth + Self.ListSeparator.Width + Self.Editor.MinimumWidth
		End Sub
	#tag EndEvent

	#tag Event
		Function ParsingFinished(Project As Ark.Project) As Boolean
		  If Project Is Nil Or Project.HasConfigGroup(Ark.Configs.NameCraftingCosts) = False Then
		    Return True
		  End If
		  
		  Var OtherConfig As Ark.Configs.CraftingCosts = Ark.Configs.CraftingCosts(Project.ConfigGroup(Ark.Configs.NameCraftingCosts))
		  If OtherConfig = Nil Or OtherConfig.Count = 0 Then
		    Return True
		  End If
		  
		  Var Config As Ark.Configs.CraftingCosts = Self.Config(True)
		  Var NewCosts() As Ark.CraftingCost
		  Var NewEngrams() As Ark.Engram = OtherConfig.Engrams
		  For Each Engram As Ark.Engram In NewEngrams
		    Config.Cost(Engram) = OtherConfig.Cost(Engram)
		    NewCosts.Add(Config.Cost(Engram))
		  Next
		  
		  Self.Modified = True
		  Self.UpdateList(NewCosts)
		  
		  Return True
		End Function
	#tag EndEvent

	#tag Event
		Sub Resize(Initial As Boolean)
		  #Pragma Unused Initial
		  
		  Self.SetListWidth(Preferences.CraftingSplitterPosition)
		End Sub
	#tag EndEvent

	#tag Event
		Sub RunTool(Tool As Ark.ProjectTool)
		  Select Case Tool.UUID
		  Case "94eced5b-be7d-441a-a5b3-f4a9bf40a856"
		    Self.CreateFibercraftServer()
		  Case "24376f12-c256-440c-87ca-2c8309a7a754"
		    Self.AdjustCosts()
		  Case "3db64fe3-9134-4a19-a255-7712c8c70a83"
		    Self.SetupTransferrableElement()
		  Case "6600245b-54b4-4b85-8f26-3792084ca2fa"
		    If ArkAdjustIngredientDialog.Present(Self, Self.Project) Then
		      Self.SetupUI()
		    End If
		  End Select
		End Sub
	#tag EndEvent

	#tag Event
		Sub SetupUI()
		  Self.UpdateList()
		End Sub
	#tag EndEvent

	#tag Event
		Sub ShowIssue(Issue As Beacon.Issue)
		  If Issue = Nil Or Issue.UserData = Nil Then
		    Return
		  End If
		  
		  Try
		    Var Cost As Ark.CraftingCost = Issue.UserData
		    If Cost <> Nil Then
		      Self.UpdateList(Cost)
		    End If
		  Catch Err As RuntimeException
		    Return
		  End Try
		End Sub
	#tag EndEvent


	#tag Method, Flags = &h21
		Private Sub AdjustCosts()
		  // We need to adjust the currently defined costs, as well as inject new costs at the adjusted rate
		  Var Multiplier As Double = ArkAdjustCostDialog.Present(Self)
		  If Multiplier = 1.0 Then
		    Return
		  End If
		  
		  If Self.AdjusterThread.ThreadState <> Thread.ThreadStates.NotRunning Then
		    Return
		  End If
		  
		  Self.mCostMultiplier = Multiplier
		  Self.mProgressWindow = New ProgressWindow("Calculating new costs")
		  Self.mProgressWindow.Show(Self)
		  Self.AdjusterThread.Start
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h1
		Protected Function Config(ForWriting As Boolean) As Ark.Configs.CraftingCosts
		  Return Ark.Configs.CraftingCosts(Super.Config(ForWriting))
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub CreateFibercraftServer()
		  If Self.FibercraftBuilderThread.ThreadState <> Thread.ThreadStates.NotRunning Then
		    Return
		  End If
		  
		  Self.mProgressWindow = New ProgressWindow("Setting up fibercraft config")
		  Self.mProgressWindow.Show(Self)
		  Self.FibercraftBuilderThread.Start
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Function InternalName() As String
		  Return Ark.Configs.NameCraftingCosts
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub SetListWidth(NewSize As Integer)
		  If Self.Width < Self.MinimumWidth Then
		    // Don't compute anything
		    Return
		  End If
		  
		  Var AvailableSpace As Integer = Self.Width - Self.ListSeparator.Width
		  Var ListWidth As Integer = Min(Max(NewSize, Self.ListMinWidth), AvailableSpace - ArkCraftingCostEditor.MinimumWidth)
		  Var EditorWidth As Integer = AvailableSpace - ListWidth
		  
		  Self.ConfigToolbar.Width = ListWidth
		  Self.List.Width = ListWidth
		  Self.ListSeparator.Left = ListWidth
		  Self.ListStatusBar.Width = ListWidth
		  Self.FilterSeparator.Width = ListWidth
		  Self.FilterField.Width = ListWidth - (Self.FilterField.Left * 2)
		  Self.Panel.Left = Self.ListSeparator.Left + Self.ListSeparator.Width
		  Self.Panel.Width = EditorWidth
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub SetupTransferrableElement()
		  Var Config As Ark.Configs.CraftingCosts = Self.Config(False)
		  If ArkSetupTransferrableElementDialog.Present(Self, Config, Self.Project.ContentPacks) Then
		    Call Self.Config(True) // Forces adding the config to the file
		    Self.UpdateList()
		    Self.Modified = Config.Modified
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ShowAddEngram()
		  Var Config As Ark.Configs.CraftingCosts = Self.Config(False)
		  Var CurrentEngrams() As Ark.Engram = Config.Engrams
		  
		  Var WithDefaults As Boolean = True
		  Var NewEngrams() As Ark.Engram = ArkBlueprintSelectorDialog.Present(Self, "Crafting", CurrentEngrams, Self.Project.ContentPacks, ArkBlueprintSelectorDialog.SelectModes.ImpliedMultiple, WithDefaults)
		  If NewEngrams = Nil Or NewEngrams.LastIndex = -1 Then
		    Return
		  End If
		  
		  Config = Self.Config(True)
		  
		  Var NewCosts() As Ark.CraftingCost
		  For Each Engram As Ark.Engram In NewEngrams
		    Var Cost As New Ark.CraftingCost(Engram, WithDefaults)
		    Config.Add(Cost)
		    NewCosts.Add(Cost)
		  Next
		  
		  Self.UpdateList(NewCosts)
		  Self.Modified = True
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ShowDuplicateSelection()
		  If Self.List.SelectedRowCount <> 1 Or Self.List.SelectedRowIndex < 0 Or Self.List.SelectedRowIndex >= Self.List.RowCount Then
		    Return
		  End If
		  
		  Var Config As Ark.Configs.CraftingCosts = Self.Config(False)
		  Var CurrentEngrams() As Ark.Engram = Config.Engrams
		  
		  Var NewEngrams() As Ark.Engram = ArkBlueprintSelectorDialog.Present(Self, "Crafting", CurrentEngrams, Self.Project.ContentPacks, ArkBlueprintSelectorDialog.SelectModes.ExplicitMultiple)
		  If NewEngrams = Nil Or NewEngrams.LastIndex = -1 Then
		    Return
		  End If
		  
		  Var SourceCost As Ark.CraftingCost = Self.List.RowTagAt(Self.List.SelectedRowIndex)
		  Config = Self.Config(True)
		  
		  Var NewCosts() As Ark.CraftingCost
		  For Each Engram As Ark.Engram In NewEngrams
		    Var Cost As New Ark.MutableCraftingCost(SourceCost)
		    Cost.Engram = Engram
		    Config.Add(Cost)
		    NewCosts.Add(Cost)
		  Next
		  
		  Self.UpdateList(NewCosts)
		  Self.Modified = True
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateList()
		  Var Arr() As Ark.CraftingCost
		  For I As Integer = 0 To Self.List.RowCount - 1
		    If Not Self.List.RowSelectedAt(I) Then
		      Continue
		    End If
		    
		    Arr.Add(Self.List.RowTagAt(I))
		  Next
		  Self.UpdateList(Arr)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateList(SelectItems() As Ark.CraftingCost)
		  Var ScrollPosition As Integer = Self.List.ScrollPosition
		  Self.List.SelectionChangeBlocked = True
		  
		  Var ObjectIDs() As String
		  For Each Item As Ark.CraftingCost In SelectItems
		    ObjectIDs.Add(Item.ObjectID)
		  Next
		  
		  Var Filter As String = Self.FilterField.Text.Trim
		  
		  Self.List.RemoveAllRows
		  Var Config As Ark.Configs.CraftingCosts = Self.Config(False)
		  Var Engrams() As Ark.Engram = Config.Engrams
		  For Idx As Integer = Engrams.FirstIndex To Engrams.LastIndex
		    If Filter.IsEmpty = False And Engrams(Idx).Label.IndexOf(Filter) = -1 Then
		      Continue
		    End If
		    
		    Var Cost As Ark.CraftingCost = Config.Cost(Engrams(Idx))
		    Self.List.AddRow(Cost.Engram.Label)
		    Self.List.RowTagAt(Self.List.LastAddedRowIndex) = Cost
		    Self.List.RowSelectedAt(Self.List.LastAddedRowIndex) = ObjectIDs.IndexOf(Cost.ObjectID) > -1
		  Next Idx
		  
		  Self.List.Sort
		  Self.List.ScrollPosition = ScrollPosition
		  Self.List.SelectionChangeBlocked = False
		  Self.UpdateStatus
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateList(SelectItem As Ark.CraftingCost)
		  Var Arr() As Ark.CraftingCost
		  If SelectItem <> Nil Then
		    Arr.Add(SelectItem)
		  End If
		  Self.UpdateList(Arr)
		  Self.List.EnsureSelectionIsVisible()
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateStatus()
		  Var TotalItems As Integer = Self.List.RowCount
		  Var SelectedItems As Integer = Self.List.SelectedRowCount
		  
		  Var Noun As String = If(TotalItems = 1, "Engram", "Engrams")
		  
		  If SelectedItems > 0 Then
		    Self.ListStatusBar.Caption = SelectedItems.ToString(Locale.Current, ",##0") + " of " + TotalItems.ToString(Locale.Current, ",##0") + " " + Noun + " Selected"
		  Else
		    Self.ListStatusBar.Caption = TotalItems.ToString(Locale.Raw, "0") + " " + Noun
		  End If
		End Sub
	#tag EndMethod


	#tag Property, Flags = &h21
		Private mCostMultiplier As Double
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mProgressWindow As ProgressWindow
	#tag EndProperty


	#tag Constant, Name = kClipboardType, Type = String, Dynamic = False, Default = \"com.thezaz.beacon.ark.craftingcost", Scope = Private
	#tag EndConstant

	#tag Constant, Name = ListDefaultWidth, Type = Double, Dynamic = False, Default = \"315", Scope = Public
	#tag EndConstant

	#tag Constant, Name = ListMinWidth, Type = Double, Dynamic = False, Default = \"250", Scope = Public
	#tag EndConstant

	#tag Constant, Name = PageMultipleSelection, Type = Double, Dynamic = False, Default = \"2", Scope = Private
	#tag EndConstant

	#tag Constant, Name = PageNoSelection, Type = Double, Dynamic = False, Default = \"0", Scope = Private
	#tag EndConstant

	#tag Constant, Name = PageSingleSelection, Type = Double, Dynamic = False, Default = \"1", Scope = Private
	#tag EndConstant


#tag EndWindowCode

#tag Events List
	#tag Event
		Sub SelectionChanged()
		  If Me.SelectedRowCount = 1 Then
		    Self.Editor.Target = Me.RowTagAt(Me.SelectedRowIndex)
		  Else
		    Self.Editor.Target = Nil
		  End If
		  
		  If Me.SelectedRowCount = 0 Then
		    Self.Panel.SelectedPanelIndex = Self.PageNoSelection
		  ElseIf Me.SelectedRowCount = 1 Then
		    Self.Panel.SelectedPanelIndex = Self.PageSingleSelection
		  Else
		    Self.Panel.SelectedPanelIndex = Self.PageMultipleSelection
		  End If
		  
		  Var DuplicateButton As OmniBarItem = Self.ConfigToolbar.Item("DuplicateButton")
		  If (DuplicateButton Is Nil) = False Then
		    DuplicateButton.Enabled = Me.SelectedRowCount = 1
		  End If
		  
		  Self.UpdateStatus()
		End Sub
	#tag EndEvent
	#tag Event
		Function CanDelete() As Boolean
		  Return Me.SelectedRowCount > 0
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformClear(Warn As Boolean)
		  Var Costs() As Ark.CraftingCost
		  Var Bound As Integer = Me.RowCount - 1
		  For I As Integer = 0 To Bound
		    If Me.RowSelectedAt(I) = False Then
		      Continue
		    End If
		    
		    Costs.Add(Me.RowTagAt(I))
		  Next
		  
		  If Warn And Self.ShowDeleteConfirmation(Costs, "crafting cost override", "crafting cost overrides") = False Then
		    Return
		  End If
		  
		  Var Config As Ark.Configs.CraftingCosts = Self.Config(True)
		  For Each Cost As Ark.CraftingCost In Costs
		    Config.Remove(Cost)
		  Next
		  Self.Modified = True
		  Self.UpdateList()
		End Sub
	#tag EndEvent
	#tag Event
		Function CanCopy() As Boolean
		  Return Me.SelectedRowCount > 0
		End Function
	#tag EndEvent
	#tag Event
		Function CanPaste(Board As Clipboard) As Boolean
		  If Board.RawDataAvailable(Self.kClipboardType) Then
		    Return True
		  End If
		  
		  If Not Board.TextAvailable Then
		    Return False
		  End If
		  
		  Return Board.Text.IndexOf("ConfigOverrideItemCraftingCosts") > -1
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformCopy(Board As Clipboard)
		  Var Dicts() As Dictionary
		  Var SelectedCosts() As Ark.CraftingCost
		  For I As Integer = 0 To Me.RowCount - 1
		    If Not Me.RowSelectedAt(I) Then
		      Continue
		    End If
		    
		    Var Cost As Ark.CraftingCost = Me.RowTagAt(I)
		    SelectedCosts.Add(Cost)
		    Dicts.Add(Cost.Export)
		  Next
		  
		  Board.RawData(Self.kClipboardType) = Beacon.GenerateJSON(Dicts, False)
		  
		  If Not Ark.Configs.ConfigUnlocked(Ark.Configs.NameCraftingCosts, App.IdentityManager.CurrentIdentity) Then
		    Return
		  End If
		  
		  Var Lines() As String
		  For Each Cost As Ark.CraftingCost In SelectedCosts
		    If Cost.Engram Is Nil Or Cost.Engram.ValidForProject(Self.Project) = False Then
		      Continue
		    End If
		    
		    Var Config As Ark.ConfigValue = Ark.Configs.CraftingCosts.ConfigValueForCraftingCost(Cost)
		    If (Config Is Nil) = False Then
		      Lines.Add(Config.Command)
		    End If
		  Next
		  
		  If Lines.Count = 0 Then
		    Return
		  End If
		  
		  Board.Text = Lines.Join(EndOfLine)
		End Sub
	#tag EndEvent
	#tag Event
		Sub PerformPaste(Board As Clipboard)
		  If Board.TextAvailable And Board.Text.IndexOf("ConfigOverrideItemCraftingCosts") > -1 Then
		    Var ImportText As String = Board.Text.GuessEncoding
		    Self.Parse("", ImportText, "Clipboard")
		    Return
		  End If
		  
		  If Board.RawDataAvailable(Self.kClipboardType) Then
		    Var Dicts() As Variant
		    Try
		      Var Contents As String = Board.RawData(Self.kClipboardType).DefineEncoding(Encodings.UTF8)
		      Dicts = Beacon.ParseJSON(Contents)
		      
		      Var Costs() As Ark.CraftingCost
		      Var Config As Ark.Configs.CraftingCosts = Self.Config(True)
		      For Each Dict As Dictionary In Dicts
		        Var Cost As Ark.CraftingCost = Ark.CraftingCost.ImportFromBeacon(Dict)
		        If Cost <> Nil Then
		          Config.Add(Cost)
		          Costs.Add(Cost)
		        End If
		      Next
		      
		      Self.UpdateList(Costs)
		      Self.Modified = True
		    Catch Err As RuntimeException
		      System.Beep
		    End Try
		    Return
		  End If
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events Editor
	#tag Event
		Sub ContentsChanged()
		  If Self.List.SelectedRowCount = 1 Then
		    Var Blocked As Boolean = Self.List.SelectionChangeBlocked
		    Self.List.SelectionChangeBlocked = True
		    
		    Var Cost As Ark.CraftingCost = Me.Target
		    Self.List.CellTextAt(Self.List.SelectedRowIndex, 0) = Cost.Label
		    Self.List.RowTagAt(Self.List.SelectedRowIndex) = Cost
		    Self.List.Sort
		    
		    Self.Config(True).Add(Cost)
		    
		    Self.List.SelectionChangeBlocked = Blocked
		    Self.Modified = True
		  End If
		End Sub
	#tag EndEvent
	#tag Event
		Function GetActiveMods() As Beacon.StringList
		  If (Self.Project Is Nil) = False Then
		    Return Self.Project.ContentPacks
		  End If
		End Function
	#tag EndEvent
#tag EndEvents
#tag Events FibercraftBuilderThread
	#tag Event
		Sub Run()
		  Var Fiber As Ark.Engram = Ark.DataSource.Pool.Get(False).GetEngramByUUID("244bc843-2540-486e-af4a-8824500c0e56")
		  
		  Var Config As Ark.Configs.CraftingCosts = Self.Config(False)
		  Var Engrams() As Ark.Engram = Config.Engrams
		  Var EngramDict As New Dictionary
		  For Each Engram As Ark.Engram In Engrams
		    If Self.mProgressWindow.CancelPressed Then
		      Self.mProgressWindow.Close
		      Self.mProgressWindow = Nil
		      Return
		    End If
		    
		    If Engram.IsTagged("no_fibercraft") Or Engram.IsTagged("forged") Then
		      Continue
		    End If
		    
		    EngramDict.Value(Engram.ObjectID) = Engram
		  Next
		  
		  Engrams = Ark.DataSource.Pool.Get(False).GetEngrams("", Self.Project.ContentPacks, "{""required"":[""blueprintable""],""excluded"":[""generic"",""no_fibercraft"",""forged""]}")
		  For Each Engram As Ark.Engram In Engrams
		    If Self.mProgressWindow.CancelPressed Then
		      Self.mProgressWindow.Close
		      Self.mProgressWindow = Nil
		      Return
		    End If
		    
		    EngramDict.Value(Engram.ObjectID) = Engram
		  Next
		  
		  Config = New Ark.Configs.CraftingCosts
		  Config.IsImplicit = False
		  Var NumItems As Integer = EngramDict.KeyCount
		  Var ProcessedItems As Integer
		  Self.mProgressWindow.Progress = ProcessedItems / NumItems
		  For Each Entry As DictionaryEntry In EngramDict
		    If Self.mProgressWindow.CancelPressed Then
		      Self.mProgressWindow.Close
		      Self.mProgressWindow = Nil
		      Return
		    End If
		    
		    Var Engram As Ark.Engram = Entry.Value
		    Var Cost As New Ark.MutableCraftingCost(Engram)
		    Cost.Add(Fiber, 1.0, False)
		    Config.Add(Cost)
		    ProcessedItems = ProcessedItems + 1
		    Self.mProgressWindow.Progress = ProcessedItems / NumItems
		    Self.mProgressWindow.Detail = "Configured " + ProcessedItems.ToString(Locale.Current, "#,##0") + " of " + NumItems.ToString(Locale.Current, "#,##0") + " engrams"
		  Next
		  
		  Self.Project.AddConfigGroup(Config)
		  Self.InvalidateConfigRef
		  
		  Self.mProgressWindow.Close
		  Self.mProgressWindow = Nil
		  
		  Var NotifyDict As New Dictionary
		  NotifyDict.Value("Finished") = True
		  Me.AddUserInterfaceUpdate(NotifyDict)
		End Sub
	#tag EndEvent
	#tag Event
		Sub UserInterfaceUpdate(data() as Dictionary)
		  For Each Dict As Dictionary In Data
		    If Dict.HasKey("Finished") And Dict.Value("Finished").BooleanValue = True THen
		      Self.SetupUI()
		    End If
		  Next
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events AdjusterThread
	#tag Event
		Sub UserInterfaceUpdate(data() as Dictionary)
		  For Each Dict As Dictionary In Data
		    If Dict.HasKey("Finished") And Dict.Value("Finished").BooleanValue = True THen
		      Self.SetupUI()
		    End If
		  Next
		End Sub
	#tag EndEvent
	#tag Event
		Sub Run()
		  Var OriginalConfig As Ark.Configs.CraftingCosts = Self.Config(False)
		  Var Engrams() As Ark.Engram = OriginalConfig.Engrams
		  Var Filter As New Dictionary
		  For Each Engram As Ark.Engram In Engrams
		    Filter.Value(Engram.ObjectID) = True
		  Next
		  
		  Var ObjectIDs() As String = Ark.DataSource.Pool.Get(False).GetEngramUUIDsThatHaveCraftingCosts(Self.Project.ContentPacks, Self.Project.MapMask)
		  For Each ObjectID As String In ObjectIDs
		    If Filter.HasKey(ObjectID) Then
		      Continue
		    End If
		    
		    Var Engram As Ark.Engram = Ark.DataSource.Pool.Get(False).GetEngramByUUID(ObjectID)
		    If (Engram Is Nil) = False Then
		      Engrams.Add(Engram)
		    End If
		  Next
		  
		  Var NumProcessed As Integer
		  Var ReplacementConfig As New Ark.Configs.CraftingCosts
		  For Each Engram As Ark.Engram In Engrams
		    If Self.mProgressWindow.CancelPressed Then
		      Self.mProgressWindow.Close
		      Self.mProgressWindow = Nil
		      Return
		    End If
		    
		    NumProcessed = NumProcessed + 1
		    Var Cost As Ark.MutableCraftingCost
		    Var ExistingCost As Ark.CraftingCost = OriginalConfig.Cost(Engram)
		    If (ExistingCost Is Nil) = False Then
		      Cost = New Ark.MutableCraftingCost(ExistingCost)
		    Else
		      Cost = New Ark.MutableCraftingCost(Engram, True)
		    End If
		    
		    If Cost.Count = 0 Then
		      Continue
		    End If
		    
		    For IngredientIdx As Integer = 0 To Cost.LastIndex
		      Var Ingredient As Ark.CraftingCostIngredient = Cost.Ingredient(IngredientIdx)
		      Var Quantity As Integer = Ceiling(Ingredient.Quantity * Self.mCostMultiplier)
		      Cost.Ingredient(IngredientIdx) = New Ark.CraftingCostIngredient(Ingredient.Reference, Quantity, Ingredient.RequireExact)
		    Next
		    
		    ReplacementConfig.Add(Cost)
		    
		    Self.mProgressWindow.Progress = NumProcessed / Engrams.Count
		    Self.mProgressWindow.Detail = "Updated " + NumProcessed.ToString(Locale.Current, ",##0") + " of " + Engrams.Count.ToString(Locale.Current, ",##0")
		  Next
		  
		  Self.Project.AddConfigGroup(ReplacementConfig)
		  Self.InvalidateConfigRef
		  
		  Self.mProgressWindow.Close
		  Self.mProgressWindow = Nil
		  
		  Var NotifyDict As New Dictionary
		  NotifyDict.Value("Finished") = True
		  Me.AddUserInterfaceUpdate(NotifyDict)
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events ConfigToolbar
	#tag Event
		Sub ItemPressed(Item As OmniBarItem, ItemRect As Rect)
		  #Pragma Unused ItemRect
		  
		  Select Case Item.Name
		  Case "AddEngramButton"
		    Self.ShowAddEngram()
		  Case "DuplicateButton"
		    Self.ShowDuplicateSelection()
		  End Select
		End Sub
	#tag EndEvent
	#tag Event
		Sub Opening()
		  Me.Append(OmniBarItem.CreateTitle("ConfigTitle", Self.ConfigLabel))
		  Me.Append(OmniBarItem.CreateSeparator("ConfigTitleSeparator"))
		  Me.Append(OmniBarItem.CreateButton("AddEngramButton", "New Recipe", IconToolbarAdd, "Change the recipe for a new item."))
		  Me.Append(OmniBarItem.CreateButton("DuplicateButton", "Duplicate", IconToolbarClone, "Duplicate the selected recipe.", False))
		  Me.Append(OmniBarItem.CreateFlexibleSpace)
		  Me.Append(OmniBarItem.CreateHorizontalResizer("Resizer"))
		  
		  Me.Item("ConfigTitle").Priority = 5
		  Me.Item("ConfigTitleSeparator").Priority = 5
		End Sub
	#tag EndEvent
	#tag Event
		Sub Resize(DraggedResizer As OmniBarItem, DeltaX As Integer, DeltaY As Integer)
		  #Pragma Unused DraggedResizer
		  #Pragma Unused DeltaY
		  
		  Self.SetListWidth(Me.Width + DeltaX)
		End Sub
	#tag EndEvent
	#tag Event
		Sub ResizeFinished(DraggedResizer As OmniBarItem)
		  #Pragma Unused DraggedResizer
		  
		  Preferences.CraftingSplitterPosition = Me.Width
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events FilterField
	#tag Event
		Sub TextChanged()
		  If Self.SettingUp Then
		    Return
		  End If
		  
		  Self.UpdateList()
		End Sub
	#tag EndEvent
#tag EndEvents
#tag ViewBehavior
	#tag ViewProperty
		Name="Modified"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Composited"
		Visible=true
		Group="Window Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Index"
		Visible=true
		Group="ID"
		InitialValue="-2147483648"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="IsFrontmost"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="ViewTitle"
		Visible=true
		Group="Behavior"
		InitialValue="Untitled"
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="ViewIcon"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Progress"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Double"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Tooltip"
		Visible=true
		Group="Appearance"
		InitialValue=""
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowAutoDeactivate"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocusRing"
		Visible=true
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="BackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="&hFFFFFF"
		Type="ColorGroup"
		EditorType="ColorGroup"
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasBackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocus"
		Visible=true
		Group="Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowTabs"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimumWidth"
		Visible=true
		Group="Behavior"
		InitialValue="400"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimumHeight"
		Visible=true
		Group="Behavior"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Name"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Super"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Width"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Height"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="InitialParent"
		Visible=false
		Group="Position"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Left"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Top"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockLeft"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockTop"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockRight"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockBottom"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabPanelIndex"
		Visible=false
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabIndex"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabStop"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Visible"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Enabled"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Backdrop"
		Visible=true
		Group="Background"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Transparent"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
#tag EndViewBehavior
