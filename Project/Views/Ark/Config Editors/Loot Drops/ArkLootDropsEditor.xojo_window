#tag DesktopWindow
Begin ArkConfigEditor ArkLootDropsEditor
   AllowAutoDeactivate=   True
   AllowFocus      =   False
   AllowFocusRing  =   False
   AllowTabs       =   True
   Backdrop        =   0
   BackgroundColor =   &cFFFFFF00
   Composited      =   False
   Enabled         =   True
   HasBackgroundColor=   False
   Height          =   436
   Index           =   -2147483648
   InitialParent   =   ""
   Left            =   0
   LockBottom      =   True
   LockLeft        =   True
   LockRight       =   True
   LockTop         =   True
   TabIndex        =   0
   TabPanelIndex   =   0
   TabStop         =   True
   Tooltip         =   ""
   Top             =   0
   Transparent     =   True
   Visible         =   True
   Width           =   702
   Begin FadedSeparator FadedSeparator1
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   436
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   250
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   1
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   True
      Visible         =   True
      Width           =   1
   End
   Begin BeaconListbox List
      AllowAutoDeactivate=   True
      AllowAutoHideScrollbars=   True
      AllowExpandableRows=   False
      AllowFocusRing  =   False
      AllowInfiniteScroll=   False
      AllowResizableColumns=   False
      AllowRowDragging=   False
      AllowRowReordering=   False
      Bold            =   False
      ColumnCount     =   2
      ColumnWidths    =   "50,*"
      DefaultRowHeight=   34
      DefaultSortColumn=   0
      DefaultSortDirection=   0
      DropIndicatorVisible=   False
      EditCaption     =   "Edit"
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      GridLineStyle   =   0
      HasBorder       =   False
      HasHeader       =   False
      HasHorizontalScrollbar=   False
      HasVerticalScrollbar=   True
      HeadingIndex    =   -1
      Height          =   323
      Index           =   -2147483648
      InitialParent   =   ""
      InitialValue    =   ""
      Italic          =   False
      Left            =   0
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      PageSize        =   100
      PreferencesKey  =   ""
      RequiresSelection=   False
      RowSelectionType=   1
      Scope           =   2
      TabIndex        =   5
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   82
      TotalPages      =   -1
      Transparent     =   True
      TypeaheadColumn =   1
      Underline       =   False
      Visible         =   True
      VisibleRowCount =   0
      Width           =   250
      _ScrollOffset   =   0
      _ScrollWidth    =   -1
   End
   Begin DesktopPagePanel Panel
      AllowAutoDeactivate=   True
      Enabled         =   True
      Height          =   436
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   251
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   True
      LockTop         =   True
      PanelCount      =   2
      Panels          =   ""
      Scope           =   2
      SelectedPanelIndex=   0
      TabIndex        =   2
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   False
      Value           =   0
      Visible         =   True
      Width           =   451
      Begin ArkLootDropEditor Editor
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   False
         AllowTabs       =   True
         Backdrop        =   0
         BackgroundColor =   &cFFFFFF
         Composited      =   False
         Enabled         =   True
         HasBackgroundColor=   False
         Height          =   436
         Index           =   -2147483648
         InitialParent   =   "Panel"
         Left            =   251
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Modified        =   False
         Scope           =   2
         TabIndex        =   0
         TabPanelIndex   =   2
         TabStop         =   True
         Tooltip         =   ""
         Top             =   0
         Transparent     =   True
         Visible         =   True
         Width           =   451
      End
      Begin LogoFillCanvas LogoFillCanvas1
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   True
         AllowTabs       =   False
         Backdrop        =   0
         Caption         =   "No Selection"
         ContentHeight   =   0
         Enabled         =   True
         Height          =   405
         Index           =   -2147483648
         InitialParent   =   "Panel"
         Left            =   251
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Scope           =   2
         ScrollActive    =   False
         ScrollingEnabled=   False
         ScrollSpeed     =   20
         TabIndex        =   0
         TabPanelIndex   =   1
         TabStop         =   True
         Tooltip         =   ""
         Top             =   0
         Transparent     =   True
         Visible         =   True
         Width           =   451
      End
      Begin StatusContainer NoSelectionStatusBar
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   False
         AllowTabs       =   True
         Backdrop        =   0
         BackgroundColor =   &cFFFFFF
         CenterCaption   =   ""
         Composited      =   False
         Enabled         =   True
         HasBackgroundColor=   False
         Height          =   31
         Index           =   -2147483648
         InitialParent   =   "Panel"
         Left            =   251
         LeftCaption     =   ""
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   False
         RightCaption    =   ""
         Scope           =   2
         TabIndex        =   1
         TabPanelIndex   =   1
         TabStop         =   True
         Tooltip         =   ""
         Top             =   405
         Transparent     =   True
         Visible         =   True
         Width           =   451
      End
   End
   Begin OmniBar ConfigToolbar
      Alignment       =   0
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      BackgroundColor =   ""
      ContentHeight   =   0
      Enabled         =   True
      Height          =   41
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LeftPadding     =   -1
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      RightPadding    =   -1
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   0
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
   Begin DelayedSearchField FilterField
      Active          =   False
      AllowAutoDeactivate=   True
      AllowFocusRing  =   True
      AllowRecentItems=   False
      ClearMenuItemValue=   "Clear"
      DelayPeriod     =   250
      Enabled         =   True
      Height          =   22
      Hint            =   "Filter Drops"
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   9
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      MaximumRecentItems=   -1
      PanelIndex      =   0
      RecentItemsValue=   "Recent Searches"
      Scope           =   2
      TabIndex        =   3
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   ""
      Tooltip         =   ""
      Top             =   50
      Transparent     =   False
      Visible         =   True
      Width           =   232
      _mIndex         =   0
      _mInitialParent =   ""
      _mName          =   ""
      _mPanelIndex    =   0
   End
   Begin OmniBarSeparator FilterSeparator
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   1
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   4
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   81
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
   Begin StatusContainer StatusBar1
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   False
      AllowTabs       =   True
      Backdrop        =   0
      BackgroundColor =   &cFFFFFF
      CenterCaption   =   ""
      Composited      =   False
      Enabled         =   True
      HasBackgroundColor=   False
      Height          =   31
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LeftCaption     =   ""
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   False
      RightCaption    =   ""
      Scope           =   2
      TabIndex        =   7
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   405
      Transparent     =   True
      Visible         =   True
      Width           =   250
   End
End
#tag EndDesktopWindow

#tag WindowCode
	#tag Event
		Function ParsingFinished(Project As Ark.Project) As Boolean
		  If Project Is Nil Or Project.HasConfigGroup(Ark.Configs.NameLootDrops) = False Then
		    Return False
		  End If
		  
		  Var OtherConfig As Ark.Configs.LootDrops = Ark.Configs.LootDrops(Project.ConfigGroup(Ark.Configs.NameLootDrops))
		  If OtherConfig Is Nil Then
		    Return False
		  End If
		  
		  Var Overrides() As Ark.LootDropOverride = OtherConfig.Overrides
		  Var TotalNewContainers As Integer = Overrides.Count
		  If TotalNewContainers = 0 Then
		    Return False
		  End If
		  
		  Var DuplicateContainerCount As Integer
		  Var Config As Ark.Configs.LootDrops = Self.Config(True)
		  For Each Override As Ark.LootDropOverride In Overrides
		    If Config.HasOverride(Override) Then
		      DuplicateContainerCount = DuplicateContainerCount + 1
		    End If
		  Next
		  
		  Var Replace As Boolean = True
		  If DuplicateContainerCount > 0 Then
		    Replace = Self.ShowConfirm("Replace " + Language.NounWithQuantity(DuplicateContainerCount, "loot drop", "loot drops") + "?", DuplicateContainerCount.ToString + " of " + Language.NounWithQuantity(TotalNewContainers, " loot drop has already been defined in this project. Would you like to replace it?", " loot drops are already defined in this project. Would you like to replace them?"), "Replace", "Cancel")
		  End If
		  
		  Var AddedOverrides() As Ark.LootDropOverride
		  For Each Override As Ark.LootDropOverride In OtherConfig
		    If Config.HasOverride(Override) Then
		      If Replace Then
		        Config.Remove(Override)
		      Else
		        Continue
		      End If
		    End If
		    
		    AddedOverrides.Add(Override)
		    Config.Add(Override)
		  Next
		  
		  If AddedOverrides.Count > 0 Then
		    Self.Modified = True
		    Self.UpdateContainerList(AddedOverrides)
		  End If
		  
		  Return True
		End Function
	#tag EndEvent

	#tag Event
		Sub Resize(Initial As Boolean)
		  If Initial Then
		    Self.SetListWidth(Preferences.SourcesSplitterPosition, False)
		  Else
		    Self.SetListWidth(Self.ConfigToolbar.Width)
		  End If
		  
		  Var Resizer As OmniBarItem = Self.ConfigToolbar.Item("Resizer")
		  If (Resizer Is Nil) = False Then
		    Resizer.Enabled = Self.Width > Self.MinEditorWidth
		  End If
		End Sub
	#tag EndEvent

	#tag Event
		Sub RunTool(Tool As Ark.ProjectTool)
		  Select Case Tool.UUID
		  Case "08efc49c-f39f-4147-820d-201637c206b5"
		    Self.RebuildAllItemSets()
		  End Select
		End Sub
	#tag EndEvent

	#tag Event
		Sub SetupUI()
		  Self.UpdateContainerList()
		End Sub
	#tag EndEvent

	#tag Event
		Sub ShowIssue(Issue As Beacon.Issue)
		  Var LocationParts() As String = Issue.Location.Split(Beacon.Issue.Separator)
		  // ConfigSet is 0, ConfigName is 1
		  Var DropClass As String = LocationParts(2)
		  Var ItemSetUUID, EntryUUID, EngramClass As String
		  If LocationParts.LastIndex > 2 Then
		    ItemSetUUID = LocationParts(3)
		  End If
		  If LocationParts.LastIndex > 3 Then
		    EntryUUID = LocationParts(4)
		  End If
		  If LocationParts.LastIndex > 4 Then
		    EngramClass = LocationParts(5)
		  End If
		  
		  Call Self.GoToChild(DropClass, ItemSetUUID, EntryUUID, EngramClass)
		End Sub
	#tag EndEvent


	#tag Method, Flags = &h21
		Private Sub AddOverride(Override As Ark.LootDropOverride)
		  Var Arr(0) As Ark.LootDropOverride
		  Arr(0) = Override
		  Self.AddOverrides(Arr)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub AddOverrides(Overrides() As Ark.LootDropOverride)
		  If Overrides Is Nil Or Overrides.Count = 0 Then
		    Return
		  End If
		  
		  For Each Override As Ark.LootDropOverride In Overrides
		    If Override.Experimental And Not Preferences.HasShownExperimentalWarning Then
		      If Self.ShowConfirm(Language.ExperimentalWarningMessage, Language.ReplacePlaceholders(Language.ExperimentalWarningExplanation, Override.Label), Language.ExperimentalWarningActionCaption, Language.ExperimentalWarningCancelCaption) Then
		        Preferences.HasShownExperimentalWarning = True
		        Exit
		      Else
		        Return
		      End If
		    End If
		  Next
		  
		  Var Config As Ark.Configs.LootDrops = Self.Config(True)
		  For Each Override As Ark.LootDropOverride In Overrides
		    Config.Add(Override)
		    Self.Modified = Self.Project.Modified
		  Next
		  
		  Self.UpdateContainerList(Overrides)
		  Self.List.EnsureSelectionIsVisible()
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub BuildQuickDropMenu(Menu As DesktopMenuItem)
		  Var Data As Ark.DataSource = Ark.DataSource.Pool.Get(False)
		  Var Containers() As Ark.LootContainer = Data.GetLootContainers("", Self.Project.ContentPacks, "", Preferences.ShowExperimentalLootSources)
		  Var HasExperimentalContainers As Boolean = Data.HasExperimentalLootContainers(Self.Project.ContentPacks)
		  Var Config As Ark.Configs.LootDrops = Self.Config(False)
		  Var Mask As UInt64 = Self.Project.MapMask
		  If (Config Is Nil) = False Then
		    For I As Integer = Containers.LastIndex DownTo 0
		      Var Container As Ark.LootContainer = Containers(I)
		      If Config.HasOverride(Container) Or Container.ValidForMask(Mask) = False Then
		        Containers.RemoveAt(I)
		        Continue For I
		      End If
		    Next
		  End If
		  
		  Var AllContainers() As Ark.LootContainer = Data.GetLootContainers("", Self.Project.ContentPacks, "")
		  Var Labels As Dictionary = AllContainers.Disambiguate(Self.Project.MapMask)
		  
		  If Containers.Count = 0 Then
		    Var Warning As DesktopMenuItem
		    If Mask = CType(0, UInt64) Then
		      Warning = New DesktopMenuItem("List is empty because no maps have been selected.")
		    Else
		      Warning = New DesktopMenuItem("List is empty because all drops have been implemented.")
		    End If
		    Warning.Enabled = False
		    Menu.AddMenu(Warning)
		    Return
		  End If
		  
		  Containers.Sort
		  
		  For Each Container As Ark.LootContainer In Containers
		    Menu.AddMenu(New DesktopMenuItem(Labels.Lookup(Container.LootDropId, Container.Label), Container))
		  Next
		  
		  If HasExperimentalContainers Then
		    Menu.AddMenu(New DesktopMenuItem(DesktopMenuItem.TextSeparator))
		    
		    Var ExpItem As New DesktopMenuItem("Show Experimental Containers", "toggle_experimental")
		    ExpItem.HasCheckMark = Preferences.ShowExperimentalLootSources
		    Menu.AddMenu(ExpItem)
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h1
		Protected Function Config(ForWriting As Boolean) As Ark.Configs.LootDrops
		  Return Ark.Configs.LootDrops(Super.Config(ForWriting))
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Function GoToChild(DropClass As String, ItemSetUUID As String = "", EntryUUID As String = "", EngramClass As String = "") As Boolean
		  For Idx As Integer = 0 To Self.List.LastRowIndex
		    Var Container As Ark.LootDropOverride = Self.List.RowTagAt(Idx)
		    If Container Is Nil Or Container.LootDropReference.ClassString <> DropClass Then
		      Continue
		    End If
		    
		    Self.List.SelectedRowIndex = Idx
		    Self.List.EnsureSelectionIsVisible()
		    
		    If ItemSetUUID.IsEmpty = False Then
		      Return Self.Editor.GoToChild(ItemSetUUID, EntryUUID, EngramClass)
		    Else
		      Return True
		    End If
		  Next Idx
		  
		  Self.List.SelectedRowIndex = -1
		  Return False
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub HandleQuickDropMenu(ChosenItem As DesktopMenuItem)
		  Var Tag As Variant = ChosenItem.Tag
		  If Tag.IsNull Then
		    Return
		  End If
		  
		  If Tag.Type = Variant.TypeString Then
		    Select Case Tag.StringValue
		    Case "toggle_experimental"
		      Preferences.ShowExperimentalLootSources = Not Preferences.ShowExperimentalLootSources
		      Self.UpdateContainerList()
		    End Select
		  ElseIf Tag.Type = Variant.TypeObject And Tag.ObjectValue IsA Ark.LootContainer Then
		    Var Container As Ark.LootContainer = ChosenItem.Tag
		    Self.AddOverride(New Ark.LootDropOverride(Container, False))
		    Self.Focus = Self.List
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Function InternalName() As String
		  Return Ark.Configs.NameLootDrops
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Shared Function MinEditorWidth() As Integer
		  Return ListMinWidth + ArkLootDropEditor.MinEditorWidth + 1
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub RebuildAllItemSets()
		  Var NumChanges As Integer = Self.Config(True).RebuildItemSets(Self.Project.MapMask, Self.Project.ContentPacks)
		  If NumChanges = 0 Then
		    Self.ShowAlert("No item sets changed", "All item sets are already configured according to their templates.")
		    Return
		  End If
		  
		  Self.UpdateContainerList()
		  Self.Modified = Self.Modified Or Self.Project.Modified
		  
		  If NumChanges = 1 Then
		    Self.ShowAlert("1 item set changed", "Rebuilding changed 1 item set to match its template.")
		  Else
		    Self.ShowAlert(NumChanges.ToString(Locale.Current, "#,##0") + " item sets changed", "Rebuilding changed " + NumChanges.ToString(Locale.Current, "#,##0") + " item sets to match their templates.")
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub SetListWidth(NewSize As Integer, Remember As Boolean = True)
		  Var ListWidth, EditorWidth As Integer
		  If Self.Width <= Self.MinEditorWidth Then
		    ListWidth = Self.ListMinWidth
		    EditorWidth = ArkLootDropEditor.MinEditorWidth
		  Else
		    Var AvailableSpace As Integer = Self.Width - Self.FadedSeparator1.Width
		    ListWidth = Min(Max(NewSize, Self.ListMinWidth), AvailableSpace - ArkLootDropEditor.MinEditorWidth)
		    EditorWidth = AvailableSpace - ListWidth
		  End If
		  
		  Self.ConfigToolbar.Width = ListWidth
		  Self.FadedSeparator1.Left = ListWidth
		  Self.List.Width = ListWidth
		  Self.StatusBar1.Width = ListWidth
		  Self.FilterSeparator.Width = ListWidth
		  Self.FilterField.Width = ListWidth - (Self.FilterField.Left * 2)
		  Self.Panel.Left = Self.FadedSeparator1.Left + Self.FadedSeparator1.Width
		  Self.Panel.Width = EditorWidth
		  
		  If Remember Then
		    Preferences.SourcesSplitterPosition = ListWidth
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ShowAddLootContainer(DuplicateSelected As Boolean = False)
		  If DuplicateSelected And Self.List.SelectedRowCount <> 1 Then
		    Return
		  End If
		  
		  Var Config As Ark.Configs.LootDrops = Self.Config(False)
		  Var CurrentOverrides() As Ark.LootDropOverride = Config.Overrides
		  Var Map As New Dictionary
		  For Each Override As Ark.LootDropOverride In CurrentOverrides
		    Map.Value(Override.LootDropId) = True
		  Next
		  
		  Var DuplicateOverride As Ark.LootDropOverride
		  If DuplicateSelected Then
		    DuplicateOverride = Self.List.RowTagAt(Self.List.SelectedRowIndex)
		  End If
		  
		  If ArkAddLootDropDialog.Present(Self, Config, Self.Project.MapMask, Self.Project.ContentPacks, DuplicateOverride, DuplicateSelected) Then
		    Call Self.Config(True) // Actually saves the config to the document 
		    CurrentOverrides = Config.Overrides
		    Var NewOverrides() As Ark.LootDropOverride
		    For Each Override As Ark.LootDropOverride In CurrentOverrides
		      If Map.HasKey(Override.LootDropId) = False Then
		        NewOverrides.Add(Override)
		      End If
		    Next
		    Self.UpdateContainerList(NewOverrides)
		    Self.Modified = Self.Project.Modified
		    Self.Focus = Self.List
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateContainerList()
		  // Maintain selection
		  
		  Var LootDropIds() As String
		  For Idx As Integer = 0 To Self.List.LastRowIndex
		    If Self.List.RowSelectedAt(Idx) = False Then
		      Continue
		    End If
		    
		    Var Override As Ark.LootDropOverride = Self.List.RowTagAt(Idx)
		    LootDropIds.Add(Override.LootDropId)
		  Next
		  Self.UpdateContainerList(LootDropIds)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateContainerList(Containers() As Ark.LootContainer)
		  Var LootDropIds() As String
		  For Each Container As Ark.LootContainer In Containers
		    If Container Is Nil Then
		      Continue
		    End If
		    
		    LootDropIds.Add(Container.LootDropId)
		  Next
		  Self.UpdateContainerList(LootDropIds)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateContainerList(Overrides() As Ark.LootDropOverride)
		  Var LootDropIds() As String
		  For Each Override As Ark.LootDropOverride In Overrides
		    If Override Is Nil Then
		      Continue
		    End If
		    
		    LootDropIds.Add(Override.LootDropId)
		  Next
		  Self.UpdateContainerList(LootDropIds)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateContainerList(LootDropIds() As String)
		  // Select the given loot drops
		  
		  Var VisibleOverrides() As Ark.LootDropOverride = Self.Config(False).Overrides(Self.FilterField.Text)
		  Var AllContainers() As Ark.LootContainer = Ark.DataSource.Pool.Get(False).GetLootContainers("", Self.Project.ContentPacks, "")
		  Var Labels As Dictionary = AllContainers.Disambiguate(Self.Project.MapMask, VisibleOverrides)
		  VisibleOverrides.Sort
		  
		  Self.List.RowCount = VisibleOverrides.LastIndex + 1
		  
		  Self.mBlockSelectionChanged = True
		  Var Selection() As Ark.LootDropOverride
		  For Idx As Integer = 0 To VisibleOverrides.LastIndex
		    Self.List.RowTagAt(Idx) = VisibleOverrides(Idx)
		    Self.List.CellTextAt(Idx, 0) = "" // Causes a redraw of the cell
		    Self.List.CellTextAt(Idx, 1) = Labels.Lookup(VisibleOverrides(Idx).LootDropId, VisibleOverrides(Idx).Label)
		    If LootDropIds.IndexOf(VisibleOverrides(Idx).LootDropId) > -1 Then
		      Self.List.RowSelectedAt(Idx) = True
		      Selection.Add(VisibleOverrides(Idx))
		    Else
		      Self.List.RowSelectedAt(Idx) = False
		    End If
		  Next
		  Self.mBlockSelectionChanged = False
		  
		  Editor.Overrides = Selection
		  If Selection.Count = 0 Then
		    Panel.SelectedPanelIndex = 0
		  Else
		    Panel.SelectedPanelIndex = 1
		  End If
		  
		  Var RebuildButton As OmniBarItem = Self.ConfigToolbar.Item("RebuildButton")
		  If (RebuildButton Is Nil) = False Then
		    RebuildButton.Enabled = VisibleOverrides.Count > 0
		  End If
		  Self.UpdateStatus()
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateStatus()
		  Self.StatusBar1.CenterCaption = Self.List.StatusMessage("Loot Drop", "Loot Drops")
		End Sub
	#tag EndMethod


	#tag Property, Flags = &h21
		Private mBlockSelectionChanged As Boolean
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mConfigRef As WeakRef
	#tag EndProperty


	#tag Constant, Name = HelpExplanation, Type = String, Dynamic = False, Default = \"Fill This In", Scope = Private
	#tag EndConstant

	#tag Constant, Name = kClipboardType, Type = String, Dynamic = False, Default = \"com.thezaz.beacon.ark.loot.drop", Scope = Private
	#tag EndConstant

	#tag Constant, Name = ListDefaultWidth, Type = Double, Dynamic = False, Default = \"345", Scope = Public
	#tag EndConstant

	#tag Constant, Name = ListMinWidth, Type = Double, Dynamic = False, Default = \"250", Scope = Public
	#tag EndConstant


#tag EndWindowCode

#tag Events List
	#tag Event
		Sub PaintCellBackground(G As Graphics, Row As Integer, Column As Integer, BackgroundColor As Color, TextColor As Color, IsHighlighted As Boolean)
		  #Pragma Unused BackgroundColor
		  #Pragma Unused IsHighlighted
		  #Pragma Unused TextColor
		  
		  If Row >= Me.RowCount Or Column <> 0 Then
		    Return
		  End If
		  
		  Var Override As Ark.LootDropOverride = Me.RowTagAt(Row)
		  If Override Is Nil Then
		    Return
		  End If
		  
		  Var Icon As Picture
		  If Me.RowSelectedAt(Row) And IsHighlighted Then
		    Icon = Ark.DataSource.Pool.Get(False).GetLootContainerIcon(Override.LootDrop(Self.Project.ContentPacks), TextColor, BackgroundColor)
		  Else
		    Icon = Ark.DataSource.Pool.Get(False).GetLootContainerIcon(Override.LootDrop(Self.Project.ContentPacks), BackgroundColor)
		  End If
		  If Icon Is Nil Then
		    Return
		  End If
		  
		  G.DrawPicture(Icon, NearestMultiple((G.Width - Icon.Width) / 2, G.ScaleX), NearestMultiple((G.Height - Icon.Height) / 2, G.ScaleY))
		End Sub
	#tag EndEvent
	#tag Event
		Function CanCopy() As Boolean
		  Return Me.SelectedRowCount > 0 And Self.Project.ReadOnly = False
		End Function
	#tag EndEvent
	#tag Event
		Function CanDelete() As Boolean
		  Return Me.SelectedRowCount > 0
		End Function
	#tag EndEvent
	#tag Event
		Function CanPaste(Board As Clipboard) As Boolean
		  Return Board.HasClipboardData(Self.kClipboardType)
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformClear(Warn As Boolean)
		  If Self.List.SelectedRowCount = 0 Then
		    Return
		  End If
		  
		  Var Overrides() As Ark.LootDropOverride
		  For I As Integer = 0 To Self.List.RowCount - 1
		    If Self.List.RowSelectedAt(I) = False Then
		      Continue
		    End If
		    
		    Overrides.Add(Self.List.RowTagAt(I))
		  Next
		  
		  If Warn And Self.ShowDeleteConfirmation(Overrides, "loot drop", "loot drops") = False Then
		    Return
		  End If
		  
		  Var Config As Ark.Configs.LootDrops = Self.Config(True)
		  For Each Override As Ark.LootDropOverride In Overrides
		    Config.Remove(Override)
		  Next
		  
		  Self.Modified = Self.Project.Modified
		  Self.UpdateContainerList()
		End Sub
	#tag EndEvent
	#tag Event
		Sub PerformCopy(Board As Clipboard)
		  If Me.CanCopy = False Then
		    Return
		  End If
		  
		  Var Dicts() As Dictionary
		  For I As Integer = 0 To Me.RowCount - 1
		    If Me.RowSelectedAt(I) Then
		      Var Override As Ark.LootDropOverride = Me.RowTagAt(I)
		      Dicts.Add(Override.SaveData)
		    End If
		  Next
		  
		  If Dicts.Count = 0 Then
		    System.Beep
		    Return
		  End If
		  
		  Board.AddClipboardData(Self.kClipboardType, Dicts)
		End Sub
	#tag EndEvent
	#tag Event
		Sub PerformPaste(Board As Clipboard)
		  Var Contents As Variant = Board.GetClipboardData(Self.kClipboardType)
		  If Contents.IsNull = False Then
		    Try
		      Var Dicts() As Variant = Contents
		      Var Overrides() As Ark.LootDropOverride
		      For Each Dict As Dictionary In Dicts
		        Var Override As Ark.LootDropOverride = Ark.LootDropOverride.FromSaveData(Dict)
		        If Override Is Nil Then
		          Continue
		        End If
		        Overrides.Add(Override)
		      Next
		      Self.AddOverrides(Overrides)
		    Catch Err As RuntimeException
		      Self.ShowAlert("There was an error with the pasted content.", "The content is not formatted correctly.")
		    End Try
		    Return
		  End If
		End Sub
	#tag EndEvent
	#tag Event
		Sub SelectionChanged()
		  Var DuplicateButton As OmniBarItem = Self.ConfigToolbar.Item("DuplicateButton")
		  If (DuplicateButton Is Nil) = False Then
		    DuplicateButton.Enabled = Me.SelectedRowCount = 1
		  End If
		  
		  If Self.mBlockSelectionChanged Then
		    Return
		  End If
		  
		  Var Overrides() As Ark.LootDropOverride
		  For I As Integer = 0 To Me.RowCount - 1
		    If Me.RowSelectedAt(I) Then
		      Overrides.Add(Me.RowTagAt(I))
		    End If
		  Next
		  
		  Editor.Overrides = Overrides
		  If Overrides.Count = 0 Then
		    Panel.SelectedPanelIndex = 0
		  Else
		    Panel.SelectedPanelIndex = 1
		  End If
		  
		  Self.UpdateStatus()
		End Sub
	#tag EndEvent
	#tag Event
		Function CanEdit() As Boolean
		  Return Me.SelectedRowCount = 1
		End Function
	#tag EndEvent
	#tag Event
		Sub PerformEdit()
		  For I As Integer = 0 To Me.RowCount - 1
		    If Not Me.RowSelectedAt(I) Then
		      Continue
		    End If
		    
		    Var Config As Ark.Configs.LootDrops = Self.Config(False)
		    If ArkAddLootDropDialog.Present(Self, Config, Self.Project.MapMask, Self.Project.ContentPacks, Me.RowTagAt(I)) Then
		      Call Self.Config(True) // Actually saves the config to the document
		      Self.UpdateContainerList()
		      Self.Modified = True
		    End If
		    
		    Return
		  Next
		End Sub
	#tag EndEvent
	#tag Event
		Sub Opening()
		  Me.TypeaheadColumn = 1
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events Editor
	#tag Event
		Sub Updated()
		  Var Config As Ark.Configs.LootDrops = Self.Config(True)
		  Var Overrides() As Ark.LootDropOverride = Me.Overrides
		  Var Map As New Dictionary
		  For Each Override As Ark.LootDropOverride In Overrides
		    Config.Add(Override)
		    Map.Value(Override.LootDropId) = Override
		  Next
		  For RowIdx As Integer = 0 To Self.List.LastRowIndex
		    Var Override As Ark.LootDropOverride = Self.List.RowTagAt(RowIdx)
		    If Map.HasKey(Override.LootDropId) = False Then
		      Continue For RowIdx
		    End If
		    
		    Self.List.RowTagAt(RowIdx) = Map.Value(Override.LootDropId)
		  Next RowIdx
		  
		  Self.Modified = Self.Config(False).Modified
		End Sub
	#tag EndEvent
	#tag Event
		Function GetProject() As Ark.Project
		  Return Self.Project
		End Function
	#tag EndEvent
	#tag Event
		Sub PresentDropEditor(Override As Ark.LootDropOverride)
		  Var Config As Ark.Configs.LootDrops = Self.Config(False)
		  If ArkAddLootDropDialog.Present(Self, Config, Self.Project.MapMask, Self.Project.ContentPacks, Override) Then
		    Call Self.Config(True) // Actually saves the config to the document
		    Self.UpdateContainerList()
		    Self.Modified = True
		  End If
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events ConfigToolbar
	#tag Event
		Sub Opening()
		  Me.Append(OmniBarItem.CreateTitle("Title", Self.ConfigLabel))
		  Me.Append(OmniBarItem.CreateSeparator("TitleSeparator"))
		  Me.Append(OmniBarItem.CreateButton("AddContainerButton", "New Drop", IconToolbarAddMenu, "Define an additional loot drop override. Hold to quickly add a container from a menu."))
		  Me.Append(OmniBarItem.CreateButton("DuplicateButton", "Duplicate", IconToolbarClone, "Duplicate the selected loot drop.", False))
		  Me.Append(OmniBarItem.CreateButton("RebuildButton", "Rebuild", IconToolbarRebuild, "Rebuild all item sets using their templates.", False))
		  Me.Append(OmniBarItem.CreateFlexibleSpace)
		  Me.Append(OmniBarItem.CreateHorizontalResizer("Resizer"))
		  
		  Me.Item("Title").Priority = 5
		  Me.Item("TitleSeparator").Priority = 5
		End Sub
	#tag EndEvent
	#tag Event
		Sub ItemPressed(Item As OmniBarItem, ItemRect As Rect)
		  #Pragma Unused ItemRect
		  
		  Select Case Item.Name
		  Case "AddContainerButton"
		    Self.ShowAddLootContainer()
		  Case "DuplicateButton"
		    Self.ShowAddLootContainer(True)
		  Case "RebuildButton"
		    Self.RebuildAllItemSets()
		  End Select
		End Sub
	#tag EndEvent
	#tag Event
		Sub Resize(DraggedResizer As OmniBarItem, DeltaX As Integer, DeltaY As Integer)
		  #Pragma Unused DraggedResizer
		  #Pragma Unused DeltaY
		  
		  Var NewWidth As Integer = Me.Width + DeltaX
		  Self.SetListWidth(NewWidth)
		End Sub
	#tag EndEvent
	#tag Event
		Function ItemHeld(Item As OmniBarItem, ItemRect As Rect) As Boolean
		  Select Case Item.Name
		  Case "AddContainerButton"
		    Var Base As New DesktopMenuItem
		    Self.BuildQuickDropMenu(Base)
		    
		    Var Position As Point = Me.GlobalPosition
		    Var Choice As DesktopMenuItem = Base.PopUp(Position.X + ItemRect.Left, Position.Y + ItemRect.Bottom)
		    If (Choice Is Nil) = False Then
		      Call Self.HandleQuickDropMenu(Choice)
		    End If
		    Return True
		  End Select
		End Function
	#tag EndEvent
#tag EndEvents
#tag Events FilterField
	#tag Event
		Sub TextChanged()
		  If Self.SettingUp Then
		    Return
		  End If
		  
		  Self.UpdateContainerList()
		End Sub
	#tag EndEvent
#tag EndEvents
#tag ViewBehavior
	#tag ViewProperty
		Name="Modified"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Composited"
		Visible=true
		Group="Window Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Index"
		Visible=true
		Group="ID"
		InitialValue="-2147483648"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="IsFrontmost"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="ViewTitle"
		Visible=true
		Group="Behavior"
		InitialValue="Untitled"
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="ViewIcon"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Progress"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Double"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Tooltip"
		Visible=true
		Group="Appearance"
		InitialValue=""
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowAutoDeactivate"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocusRing"
		Visible=true
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="BackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="&hFFFFFF"
		Type="ColorGroup"
		EditorType="ColorGroup"
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasBackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocus"
		Visible=true
		Group="Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowTabs"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimumWidth"
		Visible=true
		Group="Behavior"
		InitialValue="400"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimumHeight"
		Visible=true
		Group="Behavior"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Backdrop"
		Visible=true
		Group="Background"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Enabled"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Height"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="InitialParent"
		Visible=false
		Group="Position"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Left"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockBottom"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockLeft"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockRight"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockTop"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Name"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Super"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabIndex"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabPanelIndex"
		Visible=false
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabStop"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Top"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Transparent"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Visible"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Width"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
#tag EndViewBehavior
