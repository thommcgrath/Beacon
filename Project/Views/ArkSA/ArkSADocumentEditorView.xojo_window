#tag DesktopWindow
Begin DocumentEditorView ArkSADocumentEditorView
   AllowAutoDeactivate=   True
   AllowFocus      =   False
   AllowFocusRing  =   False
   AllowTabs       =   True
   Backdrop        =   0
   BackgroundColor =   &cFFFFFF00
   Composited      =   False
   DoubleBuffer    =   "True"
   Enabled         =   True
   EraseBackground =   "True"
   HasBackgroundColor=   False
   Height          =   528
   Index           =   -2147483648
   InitialParent   =   ""
   Left            =   0
   LockBottom      =   True
   LockLeft        =   True
   LockRight       =   True
   LockTop         =   True
   TabIndex        =   0
   TabPanelIndex   =   0
   TabStop         =   True
   Tooltip         =   ""
   Top             =   0
   Transparent     =   True
   Visible         =   True
   Width           =   858
   Begin DesktopPagePanel PagePanel1
      AllowAutoDeactivate=   True
      Enabled         =   True
      Height          =   487
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   231
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   True
      LockTop         =   True
      PanelCount      =   2
      Panels          =   ""
      Scope           =   2
      SelectedPanelIndex=   0
      TabIndex        =   3
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   41
      Transparent     =   False
      Value           =   0
      Visible         =   True
      Width           =   627
      Begin OmniNoticeBar OmniNoticeBanner
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   True
         AllowTabs       =   False
         Backdrop        =   0
         ContentHeight   =   0
         Enabled         =   True
         Height          =   31
         Index           =   -2147483648
         InitialParent   =   "PagePanel1"
         Left            =   231
         LockBottom      =   False
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Scope           =   2
         ScrollActive    =   False
         ScrollingEnabled=   False
         ScrollSpeed     =   20
         TabIndex        =   0
         TabPanelIndex   =   2
         TabStop         =   True
         Tooltip         =   ""
         Top             =   41
         Transparent     =   True
         Visible         =   False
         Width           =   627
      End
      Begin LogoFillCanvas LogoFillCanvas1
         AllowAutoDeactivate=   True
         AllowFocus      =   False
         AllowFocusRing  =   True
         AllowTabs       =   False
         Backdrop        =   0
         Caption         =   "There was an error loading the editor"
         ContentHeight   =   0
         Enabled         =   True
         Height          =   487
         Index           =   -2147483648
         InitialParent   =   "PagePanel1"
         Left            =   231
         LockBottom      =   True
         LockedInPosition=   False
         LockLeft        =   True
         LockRight       =   True
         LockTop         =   True
         Scope           =   2
         ScrollActive    =   False
         ScrollingEnabled=   False
         ScrollSpeed     =   20
         TabIndex        =   0
         TabPanelIndex   =   1
         TabStop         =   True
         Tooltip         =   ""
         Top             =   41
         Transparent     =   True
         Visible         =   True
         Width           =   627
      End
   End
   Begin FadedSeparator SourceSeparator
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   487
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   230
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   2
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   41
      Transparent     =   True
      Visible         =   True
      Width           =   1
   End
   Begin OmniBar OmniBar1
      Alignment       =   0
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      BackgroundColor =   ""
      ContentHeight   =   0
      Enabled         =   True
      Height          =   41
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LeftPadding     =   -1
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   True
      LockTop         =   True
      RightPadding    =   -1
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   0
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   0
      Transparent     =   True
      Visible         =   True
      Width           =   858
   End
   Begin SourceList ConfigList
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   False
      AllowTabs       =   True
      Backdrop        =   0
      BackgroundColor =   &cFFFFFF00
      Composited      =   False
      Enabled         =   True
      HasBackgroundColor=   False
      Height          =   446
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LockBottom      =   True
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      SelectedRowIndex=   0
      TabIndex        =   6
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   82
      Transparent     =   True
      Visible         =   True
      Width           =   230
   End
   Begin ConfigSetPicker ConfigSetPicker
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   40
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   1
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   41
      Transparent     =   True
      Visible         =   True
      Width           =   230
   End
   Begin FadedSeparator FadedSeparator2
      AllowAutoDeactivate=   True
      AllowFocus      =   False
      AllowFocusRing  =   True
      AllowTabs       =   False
      Backdrop        =   0
      ContentHeight   =   0
      Enabled         =   True
      Height          =   1
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   0
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      ScrollActive    =   False
      ScrollingEnabled=   False
      ScrollSpeed     =   20
      TabIndex        =   5
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   81
      Transparent     =   True
      Visible         =   True
      Width           =   230
   End
   Begin Thread GFIComputeThread
      DebugIdentifier =   ""
      Index           =   -2147483648
      LockedInPosition=   False
      Priority        =   5
      Scope           =   2
      StackSize       =   0
      TabPanelIndex   =   0
      ThreadID        =   0
      ThreadState     =   0
   End
End
#tag EndDesktopWindow

#tag WindowCode
	#tag Event
		Sub Closing()
		  If (Self.mImportWindow Is Nil) = False Then
		    Self.CleanupImportWindow()
		    Self.mImportWindow.Close
		    Self.mImportWindow = Nil
		  End If
		  
		  If (Self.Project Is Nil) = False Then
		    ArkSA.DeactivateBlueprintProvider(Self.Project)
		  End If
		End Sub
	#tag EndEvent

	#tag Event
		Sub EnableMenuItems()
		  If Self.ReadyToDeploy Then
		    FileDeploy.Enabled = True
		  End If
		  
		  If Self.ReadyToExport Then
		    FileExport.Enabled = True
		  End If
		  
		  If Self.Project.ActiveConfigSet.IsBase = False Then
		    ViewSwitchToBaseConfigSet.Enabled = True
		  End If
		  
		  If Self.CurrentPanel <> Nil Then
		    Self.CurrentPanel.EnableMenuItems()
		  End If
		End Sub
	#tag EndEvent

	#tag Event
		Sub FirstShow()
		  Self.MigrateSinglePlayer()
		End Sub
	#tag EndEvent

	#tag Event
		Sub Hidden()
		  Var Panel As ArkSAConfigEditor = Self.CurrentPanel
		  If (Panel Is Nil) = False Then
		    Panel.SwitchedFrom()
		  End If
		  
		  ArkSA.DeactivateBlueprintProvider(Self.Project)
		End Sub
	#tag EndEvent

	#tag Event
		Sub IdentityChanged()
		  // Simply toggle the menu to force a redraw
		  Var CurrentConfig As String = Self.CurrentConfigName
		  Self.CurrentConfigName = ""
		  Self.CurrentConfigName = CurrentConfig
		End Sub
	#tag EndEvent

	#tag Event
		Sub Opening()
		  If (Self.Project Is Nil) = False Then
		    ArkSA.ActivateBlueprintProvider(Self.Project)
		    Var ProjectId As String = Self.Project.ProjectId
		    Var LastConfigName As String = Preferences.ProjectState(ProjectId, "Editor", "")
		    Var LastConfigSet As Beacon.ConfigSet = Self.Project.FindConfigSet(Preferences.ProjectState(ProjectId, "Config Set", "").StringValue)
		    If LastConfigName.IsEmpty Or LastConfigSet Is Nil Then
		      If Self.URL.Type.OneOf(Beacon.ProjectURL.TypeWeb, Beacon.ProjectURL.TypeCommunity) Then
		        LastConfigName = ArkSA.Configs.NameProjectSettings
		      Else
		        LastConfigName = ArkSA.Configs.NameLootDrops
		      End If
		      LastConfigSet = Beacon.ConfigSet.BaseConfigSet
		    End If
		    Self.ActiveConfigSet = LastConfigSet
		    Self.CurrentConfigName = LastConfigName
		  End If
		End Sub
	#tag EndEvent

	#tag Event
		Sub RoleChanged()
		  Self.MigrateSinglePlayer()
		End Sub
	#tag EndEvent

	#tag Event
		Sub RunScriptAction(Action As Beacon.ScriptAction)
		  Select Case Action.Action
		  Case "Deploy"
		    Var Plan As Beacon.DeployPlan = Beacon.DeployPlan.StopUploadStart
		    Try
		      Var PlanString As String = Action.Value("Plan")
		      If PlanString.IsEmpty = False Then
		        Plan = CType(Integer.FromString(PlanString, Locale.Raw), Beacon.DeployPlan)
		      End If
		    Catch Err As RuntimeException
		    End Try
		    
		    Var Settings As New Beacon.DeploySettings
		    Settings.Options = UInt64.FromString(Action.Value("Options"), Locale.Raw)
		    Settings.StopMessage = Action.Value("StopMessage")
		    Settings.Plan = Plan
		    Var ProfileIds() As String = Action.Value("Servers").Split(",")
		    For Each ProfileId As String In ProfileIds
		      Var Profile As Beacon.ServerProfile = Self.Project.ServerProfile(ProfileId)
		      If (Profile Is Nil) = False Then
		        Settings.Servers.Add(Profile)
		      End If
		    Next
		    
		    Self.BeginDeploy(Settings)
		  End Select
		End Sub
	#tag EndEvent

	#tag Event
		Sub ShouldSave(CloseWhenFinished As Boolean)
		  #Pragma Unused CloseWhenFinished
		  
		  If Self.mUpdateUITag <> "" Then
		    CallLater.Cancel(Self.mUpdateUITag)
		    Self.mUpdateUITag = ""
		    Self.UpdateUI()
		  End If
		End Sub
	#tag EndEvent

	#tag Event
		Sub Shown(UserData As Variant = Nil)
		  ArkSA.ActivateBlueprintProvider(Self.Project)
		  
		  Var Panel As ArkSAConfigEditor = Self.CurrentPanel
		  If (Panel Is Nil) = False Then
		    Panel.SwitchedTo(UserData)
		  End If
		End Sub
	#tag EndEvent

	#tag Event
		Sub SwitchToEditor(EditorName As String)
		  Self.CurrentConfigName = EditorName
		End Sub
	#tag EndEvent


	#tag MenuHandler
		Function FileDeploy() As Boolean Handles FileDeploy.Action
		  If Self.IsFrontmost = False Then
		    Return False
		  End If
		  
		  Self.BeginDeploy()
		  Return True
		  
		End Function
	#tag EndMenuHandler

	#tag MenuHandler
		Function FileExport() As Boolean Handles FileExport.Action
		  If Self.IsFrontmost = False Then
		    Return False
		  End If
		  
		  Self.BeginExport()
		  Return True
		  
		End Function
	#tag EndMenuHandler

	#tag MenuHandler
		Function ViewSwitchToBaseConfigSet() As Boolean Handles ViewSwitchToBaseConfigSet.Action
		  If Self.IsFrontmost = False Then
		    Return False
		  End If
		  
		  Self.ActiveConfigSet = Beacon.ConfigSet.BaseConfigSet
		  Return True
		End Function
	#tag EndMenuHandler


	#tag Method, Flags = &h0
		Sub ActiveConfigSet(Assigns Value As Beacon.ConfigSet)
		  Var ConfigName As String = Self.CurrentConfigName
		  If Value.IsBase = False And ArkSA.Configs.SupportsConfigSets(ConfigName) = False Then
		    // If switching from base and on an editor that won't exist in the desired set, switch to something else
		    ConfigName = ArkSA.Configs.NameLootDrops
		  End If
		  Self.CurrentConfigName = "" // To unload the current version
		  
		  Self.Project.ActiveConfigSet = Value
		  Self.ConfigSetPicker.Refresh
		  Self.UpdateConfigList
		  
		  If (Self.Project Is Nil) = False And Self.Controller.URL.Type <> Beacon.ProjectURL.TypeTransient Then
		    Preferences.ProjectState(Self.Project.ProjectId, "Config Set") = Value.Name
		  End If
		  
		  Self.CurrentConfigName = ConfigName
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub BeginDeploy()
		  Self.BeginDeploy(Preferences.NewDeploySettings)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub BeginDeploy(Settings As Beacon.DeploySettings)
		  If Self.mDeployWindow <> Nil And Self.mDeployWindow.Value <> Nil And Self.mDeployWindow.Value IsA DeployManager Then
		    DeployManager(Self.mDeployWindow.Value).BringToFront()
		  Else
		    If Self.Project.ReadOnly Then
		      Self.ShowAlert("This is a read-only project", "Your access to this project does not allow deploy.")
		      Return
		    End If
		    
		    Self.Autosave()
		    
		    If Not Self.ReadyToDeploy Then
		      Self.ShowAlert("This project is not ready for deploy.", "You must import at least one server into this project to use the deploy feature. Use the Import button in the top left.") 
		      Return
		    End If
		    
		    If Not Self.ContinueWithoutExcludedConfigs() Then
		      Return
		    End If
		    
		    If Self.mValidator Is Nil And (Self.mValidationResultsDialog Is Nil Or Self.mValidationResultsDialog.Value Is Nil) Then
		      Var Validator As New Beacon.ProjectValidator
		      AddHandler Validator.Validating, WeakAddressOf mValidator_Validating
		      AddHandler Validator.ValidationComplete, WeakAddressOf mValidator_ValidationComplete_Deploy
		      Validator.StartValidation(Self.Project, Settings)
		      Self.mValidator = Validator
		    End If
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub BeginDeploy(PreselectServers() As Beacon.ServerProfile)
		  Var Settings As Beacon.DeploySettings = Preferences.NewDeploySettings
		  Settings.Servers = PreselectServers
		  Self.BeginDeploy(Settings)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub BeginExport()
		  If Self.Project.ReadOnly Then
		    Self.ShowAlert("This is a read-only project", "Your access to this project does not allow export.")
		    Return
		  End If
		  
		  Self.Autosave()
		  
		  If Not Self.ContinueWithoutExcludedConfigs() Then
		    Return
		  End If
		  
		  If Self.mValidator Is Nil And (Self.mValidationResultsDialog Is Nil Or Self.mValidationResultsDialog.Value Is Nil) Then
		    Var Validator As New Beacon.ProjectValidator
		    AddHandler Validator.Validating, WeakAddressOf mValidator_Validating
		    AddHandler Validator.ValidationComplete, WeakAddressOf mValidator_ValidationComplete_Export
		    Validator.StartValidation(Self.Project)
		    Self.mValidator = Validator
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub BeginImport(ForDeployment As Boolean)
		  If (Self.mImportWindow Is Nil) = False Then
		    Self.mImportWindow.Show()
		    Return
		  End If
		  
		  Var ProjectId As String = Self.Project.ProjectId
		  Var OtherEditors() As DocumentEditorView = Self.EditorsForGameId(ArkSA.Identifier, Ark.Identifier)
		  Var OtherProjects() As Beacon.Project
		  For Each Editor As DocumentEditorView In OtherEditors
		    Var OtherProject As Beacon.Project = Editor.Project
		    If (OtherProject Is Nil) = False And OtherProject.ReadOnly = False And OtherProject.ProjectId <> ProjectId Then
		      OtherProjects.Add(OtherProject)
		    End If
		  Next
		  
		  Var ImportView As New ArkSAImportView
		  Var Ref As New DocumentImportWindow(ImportView, Self.Project, OtherProjects)
		  AddHandler Ref.ProjectsImported, WeakAddressOf mImportWindow_ProjectsImported
		  AddHandler Ref.Closing, WeakAddressOf mImportWindow_Closing
		  Ref.UserData = ForDeployment
		  Ref.Show()
		  Self.mImportWindow = Ref
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub CleanupImportWindow()
		  If Self.mImportWindow Is Nil Then
		    Return
		  End If
		  
		  Try
		    RemoveHandler mImportWindow.Closing, WeakAddressOf mImportWindow_Closing
		  Catch Err As RuntimeException
		  End Try
		  
		  Try
		    RemoveHandler mImportWindow.ProjectsImported, WeakAddressOf mImportWindow_ProjectsImported
		  Catch Err As RuntimeException
		  End Try
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ComputeGFICodes()
		  If Self.ShowConfirm("Computing GFI codes will take a few minutes to complete.", "If you continue, you will be asked to save a CSV file with the results, then the process will start.", "Continue", "Cancel") = False Then
		    Return
		  End If
		  
		  Var SaveDialog As New SaveFileDialog
		  SaveDialog.Filter = BeaconFileTypes.CSVFile
		  SaveDialog.SuggestedFileName = "Ark GFI Codes " + DateTime.Now.SimpleSQLDate + ".csv"
		  
		  Var OutputFile As FolderItem = SaveDialog.ShowModal(Self.TrueWindow)
		  If OutputFile Is Nil Then
		    Return
		  End If
		  
		  Self.mGFIOutputFile = OutputFile
		  Self.mGFIProgress = New ProgressWindow
		  Self.mGFIProgress.ShowDelayed(Self.TrueWindow)
		  
		  Self.GFIComputeThread.Start
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Function ConfirmClose() As Boolean
		  If Self.Progress <> BeaconSubview.ProgressNone Then
		    Self.RequestFrontmost()
		    
		    Self.ShowAlert(Self.ViewTitle + " cannot be closed right now because it is busy.", "Wait for the progress indicator at the top of the tab to go away before trying to close it.")
		    Return False
		  End If
		  
		  Return Super.ConfirmClose()
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub Constructor(Controller As Beacon.ProjectController)
		  Self.Panels = New Dictionary
		  
		  Super.Constructor(Controller)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function ContinueWithoutExcludedConfigs() As Boolean
		  Var ExcludedConfigs() As ArkSA.ConfigGroup = Self.Project.UsesOmniFeaturesWithoutOmni(App.IdentityManager.CurrentIdentity)
		  If ExcludedConfigs.LastIndex = -1 Then
		    Return True
		  End If
		  
		  Var HumanNames() As String
		  For Each Config As ArkSA.ConfigGroup In ExcludedConfigs
		    HumanNames.Add("""" + Language.LabelForConfig(Config) + """")
		  Next
		  HumanNames.Sort
		  
		  Var Message, Explanation As String
		  If HumanNames.Count = 1 Then
		    Message = Self.OmniWarningSingularMessage
		    Explanation = Language.ReplacePlaceholders(Self.OmniWarningSingularExplanation, HumanNames(0), Language.GameName(ArkSA.Identifier))
		  Else
		    Message = Self.OmniWarningPluralMessage
		    Explanation = Language.ReplacePlaceholders(Self.OmniWarningPluralExplanation, HumanNames.EnglishOxfordList(), Language.GameName(ArkSA.Identifier))
		  End If
		  
		  Return Self.ShowConfirm(Message, Explanation, "Continue", "Cancel")
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub CopyFromDocuments(SourceProjects As Variant)
		  Var Projects() As Beacon.Project
		  Try
		    Projects = SourceProjects
		  Catch Err As RuntimeException
		  End Try
		  DocumentMergerWindow.Present(Self, Projects, Self.Project, WeakAddressOf MergeCallback)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub CopyFromDocumentsAndDeploy(SourceProjects As Variant)
		  Var Projects() As Beacon.Project
		  Try
		    Projects = SourceProjects
		  Catch Err As RuntimeException
		  End Try
		  DocumentMergerWindow.Present(Self, Projects, Self.Project, WeakAddressOf MergeAndDeployCallback)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub CurrentConfigName(Assigns Value As String)
		  If Self.CurrentConfigName = Value Then
		    Return
		  End If
		  
		  If (Value = ArkSA.Configs.NameAccounts Or Value = ArkSA.Configs.NameServers Or Value = ArkSA.Configs.NameProjectSettings) And Self.ActiveConfigSet.IsBase = False Then
		    Self.ActiveConfigSet = Beacon.ConfigSet.BaseConfigSet
		  End If
		  
		  Super.CurrentConfigName = Value
		  
		  Var NewPanel As ArkSAConfigEditor
		  Var Embed As Boolean
		  If Value.IsEmpty = False Then
		    Var CacheKey As String = Self.ActiveConfigSet.ConfigSetId + ":" + Value
		    
		    If (Self.Project Is Nil) = False And Self.Controller.URL.Type <> Beacon.ProjectURL.TypeTransient Then
		      Preferences.ProjectState(Self.Project.ProjectId, "Editor") = Value
		    End If
		    
		    Var HistoryIndex As Integer = Self.mPanelHistory.IndexOf(CacheKey)
		    If HistoryIndex > 0 Then
		      Self.mPanelHistory.RemoveAt(HistoryIndex)
		    End If
		    Self.mPanelHistory.AddAt(0, CacheKey)
		    
		    // Close older panels
		    If Self.mPanelHistory.LastIndex > 2 Then
		      For I As Integer = Self.mPanelHistory.LastIndex DownTo 3
		        Var PanelTag As String = Self.mPanelHistory(I)
		        Self.DiscardConfigPanel(PanelTag)
		      Next
		    End If
		    
		    If Self.Panels.HasKey(CacheKey) Then
		      NewPanel = Self.Panels.Value(CacheKey)
		    Else
		      Select Case Value
		      Case ArkSA.Configs.NameServers
		        NewPanel = New ArkSAServersEditor(Self.Project)
		      Case ArkSA.Configs.NameAccounts
		        NewPanel = New ArkSAAccountsEditor(Self.Project)
		      Case ArkSA.Configs.NameProjectSettings
		        NewPanel = New ArkSAProjectSettingsEditor(Self.Project)
		      Case ArkSA.Configs.NameLootDrops
		        NewPanel = New ArkSALootDropsEditor(Self.Project)
		      Case ArkSA.Configs.NameDifficulty
		        NewPanel = New ArkSADifficultyEditor(Self.Project)
		      Case ArkSA.Configs.NameLevelsAndXP
		        NewPanel = New ArkSAExperienceEditor(Self.Project)
		      Case ArkSA.Configs.NameCustomConfig
		        NewPanel = New ArkSACustomConfigEditor(Self.Project)
		      Case ArkSA.Configs.NameCraftingCosts
		        NewPanel = New ArkSACraftingCostsEditor(Self.Project)
		      Case ArkSA.Configs.NameStackSizes
		        NewPanel = New ArkSAStackSizesEditor(Self.Project)
		      Case ArkSA.Configs.NameBreedingMultipliers
		        NewPanel = New ArkSABreedingMultipliersEditor(Self.Project)
		      Case ArkSA.Configs.NameHarvestRates
		        NewPanel = New ArkSAHarvestRatesEditor(Self.Project)
		      Case ArkSA.Configs.NameCreatureAdjustments
		        NewPanel = New ArkSADinoAdjustmentsEditor(Self.Project)
		      Case ArkSA.Configs.NameStatMultipliers
		        NewPanel = New ArkSAStatMultipliersEditor(Self.Project)
		      Case ArkSA.Configs.NameDayCycle
		        NewPanel = New ArkSADayCycleEditor(Self.Project)
		      Case ArkSA.Configs.NameCreatureSpawns
		        NewPanel = New ArkSACreatureSpawnsEditor(Self.Project)
		      Case ArkSA.Configs.NameStatLimits
		        NewPanel = New ArkSAStatLimitsEditor(Self.Project)
		      Case ArkSA.Configs.NameEngramControl
		        NewPanel = New ArkSAEngramControlEditor(Self.Project)
		      Case ArkSA.Configs.NameDecayAndSpoil
		        NewPanel = New ArkSASpoilTimersEditor(Self.Project)
		      Case ArkSA.Configs.NameGeneralSettings
		        NewPanel = New ArkSAGeneralSettingsEditor(Self.Project)
		      End Select
		      If NewPanel <> Nil Then
		        Self.Panels.Value(CacheKey) = NewPanel
		        Embed = True
		      End If
		    End If
		  End If
		  
		  If Self.CurrentPanel = NewPanel Then
		    Return
		  End If
		  
		  If (Self.CurrentPanel Is Nil) = False Then
		    Self.CurrentPanel.SwitchedFrom()
		    Self.CurrentPanel.Visible = False
		    Self.CurrentPanel = Nil
		  End If
		  
		  Self.CurrentPanel = NewPanel
		  
		  If (Self.CurrentPanel Is Nil) = False Then
		    Var RequiresPurchase As Boolean
		    If Value.Length > 0 Then
		      RequiresPurchase = Not ArkSA.Configs.ConfigUnlocked(Value, App.IdentityManager.CurrentIdentity)
		    End If
		    Var TopOffset As Integer
		    If RequiresPurchase Then
		      TopOffset = (Self.OmniNoticeBanner.Top + Self.OmniNoticeBanner.Height) - Self.PagePanel1.Top
		    End If
		    If Embed Then
		      AddHandler Self.CurrentPanel.ContentsChanged, WeakAddressOf Panel_ContentsChanged
		      If Self.CurrentPanel IsA ArkSAServersEditor Then
		        AddHandler ArkSAServersEditor(Self.CurrentPanel).ShouldDeployProfiles, WeakAddressOf ServersEditor_ShouldDeployProfiles
		      End If
		      Self.CurrentPanel.EmbedWithinPanel(Self.PagePanel1, 1, 0, TopOffset, Self.PagePanel1.Width, Self.PagePanel1.Height - TopOffset)
		    Else
		      Self.CurrentPanel.Top = Self.PagePanel1.Top + TopOffset
		      Self.CurrentPanel.Height = Self.PagePanel1.Height - TopOffset
		    End If
		    Self.OmniNoticeBanner.Visible = RequiresPurchase
		    Self.CurrentPanel.Visible = True
		    Self.CurrentPanel.SwitchedTo()
		    Self.PagePanel1.SelectedPanelIndex = 1
		  Else
		    Self.PagePanel1.SelectedPanelIndex = 0
		  End If
		  
		  Self.ConfigList.SelectedTag = Value
		  
		  Self.UpdateMinimumDimensions()
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub Destructor()
		  If (Self.mMapsPopoverController Is Nil) = False Then
		    RemoveHandler mMapsPopoverController.Finished, WeakAddressOf MapsPopoverController_Finished
		    Self.mModsPopoverController.Dismiss(True)
		    Self.mMapsPopoverController = Nil
		  End If
		  
		  If (Self.mModsPopoverController Is Nil) = False Then
		    RemoveHandler mModsPopoverController.Finished, WeakAddressOf ModsPopoverController_Finished
		    Self.mModsPopoverController.Dismiss(True)
		    Self.mModsPopoverController = Nil
		  End If
		  
		  If (Self.mValidationResultsDialog Is Nil) = False And (Self.mValidationResultsDialog.Value Is Nil) = False Then
		    ResolveIssuesDialog(Self.mValidationResultsDialog.Value).Close
		  End If
		  
		  If (Self.mDeployWindow Is Nil) = False And (Self.mDeployWindow.Value Is Nil) = False Then
		    DeployManager(Self.mDeployWindow.Value).Close
		  End If
		  
		  Super.Destructor()
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub DiscardConfigPanel(CacheKey As String)
		  // This is indiscriminate and will close the current config if you ask it to.
		  
		  If Not Self.Panels.HasKey(CacheKey) Then
		    Return
		  End If
		  
		  Var Panel As ArkSAConfigEditor = Self.Panels.Value(CacheKey)
		  If Panel IsA ArkSAServersEditor Then
		    RemoveHandler ArkSAServersEditor(Panel).ShouldDeployProfiles, WeakAddressOf ServersEditor_ShouldDeployProfiles
		  End If
		  RemoveHandler Panel.ContentsChanged, WeakAddressOf Panel_ContentsChanged
		  Panel.Close
		  Self.Panels.Remove(CacheKey)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub MapsPopoverController_Finished(Sender As PopoverController, Cancelled As Boolean)
		  If Not Cancelled Then
		    Self.Project.MapMask = ArkSA.Maps.MaskForMaps(MapSelectionGrid(Sender.Container).CheckedMaps)
		    Self.Modified = Self.Project.Modified
		  End If
		  
		  If (Self.CurrentPanel Is Nil) = False Then
		    Self.CurrentPanel.SetupUI()
		  End If
		  
		  Self.OmniBar1.Item("MapsButton").Toggled = False
		  Self.mMapsPopoverController = Nil
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub MergeAndDeployCallback()
		  Self.MergeCallback()
		  Self.BeginDeploy()
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub MergeCallback()
		  Var Keys() As Variant = Self.Panels.Keys
		  For Each Key As Variant In Keys
		    Var Panel As ArkSAConfigEditor = Self.Panels.Value(Key)
		    If Panel <> Nil Then
		      Panel.ImportFinished()
		    End If
		  Next
		  
		  Self.Autosave()
		  Self.UpdateConfigList()
		  Self.Panel_ContentsChanged(Nil)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub MigrateSinglePlayer()
		  If Self.Project.ReadOnly Or Self.mSinglePlayerMigrated = True Then
		    Return
		  End If
		  
		  Self.mSinglePlayerMigrated = True
		  
		  If Self.Project.LastSavedVersion >= 20302000 Then
		    Return
		  End If
		  
		  Var OtherSettings As Beacon.ConfigGroup = Self.Project.ConfigGroup(ArkSA.Configs.NameGeneralSettings, Beacon.ConfigSet.BaseConfigSet, False)
		  If (OtherSettings Is Nil) = False And OtherSettings IsA ArkSA.Configs.OtherSettings Then
		    Var Option As ArkSA.ConfigOption = ArkSA.DataSource.Pool.Get(False).GetConfigOption(ArkSA.ConfigFileGame, ArkSA.HeaderShooterGame, "bUseSingleplayerSettings")
		    If (Option Is Nil) = False Then
		      Var GeneralSettings As ArkSA.Configs.OtherSettings = ArkSA.Configs.OtherSettings(OtherSettings)
		      Var Value As Variant = GeneralSettings.Value(Option)
		      If Value.Type = Variant.TypeBoolean Then
		        Self.Project.IsFlagged(ArkSA.Project.FlagSinglePlayer) = Value.BooleanValue
		        Return
		      End If
		    End If
		  End If
		  
		  Call CallLater.Schedule(100, AddressOf MigrateSinglePlayerWitUI)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub MigrateSinglePlayerWitUI()
		  Var Multiplayer As Boolean = Self.ShowConfirm(Self.SinglePlayerMessage, Self.SinglePlayerExplanation, Self.SinglePlayerActionCaption, Self.SinglePlayerCancelCaption)
		  Self.Project.IsFlagged(ArkSA.Project.FlagSinglePlayer) = Not Multiplayer
		  Self.mSinglePlayerMigrated = True
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub mImportWindow_Closing(Sender As DocumentImportWindow)
		  #Pragma Unused Sender
		  
		  Self.CleanupImportWindow()
		  Self.mImportWindow = Nil
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub mImportWindow_ProjectsImported(Sender As DocumentImportWindow, Projects() As Beacon.Project)
		  #Pragma Unused Sender
		  
		  If Sender.UserData.BooleanValue Then
		    Call CallLater.Schedule(0, WeakAddressOf CopyFromDocumentsAndDeploy, Projects)
		  Else
		    Call CallLater.Schedule(0, WeakAddressOf CopyFromDocuments, Projects)
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ModsPopoverController_Finished(Sender As PopoverController, Cancelled As Boolean)
		  If Not Cancelled Then
		    Var Editor As ModSelectionGrid = ModSelectionGrid(Sender.Container)
		    Var ContentPackIds() As String = Editor.ContentPackIds
		    For Each ContentPackId As String In ContentPackIds
		      Self.Project.ContentPackEnabled(ContentPackId) = Editor.ModEnabled(ContentPackId)
		    Next
		    
		    Self.Project.MigrateModContent(ArkSA.DataSource.Pool)
		    Self.Modified = Self.Project.Modified
		  End If
		  
		  If (Self.CurrentPanel Is Nil) = False Then
		    Self.CurrentPanel.SetupUI()
		  End If
		  
		  Self.OmniBar1.Item("ModsButton").Toggled = False
		  Self.mModsPopoverController = Nil
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub mValidationResultsDialog_GoToIssue(Sender As ResolveIssuesDialog, Issue As Beacon.Issue)
		  Sender.Close
		  
		  Select Case Issue.Location
		  Case "MapMask"
		    Self.ShowMapPicker()
		    Return
		  End Select
		  
		  Var Parts() As String = Issue.Location.Split(Beacon.Issue.Separator)
		  If Parts.Count < 3 Then
		    Break
		    App.Log("Unknown issue path " + Issue.Location)
		    Return
		  End If
		  
		  Var ConfigSet As String = Parts(0)
		  Var ConfigName As String = Parts(1)
		  Self.ActiveConfigSet = Self.Project.FindConfigSet(ConfigSet)
		  Self.CurrentConfigName = ConfigName
		  Self.CurrentPanel.GoToIssue(Issue)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub mValidator_Validating(Sender As Beacon.ProjectValidator)
		  #Pragma Unused Sender
		  
		  Var Progress As New ProgressWindow
		  Progress.Message = "Checking project for errors…"
		  Progress.Detail = "Just a moment…"
		  Progress.Show
		  Self.mValidatorProgress = Progress
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function mValidator_ValidationComplete_Common(Sender As Beacon.ProjectValidator, Results As Beacon.ProjectValidationResults, UserData As Variant) As Boolean
		  #Pragma Unused Sender
		  #Pragma Unused UserData
		  
		  Self.mValidator = Nil
		  If (Self.mValidatorProgress Is Nil) = False Then
		    Self.mValidatorProgress.Close
		    Self.mValidatorProgress = Nil
		  End If
		  
		  If Results.Count > 0 Then
		    Var Dialog As New ResolveIssuesDialog(Results)
		    AddHandler Dialog.GoToIssue, WeakAddressOf mValidationResultsDialog_GoToIssue
		    Dialog.Show(Self)
		    Self.mValidationResultsDialog = New WeakRef(Dialog)
		    Return False
		  End If
		  
		  Return True
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub mValidator_ValidationComplete_Deploy(Sender As Beacon.ProjectValidator, Results As Beacon.ProjectValidationResults, UserData As Variant)
		  If Self.mValidator_ValidationComplete_Common(Sender, Results, UserData) = False Then
		    Return
		  End If
		  
		  Var Settings As Beacon.DeploySettings
		  Try
		    Settings = UserData
		  Catch Err As RuntimeException
		    App.Log(Err, CurrentMethodName, "Casting UserData into PreselectServers")
		  End Try
		  Var Win As DeployManager = New DeployManager(Self.Controller, Settings)
		  Self.mDeployWindow = New WeakRef(Win)
		  Win.BringToFront()
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub mValidator_ValidationComplete_Export(Sender As Beacon.ProjectValidator, Results As Beacon.ProjectValidationResults, UserData As Variant)
		  If Self.mValidator_ValidationComplete_Common(Sender, Results, UserData) = False Then
		    Return
		  End If
		  
		  ArkSAExportWindow.Present(Self, Self.Project, Keyboard.AsyncOptionKey And Keyboard.AsyncShiftKey)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub ObservedValueChanged(Source As ObservationKit.Observable, Key As String, OldValue As Variant, NewValue As Variant)
		  // Part of the ObservationKit.Observer interface.
		  
		  Select Case Key
		  Case "MinimumWidth", "MinimumHeight"
		    Self.UpdateMinimumDimensions()
		  Else
		    Super.ObservedValueChanged(Source, Key, OldValue, NewValue)
		  End Select
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub Panel_ContentsChanged(Sender As ArkSAConfigEditor)
		  #Pragma Unused Sender
		  
		  If Self.Modified <> Self.Project.Modified Then
		    Self.Modified = Self.Project.Modified
		  End If
		  
		  If Self.mUpdateUITag <> "" Then
		    CallLater.Cancel(Self.mUpdateUITag)
		    Self.mUpdateUITag = ""
		  End If
		  
		  Self.mUpdateUITag = CallLater.Schedule(500, WeakAddressOf UpdateUI)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Function Project() As ArkSA.Project
		  Return ArkSA.Project(Super.Project())
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub PruneUnknownContent()
		  PruningWindow.Present(Self, Self.Project, AddressOf PruningFinished)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub PruningFinished()
		  Self.Modified = Project.Modified
		  If (Self.CurrentPanel Is Nil) = False Then
		    Self.CurrentPanel.SetupUI()
		  End If
		  Self.ShowAlert(Self.PruningFinishedMessage, Self.PruningFinishedExplanation)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function ReadyToDeploy() As Boolean
		  If Self.Project Is Nil Or Self.Project.ServerProfileCount = 0 Then
		    Return False
		  End If
		  
		  Var Bound As Integer = Self.Project.ServerProfileCount - 1
		  For I As Integer = 0 To Bound
		    If Self.Project.ServerProfile(I) <> Nil And Self.Project.ServerProfile(I).DeployCapable Then
		      Return True
		    End If
		  Next
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function ReadyToExport() As Boolean
		  Return Self.Project <> Nil
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub RestoreEditor(ConfigName As String)
		  Var Label As String = Language.LabelForConfig(ConfigName)
		  If Self.ShowConfirm("Are you sure you want to restore """ + Label + """ to default settings?", "Wherever possible, this will remove the config options from your file completely, restoring settings to Ark's default values. You cannot undo this action.", "Restore", "Cancel") Then
		    Var IsSelected As Boolean = Self.CurrentConfigName = ConfigName
		    
		    If IsSelected Then
		      Self.CurrentConfigName = ""
		    End If
		    
		    Self.Project.RemoveConfigGroup(ConfigName)
		    
		    Var CacheKey As String = Self.ActiveConfigSet.ConfigSetId + ":" + ConfigName
		    Self.DiscardConfigPanel(CacheKey)
		    
		    If IsSelected Then
		      Self.CurrentConfigName = ConfigName
		    End If
		    
		    Self.Modified = True
		    Self.UpdateConfigList()
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub RunTool(ToolId As String)
		  Select Case ToolId
		  Case "6bcf5785-b8e2-4889-91c8-545c34f30d8a"
		    Self.ComputeGFICodes()
		  Case "56742123-8e17-4285-b196-fd79823b61f7"
		    Self.PruneUnknownContent()
		  End Select
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ServersEditor_ShouldDeployProfiles(Sender As ArkSAServersEditor, SelectedProfiles() As Beacon.ServerProfile)
		  #Pragma Unused Sender
		  Self.BeginDeploy(SelectedProfiles)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub ShowMapPicker()
		  Var Item As OmniBarItem = Self.OmniBar1.Item("MapsButton")
		  If Item Is Nil Then
		    Return
		  End If
		  
		  Var ItemRect As Rect = Self.OmniBar1.RectForItem(Item)
		  Self.ShowMapPicker(Item, ItemRect)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ShowMapPicker(Item As OmniBarItem, ItemRect As Rect)
		  If (Self.mMapsPopoverController Is Nil) = False And Self.mMapsPopoverController.Visible Then
		    Self.mMapsPopoverController.Dismiss(False)
		    Self.mMapsPopoverController = Nil
		    Item.Toggled = False
		    Return
		  End If
		  
		  Var Editor As New MapSelectionGrid(ArkSA.Maps.All)
		  Var Controller As New PopoverController("Select Maps", Editor)
		  Editor.SetWithMaps(ArkSA.Maps.ForMask(Self.Project.MapMask))
		  Controller.Show(Self.OmniBar1, ItemRect)
		  
		  Item.Toggled = True
		  
		  AddHandler Controller.Finished, WeakAddressOf MapsPopoverController_Finished
		  Self.mMapsPopoverController = Controller
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateConfigList()
		  Var Labels(), Tags() As String
		  
		  Var ActiveConfigSet As Beacon.ConfigSet = Self.ActiveConfigSet
		  Var IsBase As Boolean = ActiveConfigSet.IsBase
		  If IsBase Then
		    Var PsuedoTags() As String = Array(ArkSA.Configs.NameProjectSettings, ArkSA.Configs.NameAccounts)
		    // Show everything
		    #if DeployEnabled
		      PsuedoTags.Add(ArkSA.Configs.NameServers)
		    #endif
		    For Each Tag As String In PsuedoTags
		      Labels.Add(Language.LabelForConfig(Tag))
		      Tags.Add(Tag)
		    Next Tag
		  End If
		  
		  Var Names() As String = ArkSA.Configs.AllNames
		  For Each Name As String In Names
		    Labels.Add(Language.LabelForConfig(Name))
		    Tags.Add(Name)
		  Next
		  
		  Labels.SortWith(Tags)
		  
		  Var SourceItems() As SourceListItem
		  For I As Integer = 0 To Labels.LastIndex
		    Var Item As New SourceListItem(Labels(I), Tags(I))
		    Var SupportsConfigSets As Boolean = ArkSA.Configs.SupportsConfigSets(Tags(I))
		    If IsBase = False And SupportsConfigSets = False Then
		      Continue
		    End If
		    Item.CanDismiss = SupportsConfigSets And Self.Project.HasConfigGroup(Tags(I)) = True And Self.Project.ConfigGroup(Tags(I)).IsImplicit = False
		    SourceItems.Add(Item)
		  Next
		  
		  Self.ConfigList.ReplaceContents(SourceItems)
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateMinimumDimensions()
		  Self.MinimumWidth = If(Self.CurrentPanel <> Nil, Max(Self.CurrentPanel.MinimumWidth, Self.LocalMinWidth), Self.LocalMinWidth) + Self.PagePanel1.Left
		  Self.MinimumHeight = If(Self.CurrentPanel <> Nil, Max(Self.CurrentPanel.MinimumHeight, Self.LocalMinHeight), Self.LocalMinHeight) + Self.PagePanel1.Top
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub UpdateUI()
		  Self.ViewTitle = Self.Controller.Name
		  Self.UpdateConfigList()
		End Sub
	#tag EndMethod


	#tag Property, Flags = &h21
		Private CurrentPanel As ArkSAConfigEditor
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mDeployWindow As WeakRef
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mGFIOutputFile As FolderItem
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mGFIProgress As ProgressWindow
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mImportWindow As DocumentImportWindow
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mMapsPopoverController As PopoverController
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mModsPopoverController As PopoverController
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mPanelHistory() As String
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mSinglePlayerMigrated As Boolean
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mUpdateUITag As String
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mValidationResultsDialog As WeakRef
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mValidator As Beacon.ProjectValidator
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mValidatorProgress As ProgressWindow
	#tag EndProperty

	#tag Property, Flags = &h21
		Private Panels As Dictionary
	#tag EndProperty

	#tag ComputedProperty, Flags = &h21
		#tag Getter
			Get
			  Return (Self.OmniBar1.Item("BlueprintsButton") Is Nil) = False
			End Get
		#tag EndGetter
		#tag Setter
			Set
			  If Value = Self.ShowBlueprintsButton Then
			    Return
			  End If
			  
			  If Value Then
			    Self.OmniBar1.Append(OmniBarItem.CreateSpace("BlueprintsSpaceA"))
			    Self.OmniBar1.Append(OmniBarItem.CreateSeparator("BlueprintsSeparator"))
			    Self.OmniBar1.Append(OmniBarItem.CreateSpace("BlueprintsSpaceB"))
			    
			    Var BlueprintsButton As OmniBarItem = OmniBarItem.CreateButton("BlueprintsButton", "Save Blueprints", IconToolbarGift, "This project has embedded blueprints that you can save.")
			    BlueprintsButton.AlwaysUseActiveColor = True
			    BlueprintsButton.ActiveColor = OmniBarItem.ActiveColors.Green
			    Self.OmniBar1.Append(BlueprintsButton)
			  Else
			    Self.OmniBar1.Remove("BlueprintsButton")
			    Self.OmniBar1.Remove("BlueprintsSpaceA")
			    Self.OmniBar1.Remove("BlueprintsSeparator")
			    Self.OmniBar1.Remove("BlueprintsSpaceB")
			  End If
			End Set
		#tag EndSetter
		Private ShowBlueprintsButton As Boolean
	#tag EndComputedProperty


	#tag Constant, Name = DeployEnabled, Type = Boolean, Dynamic = False, Default = \"True", Scope = Private
	#tag EndConstant

	#tag Constant, Name = kConfigGroupClipboardType, Type = String, Dynamic = False, Default = \"com.thezaz.beacon.arksa.configgroup", Scope = Private
	#tag EndConstant

	#tag Constant, Name = PruningFinishedExplanation, Type = String, Dynamic = True, Default = \"Any object that is not in Beacon\'s game database has been removed from the project.", Scope = Private
	#tag EndConstant

	#tag Constant, Name = PruningFinishedMessage, Type = String, Dynamic = True, Default = \"The process has finished", Scope = Private
	#tag EndConstant

	#tag Constant, Name = SinglePlayerActionCaption, Type = String, Dynamic = True, Default = \"No\x2C Multi Player", Scope = Private
	#tag EndConstant

	#tag Constant, Name = SinglePlayerCancelCaption, Type = String, Dynamic = True, Default = \"Yes\x2C Single Player", Scope = Private
	#tag EndConstant

	#tag Constant, Name = SinglePlayerExplanation, Type = String, Dynamic = True, Default = \"Since you used this project last\x2C Beacon treats single player projects differently and was not able to determine if this is a single player project or not. This can be changed later in Project Settings.", Scope = Private
	#tag EndConstant

	#tag Constant, Name = SinglePlayerMessage, Type = String, Dynamic = True, Default = \"Is this a single player project\?", Scope = Private
	#tag EndConstant


#tag EndWindowCode

#tag Events OmniNoticeBanner
	#tag Event
		Sub Pressed()
		  System.GotoURL(Beacon.WebURL("/omni#ArkSA"))
		End Sub
	#tag EndEvent
	#tag Event
		Function GameName() As String
		  Return Language.GameName(ArkSA.Identifier)
		End Function
	#tag EndEvent
#tag EndEvents
#tag Events OmniBar1
	#tag Event
		Sub Opening()
		  Me.Append(OmniBarItem.CreateTitle("GameName", ArkSA.FullName))
		  Me.Append(OmniBarItem.CreateSeparator)
		  
		  Me.Append(OmniBarItem.CreateButton("ImportButton", "Import", IconToolbarImport, "Import config files."))
		  Me.Append(OmniBarItem.CreateSpace())
		  Me.Append(OmniBarItem.CreateButton("ExportButton", "Export", IconToolbarExport, "Save new config file."))
		  
		  #if DeployEnabled
		    Me.Append(OmniBarItem.CreateButton("DeployButton", "Deploy", IconToolbarDeploy, "Make config changes live."))
		  #endif
		  
		  Me.Append(OmniBarItem.CreateSpace())
		  Me.Append(OmniBarItem.CreateButton("ShareButton", "Share", IconToolbarShare, "Share this project with other users."))
		  Me.Append(OmniBarItem.CreateSpace())
		  
		  Me.Append(OmniBarItem.CreateSeparator())
		  Me.Append(OmniBarItem.CreateSpace())
		  Me.Append(OmniBarItem.CreateButton("MapsButton", "Maps", IconToolbarMaps, "Change the maps for this project."))
		  Me.Append(OmniBarItem.CreateButton("ModsButton", "Mods", IconToolbarMods, "Enable or disable Beacon's built-in mods."))
		  Me.Append(OmniBarItem.CreateButton("ToolsButton", "Tools", IconToolbarTools, "Use convenience tools for this project."))
		  
		  Self.ShowBlueprintsButton = Self.Project.HasUnsavedContent
		End Sub
	#tag EndEvent
	#tag Event
		Sub ItemPressed(Item As OmniBarItem, ItemRect As Rect)
		  Select Case Item.Name
		  Case "ImportButton"
		    Self.BeginImport(False)
		  Case "ExportButton"
		    Self.BeginExport()
		  Case "ShareButton"
		    If Self.URL.Type.OneOf(Beacon.ProjectURL.TypeCloud, Beacon.ProjectURL.TypeShared) Then
		      SharingDialog.Present(Self, Self.Project)
		      Self.Modified = Self.Project.Modified
		    ElseIf Self.URL.Type = Beacon.ProjectURL.TypeLocal Then
		      Self.ShowAlert("Project sharing is only available to cloud projects", "Use ""Save As…"" under the file menu to save a new copy of this project to the cloud if you would like to use Beacon's sharing features.")
		    Else
		      Self.ShowAlert("Project sharing is only available to cloud projects", "If you would like to use Beacon's sharing features, first save your project using ""Save"" under the file menu.")
		    End If
		  Case "DeployButton"
		    Self.BeginDeploy()
		  Case "MapsButton"
		    Self.ShowMapPicker(Item, ItemRect)
		  Case "ModsButton"
		    If (Self.mModsPopoverController Is Nil) = False And Self.mModsPopoverController.Visible Then
		      Self.mModsPopoverController.Dismiss(False)
		      Self.mModsPopoverController = Nil
		      Item.Toggled = False
		      Return
		    End If
		    
		    Var Editor As New ModSelectionGrid(Self.Project)
		    Var Controller As New PopoverController("Select Mods", Editor)
		    Controller.Show(Me, ItemRect)
		    
		    Item.Toggled = True
		    
		    AddHandler Controller.Finished, WeakAddressOf ModsPopoverController_Finished
		    Self.mModsPopoverController = Controller
		  Case "ToolsButton"
		    Var Tools() As ArkSA.ProjectTool = ArkSA.Configs.AllTools
		    Var GroupNames() As String
		    Var GroupedTools As New Dictionary
		    For Each Tool As ArkSA.ProjectTool In Tools
		      Var GroupName As String = Tool.FirstGroup
		      If GroupNames.IndexOf(GroupName) = -1 Then
		        GroupNames.Add(GroupName)
		      End If
		      
		      Var Siblings() As ArkSA.ProjectTool
		      If GroupedTools.HasKey(GroupName) Then
		        Siblings = GroupedTools.Value(GroupName)
		        Siblings.Add(Tool)
		      Else
		        Siblings.Add(Tool)
		        GroupedTools.Value(GroupName) = Siblings
		      End If
		    Next
		    GroupNames.Sort
		    
		    Var Base As New DesktopMenuItem
		    For Each GroupName As String In GroupNames
		      If GroupName.IsEmpty = False Then
		        If Base.Count > 0 Then
		          Base.AddMenu(New DesktopMenuItem(DesktopMenuItem.TextSeparator))
		        End If
		        
		        Var Header As New DesktopMenuItem(Language.LabelForConfig(GroupName))
		        Header.Enabled = False
		        Base.AddMenu(Header)
		      End If
		      
		      Var GroupTools() As ArkSA.ProjectTool = GroupedTools.Value(GroupName)
		      Var ToolNames() As String
		      For Each Tool As ArkSA.ProjectTool In GroupTools
		        ToolNames.Add(Tool.Caption)
		      Next
		      ToolNames.SortWith(GroupTools)
		      
		      For Each Tool As ArkSA.ProjectTool In GroupTools
		        Base.AddMenu(New DesktopMenuItem(Tool.Caption, Tool))
		      Next
		    Next
		    
		    Var Position As Point = Me.GlobalPosition
		    Var Choice As DesktopMenuItem = Base.PopUp(Position.X + ItemRect.Left, Position.Y + ItemRect.Bottom)
		    If Choice Is Nil Then
		      Return
		    End If
		    
		    Var Tool As ArkSA.ProjectTool = Choice.Tag
		    If Tool.IsGlobal Then
		      Self.RunTool(Tool.UUID)
		    Else
		      If Tool.IsRelevantForGroup(Self.CurrentConfigName) = False Then
		        Self.CurrentConfigName = Tool.FirstGroup
		      End If
		      Call Self.CurrentPanel.RunTool(Tool.UUID)
		    End If
		    Self.UpdateConfigList()
		  Case "BlueprintsButton"
		    Self.Project.ProcessEmbeddedContent()
		    If Self.Project.HasUnsavedContent = False Then
		      Self.ShowAlert("Sorry, Beacon made a mistake", "Looks like there are no more blueprints in this project that are missing from your Mods section. This button will be hidden.")
		      Self.ShowBlueprintsButton = False
		      Return
		    End If
		    
		    ArkSASaveBlueprintsDialog.Present(Self, Self.Project)
		    Self.ShowBlueprintsButton = Self.Project.HasUnsavedContent
		  End Select
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events ConfigList
	#tag Event
		Sub Change()
		  Var TagVar As Variant
		  If Me.SelectedRowIndex > -1 Then
		    TagVar = Me.Item(Me.SelectedRowIndex).Tag
		  End If
		  If IsNull(TagVar) = False And (TagVar.Type = Variant.TypeString Or TagVar.Type = Variant.TypeText) Then
		    Self.CurrentConfigName = TagVar.StringValue
		  Else
		    Self.CurrentConfigName = ""
		  End If
		End Sub
	#tag EndEvent
	#tag Event
		Sub Opening()
		  Self.UpdateConfigList()
		End Sub
	#tag EndEvent
	#tag Event
		Function ShouldChange(DesiredIndex As Integer) As Boolean
		  #Pragma Unused DesiredIndex
		  
		  Var CurrentItem As SourceListItem = Me.SelectedItem
		  If (CurrentItem Is Nil) = False Then
		    Try
		      Var GroupName As String = CurrentItem.Tag
		      CurrentItem.CanDismiss = Self.Project.HasConfigGroup(GroupName) = True And Self.Project.ConfigGroup(GroupName).IsImplicit = False
		    Catch Err As RuntimeException
		    End Try
		  End If
		  
		  Return True
		End Function
	#tag EndEvent
	#tag Event
		Sub DismissPressed(Item As SourceListItem, ItemIndex As Integer, ItemRect As Rect)
		  #Pragma Unused ItemIndex
		  #Pragma Unused ItemRect
		  
		  Self.RestoreEditor(Item.Tag)
		End Sub
	#tag EndEvent
	#tag Event
		Sub ContextualClick(MouseX As Integer, MouseY As Integer, ItemIndex As Integer, ItemRect As Rect)
		  Const RestoreTag = "b4d7f3d8-17f2-425f-8ab8-9032d558b29d"
		  Const CopyTag = "a0b7a0ee-518a-4ee8-a33c-5c8e46ba570f"
		  Const PasteTag = "31f1decc-7706-4baf-af11-f4d4fdde799d"
		  Const HelpTag = "f3766fd7-7483-446f-8fa9-47dd0dd09209"
		  
		  Var Base As New DesktopMenuItem
		  Var ReadOnly As Boolean = Self.Project.ReadOnly
		  Var Item As SourceListItem
		  Var ConfigName As String
		  Var Config As ArkSA.ConfigGroup
		  If ItemIndex > -1 Then
		    Item = Me.Item(ItemIndex)
		    ConfigName = Item.Tag
		    Config = Self.Project.ConfigGroup(ConfigName, False)
		    
		    Var HelpItem As New DesktopMenuItem(Item.Caption + " Help", HelpTag)
		    HelpItem.Enabled = True
		    Base.AddMenu(HelpItem)
		    Base.AddMenu(New DesktopMenuItem(DesktopMenuItem.TextSeparator))
		  End If
		  
		  Var CopyItem As New DesktopMenuItem("Copy", CopyTag)
		  CopyItem.Enabled = ReadOnly = False And (ItemIndex > -1) And (Config Is Nil) = False And Config.IsImplicit = False
		  Base.AddMenu(CopyItem)
		  Var Board As New Clipboard
		  Var PasteItem As New DesktopMenuItem("Paste", PasteTag)
		  PasteItem.Enabled = Board.RawDataAvailable(Self.kConfigGroupClipboardType) And (ItemIndex = -1 Or Board.RawData(Self.kConfigGroupClipboardType).IndexOf("""GroupName"":""" + ConfigName + """") > -1)
		  Base.AddMenu(PasteItem)
		  
		  If ItemIndex > -1 Then
		    Base.AddMenu(New DesktopMenuItem(DesktopMenuItem.TextSeparator))
		    Var Tools() As ArkSA.ProjectTool = ArkSA.Configs.AllTools
		    For Each Tool As ArkSA.ProjectTool In Tools
		      If Tool.IsRelevantForGroup(ConfigName) Then
		        Base.AddMenu(New DesktopMenuItem(Tool.Caption, Tool.UUID))
		      End If
		    Next
		    If Base.Count > 0 Then
		      Base.AddMenu(New DesktopMenuItem(DesktopMenuItem.TextSeparator))
		    End If
		    Base.AddMenu(New DesktopMenuItem("Restore """ + Item.Caption + """ to Default", RestoreTag))
		  End If
		  
		  Var Position As Point = Me.GlobalPosition
		  Var Choice As DesktopMenuItem
		  If ItemRect Is Nil Then
		    Choice = Base.PopUp(Position.X + MouseX, Position.Y + MouseY)
		  Else
		    Choice = Base.PopUp(Position.X + ItemRect.Left, Position.Y + ItemRect.Bottom)
		  End If
		  If Choice Is Nil Then
		    Return
		  End If
		  
		  Select Case Choice.Tag
		  Case HelpTag
		    Var HelpPath As String = Beacon.ConfigHelpPath(ConfigName)
		    Var HelpUrl As String = Beacon.HelpUrl(HelpPath)
		    
		    Var Component As HelpComponent = App.MainWindow.Help(False)
		    If Component Is Nil Then
		      System.GotoURL(HelpUrl)
		      Return
		    End If
		    App.MainWindow.ShowHelp()
		    Component.LoadURL(HelpUrl)
		  Case RestoreTag
		    Self.RestoreEditor(ConfigName)
		  Case CopyTag
		    If (Config Is Nil) = False And ReadOnly = False Then
		      Var SaveData As New Dictionary
		      SaveData.Value("GroupName") = ConfigName
		      SaveData.Value("SaveData") = Config.SaveData()
		      Var JSON As String = Beacon.GenerateJSON(SaveData, False)
		      Board.RawData(Self.kConfigGroupClipboardType) = JSON
		    End If
		  Case PasteTag
		    Try
		      Var Parsed As Dictionary = Beacon.ParseJSON(Board.RawData(Self.kConfigGroupClipboardType))
		      Var NewConfigName As String = Parsed.Value("GroupName")
		      Var NewConfigData As Dictionary = Parsed.Value("SaveData")
		      Var NewConfig As ArkSA.ConfigGroup = ArkSA.Configs.CreateInstance(NewConfigName, NewConfigData, Nil)
		      Self.Project.AddConfigGroup(NewConfig)
		      Self.UpdateConfigList()
		      Self.Modified = Self.Project.Modified
		      
		      If Me.SelectedRowIndex = ItemIndex Or (ItemIndex = -1 And Me.SelectedRowIndex > -1 And Me.Item(Me.SelectedRowIndex).Tag = NewConfigName) Then
		        // Refresh
		        Self.CurrentConfigName = ""
		        Self.CurrentConfigName = NewConfigName
		      End If
		    Catch Err As RuntimeException
		      App.Log(Err, CurrentMethodName, "Working with pasted data")
		      App.Log("Pasted data: " + EncodeBase64(Board.RawData(Self.kConfigGroupClipboardType), 0))
		      Self.ShowAlert("Beacon was unable to complete the paste.", "The error was '" + Err.Message + "'. More data may be available in the log files.")
		    End Try
		  Else
		    If Me.SelectedRowIndex <> ItemIndex Then
		      Me.SelectedRowIndex = ItemIndex
		    End If
		    
		    Call Self.CurrentPanel.RunTool(Choice.Tag)
		    Self.UpdateConfigList()
		  End Select
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events ConfigSetPicker
	#tag Event
		Function GetProject() As Beacon.Project
		  Return Self.Project
		End Function
	#tag EndEvent
	#tag Event
		Sub ManageConfigSets()
		  Self.ManageConfigSets()
		End Sub
	#tag EndEvent
	#tag Event
		Sub NewConfigSet()
		  Self.NewConfigSet()
		End Sub
	#tag EndEvent
	#tag Event
		Sub Changed(Set As Beacon.ConfigSet)
		  Self.ActiveConfigSet = Set
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events GFIComputeThread
	#tag Event
		Sub Run()
		  Var Result As New Dictionary("Finished": True)
		  
		  Var DataSource As ArkSA.DataSource = ArkSA.DataSource.Pool.Get(False)
		  Var Lines() As String = DataSource.ComputeGFICodes(Self.Project.ContentPacks, Self.mGFIProgress)
		  If Lines.Count > 0 Then
		    Var Stream As TextOutputStream = TextOutputStream.Create(Self.mGFIOutputFile)
		    Stream.Write(String.FromArray(Lines, EndOfLine))
		    Stream.Close
		    
		    Result.Value("Success") = True
		  End If
		  
		  Me.AddUserInterfaceUpdate(Result)
		End Sub
	#tag EndEvent
	#tag Event
		Sub UserInterfaceUpdate(data() as Dictionary)
		  For Each Dict As Dictionary In Data
		    If Dict.Lookup("Finished", False) = True Then
		      Self.mGFIProgress.Close
		    End If
		    If Dict.Lookup("Success", False) = True Then
		      Self.ShowAlert("GFI codes are ready.", "See the results in " + Self.mGFIOutputFile.NativePath)
		    End If
		  Next
		End Sub
	#tag EndEvent
#tag EndEvents
#tag ViewBehavior
	#tag ViewProperty
		Name="Modified"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Composited"
		Visible=true
		Group="Window Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Index"
		Visible=true
		Group="ID"
		InitialValue="-2147483648"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="IsFrontmost"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="ViewTitle"
		Visible=true
		Group="Behavior"
		InitialValue="Untitled"
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="ViewIcon"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Progress"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="Double"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Tooltip"
		Visible=true
		Group="Appearance"
		InitialValue=""
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowAutoDeactivate"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocusRing"
		Visible=true
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="BackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="&hFFFFFF"
		Type="ColorGroup"
		EditorType="ColorGroup"
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasBackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowFocus"
		Visible=true
		Group="Behavior"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="AllowTabs"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimumWidth"
		Visible=true
		Group="Behavior"
		InitialValue="400"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimumHeight"
		Visible=true
		Group="Behavior"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Name"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Super"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Width"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Height"
		Visible=true
		Group="Size"
		InitialValue="300"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="InitialParent"
		Visible=false
		Group="Position"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Left"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Top"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockLeft"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockTop"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockRight"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="LockBottom"
		Visible=true
		Group="Position"
		InitialValue=""
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabPanelIndex"
		Visible=false
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabIndex"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="TabStop"
		Visible=true
		Group="Position"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Visible"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Enabled"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Backdrop"
		Visible=true
		Group="Background"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Transparent"
		Visible=true
		Group="Behavior"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
#tag EndViewBehavior
